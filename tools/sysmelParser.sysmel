Compiler importModulesNamed: #(
    #"Sysmel.Compiler.SysmelLanguage"
    #"Sysmel.Tools.LanguageServerProtocol"
).

useNamespace: Sysmel Compiler Parsing.
useNamespace: Sysmel Compiler SysmelLanguage.
useNamespace: Sysmel Tools LanguageServerProtocol.

function parseAndVerifySourceCode(fileName: Stdn ImmutableString, content: Stdn String const ref) => Void := {
    let sourceCode := SourceCode sharedNew.
    sourceCode _
        content: content;
        name: fileName.

    let tokenizedSourceCode := Scanner() scan: sourceCode.
    tokenizedSourceCode _ tokens do: {:each :: Void |
        each type isError ifTrue: {
            Stdn stderr << each position << ": tokenization error: " << each type; nl
        }.
    }.

    let ast := Parser() parse: tokenizedSourceCode.
    ASTParseErrorCheckerVisitor verify: ast _ callback: {:parserErrorNode :: Void |
        Stdn stderr << parserErrorNode sourcePosition << ": " << parserErrorNode errorMessage; nl
    }.
}.

function startLanguageServerOnStdio() => Void := {
    let server mutable := LanguageServer().
    server
        setForStdio;
        run
}.

Stdn MainEntryPoint: {:args :: Int32 |
    for: (let i mutable type: UIntPointer := 1) while: (i < args size) do: {
        let arg := args[i].

        arg selectCase: #{
        "-" : {
            let contents := Stdn stdin readUntilEnd.
            parseAndVerifySourceCode("stdin", contents)
        }.
        "-lang-server" : {
            let endpointName := args[i := i + 1].
            endpointName = "stdio" ifTrue: {
                startLanguageServerOnStdio()
            } ifFalse: {
                Stdn stderr << "Unsupported language server protocol endpoint."; nl.
                return: 1
            }
        }.
        _ : {
            let contents := arg asNativeFileReference utf8Contents.
            contents ifNil: {
                Stdn stderr << "Failed to read content of file named " << arg; nl.
                return: 1
            }.

            parseAndVerifySourceCode(arg, contents _)
        }.
        }
    } continueWith: (i := i + 1).

    0
}.
