namespace SysmelKernel definition: {

struct GCFunctionStackMapEntry definition: {
    field stackFrameSize public type: UInt32.
    field _ public type: UInt32.

    field safePointCount public type: UInt32.
    field rootCount public type: UInt32.

    field compiledMethod public type: UIntPointer. ## Can be null

    ## Safe points
    method safePoints => UIntPointer pointer
        := (self address [1]) address reinterpretCastTo: UIntPointer pointer.

    ## The root count with extra padding, if needed.
    if: UIntPointer instanceSize == 4 then: {
        method paddedRootCount => UInt32
            := rootCount.
    } else: {
        method paddedRootCount => UInt32
            := (rootCount & 1) == 0
                ifTrue: {rootCount}
                ifFalse: {rootCount + 1}.
    }.

    ## Roots
    method roots => Int32 pointer
        := self safePoints[safePointCount] address reinterpretCastTo: Int32 pointer.

    ## Next stack map entry address.
    method next => GCFunctionStackMapEntry pointer
        := self roots[self paddedRootCount] address reinterpretCastTo: GCFunctionStackMapEntry pointer.
}.

global __sysmel_gc_section_start external externC mutable type: GCFunctionStackMapEntry.
global __sysmel_gc_section_end external externC mutable type: GCFunctionStackMapEntry.

class SysmelGC definition: {
    field memoryHeap mutable type: Stdn NativeMemoryHeap.
    method printStackMap => Void := {
        <nogc>

        let start := __sysmel_gc_section_start address.
        let end := __sysmel_gc_section_end address.
        Stdn stdout << "Stack map start " << start << " end " << end; nl.

        for: (let position mutable := start) while: (position ~~ end) do: {
            Stdn stdout << "position " << position << " roots " << position _ rootCount << " safePoints " << position _ safePointCount; nl.
        } continueWith: (position := position _ next).
    }.

    method allocate: (objectSize: UIntPointer)
        variableDataSize: (variableDataSize: UInt32)
        initializingWith: (basicInitializer: Stdn Reflection BasicInitializeFunctionPointerType)
          ::=> Stdn Reflection GCObjectHeader pointer := {
        <nogc>

        ##self printStackMap.

        ## Allocate the object memory.
        let allocatedObject := (memoryHeap allocate: objectSize) reinterpretCastTo: Stdn Reflection GCObjectHeader pointer.
        LibC memset(allocatedObject, 0, objectSize).

        ## Set the allocated object class.
        allocatedObject value
            __variableDataSize: variableDataSize.

        ## Perform the basic initialization. This sets the vtable pointers.
        basicInitializer _ (allocatedObject).

        return: allocatedObject
    }.
}.

global globalSysmelGC mutable type: SysmelGC.

}.

#**
 * This is the entry point for a sysmel image.
 *#
function __sylsif_entryPoint externC(sysmelImageMetadata: Void pointer, argc: Int32, argv: Char8 const pointer pointer) => Int32 := {
    LibC printf("__sylsif_entryPoint executed. Image metadata: %p\n", sysmelImageMetadata).
    return: 0.
}.
