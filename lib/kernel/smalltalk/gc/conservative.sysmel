namespace SysmelKernel definition: {

Compiler addLinkerArguments: #("-lgc").

namespace BoehmGC definition: {
    function GC_init externC() => Void.
    function GC_malloc externC(size: UIntPointer) => Void pointer.
}.

class SysmelGC definition: {
    method initialize => Void := {
        <nogc>
        BoehmGC GC_init().
    }.

    method finalize => Void := {
        <nogc>
    }.

    method allocate: (objectSize: UIntPointer)
        variableDataSize: (variableDataSize: UIntPointer)
        initializingWith: (basicInitializer: NStd Reflection BasicInitializeFunctionPointerType)
          ::=> NStd Reflection GCObjectHeader pointer := {
        <nogc>

        ## Allocate the object memory.
        let allocatedObject := BoehmGC GC_malloc(objectSize) reinterpretCastTo: NStd Reflection GCObjectHeader pointer.
        NStd memset(allocatedObject, 0, objectSize).

        ## Set the allocated object class.
        allocatedObject value
            __variableDataSize: (variableDataSize castTo: UInt32).

        ## Perform the basic initialization. This sets the vtable pointers.
        basicInitializer _ (allocatedObject).

        return: allocatedObject
    }.

	method allocate: (instanceSize: UIntPointer) instanceAlignment: (instanceAligment: UIntPointer)
		copyingFromPrototype: (prototype: Void pointer)
		  ::=> NStd Reflection GCObjectHeader pointer := {
		<nogc>

        ## Allocate the object memory.
        let allocatedObject := BoehmGC GC_malloc(instanceSize) reinterpretCastTo: NStd Reflection GCObjectHeader pointer.

		## Copy the data from the prototype
        NStd memcpy(allocatedObject, prototype, instanceSize).

        return: allocatedObject
	}.
}.

global globalSysmelGC mutable type: SysmelGC; constructionPriority: Compiler compilationTarget gcInitializationPriority.

}.
