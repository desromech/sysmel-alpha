namespace SysmelKernel definition: {

alias _BasicInitializeFunctionPointerType := ((Void pointer) => Void) pointer.

/**
 * _ObjectHeader
 * The header an object.
 */
struct _ObjectHeader definition: {
    // Pointer towards the main virtual table. Objects that implement interfaces
    // can have additional vtables.
    field __vtable public type: Void pointer.

    if: UIntPointer instanceSize == 4 then: {
        // TODO: Support big-endian.
        field __padding public type: Behavior.
    }.

    // 32 bits: gc color, isPinned, isImmutable.
    field _ public bits: 5; type: UInt32.

    field __gcBits public type: UInt32; bits: 3.
    field __isPinned public type: UInt32; bits: 1.
    field __isImmutable public type: UInt32; bits: 1.
    field __identityHash public type: UInt32; bits: 22.

    // Variable data size
    field __variableDataSize public type: UInt32.
}.

class SysmelGC definition: {
    field memoryHeap mutable type: StdNative NativeMemoryHeap.

    message allocate: (objectSize: UIntPointer)
        variableDataSize: (variableDataSize: UInt32)
        initializingWith: (basicInitializer: _BasicInitializeFunctionPointerType)
          ::=> _ObjectHeader pointer := {

        // Allocate the object memory.
        let allocatedObject := (memoryHeap allocate: objectSize) reinterpretCastTo: _ObjectHeader pointer.
        LibC memset(allocatedObject, 0, objectSize).

        // Set the allocated object class.
        allocatedObject value
            __variableDataSize: variableDataSize.
        basicInitializer _ (allocatedObject).

        return: allocatedObject
    }.
}.

global globalSysmelGC mutable type: SysmelGC.

}.
