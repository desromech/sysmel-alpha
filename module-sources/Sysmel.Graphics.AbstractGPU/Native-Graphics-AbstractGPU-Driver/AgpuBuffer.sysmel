namespace Stdn definition: {
namespace Graphics definition: {
namespace DriverAGPU definition: {

useNamespace: Stdn Graphics Core.

#**
 * I am a agpu based device.
 *#
class AgpuBuffer superclass: Buffer; definition: {
    public field handle type: AGPU BufferRef.
    public field creationDescription type: BufferDescription.

    const override method description => BufferDescription
        := creationDescription.

    const override method stride => UIntPointer
        := creationDescription stride.

    const override method size => UIntPointer
        := creationDescription size.

    override method map: (access: MappingAccessMask) ::=> Void pointer := {
        handle _ mapBuffer: (access castTo: AGPU MappingAccess)
    }.

    override method unmap ::=> Void := {
        handle _ unmapBuffer
    }.

    override method uploadAt: (offset: UIntPointer) size: (size: UIntPointer) data: (data: Void const pointer) ::=> Void := {
        handle _ uploadBufferData: (offset castTo: UInt32) size: (size castTo: UInt32) data: (data reinterpretCastTo: Void pointer)
    }.

    override method readAt: (offset: UIntPointer) size: (size: UIntPointer) into: (buffer: Void pointer) ::=> Void := {
        handle _ readBufferData: (offset castTo: UInt32) size: (size castTo: UInt32) data: buffer
    }
}.

compileTime constant AgpuBufferPtr := AgpuBuffer sharedPointer.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Stdn
