namespace Std definition: {
namespace Serialization definition: {
namespace MessagePack definition: {

useNamespace: Std Serialization DOM.

Encoder extend: {
    method encodeString: (value: Std String const ref) ::=> Void := {
        self encodeStringSize: (value size castTo: UInt32).
        value isNotEmpty ifTrue: {
            self writeBytes: value size from: value data
        }.
    }.

    method encodeByteVector: (value: Std ByteVector const ref) ::=> Void := {
        self encodeByteArraySize: (value size castTo: UInt32).
        value isNotEmpty ifTrue: {
            self writeBytes: value size from: value data
        }.
    }.

    method encodeValue: (value: Value const ref) ::=> Void := {
        value selectCase: #{
        (let _ type: _UndefinedType) : (self encodeNil).
        (let x type: Boolean8) : (self encodeBoolean: x).
        (let x type: Int64) : (self encodeInteger: x).
        (let x type: UInt64) : (self encodeInteger: x).
        (let x type: Float64) : (self encodeFloat: x).
        (let x type: Std String) : (self encodeString: x).
        (let x type: Std ByteVector) : (self encodeByteVector: x).
        (let list type: ListRef) : {
            self encodeArraySize: (list size castTo: UInt32).
            list do: {:(DOM Value const ref)each :: Void |
                self encodeValue: each
            }
        }.
        (let object type: ObjectRef) : {
            self encodeMapSize: (object size castTo: UInt32).
            object keysAndValuesDo: {:(Std String const ref)key :(DOM Value const ref)value :: Void |
                self encodeString: key.
                self encodeValue: value
            }
        }.
        }.
    }.

    meta definition: {
        static method encodeValue: (value: Value const ref) onStream: (out: Std IO Stream ref) ::=> Void := {
            let outTextStreamWriter mutable := Std IO TextStreamWriter for: out address.
            Encoder()
                beginEncodingIntoStream: out address;
                encodeValue: value
        }.
    }.

}.

}. ## End of namespace MessagePack
}. ## End of namespace Serialization
}. ## End of namespace Std
