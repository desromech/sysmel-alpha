namespace Std definition: {
namespace Serialization definition: {
namespace JSON definition: {

useNamespace: Std Serialization DOM.

Writer definition: {

    method putValue: (value: Value const ref) ::=> Void := {
        value selectCase: #{
        (let _ type: _UndefinedType) : (self putNull).
        (let x type: Boolean8) : (self putBoolean: x).
        (let x type: Int64) : (self putInteger: x).
        (let x type: UInt64) : (self putInteger: x).
        (let x type: Float64) : (self putFloat: x).
        (let x type: Std String) : (self putString: x).
        (let x type: Std ByteVector) : (self putString: Std Serialization BinaryEncoding encodeBase64String(x)).
        (let list type: ListRef) : {
            self beginList.
            let isFirst mutable := true.
            list do: {:(Value const ref)el :: Void |
                isFirst ifTrue: {
                    isFirst := false
                } ifFalse: {
                    self putElementSeparator
                }.
                self putValue: el.
            }.
            self endList.
        }.
        (let object type: ObjectRef) : {
            self beginObject.
            let isFirst mutable := true.
            object keysAndValuesDo: {:(Std String const ref)k :(Value const ref)v :: Void |
                isFirst ifTrue: {
                    isFirst := false
                } ifFalse: {
                    self putElementSeparator
                }.
                self putKey: k.
                self putValue: v.
            }.
            self endObject.
        }.
        }.
    }.

    meta definition: {
        static method writeValue: (value: Value const ref) onStream: (out: Std IO Stream ref) ::=> Void := {
            let outTextStreamWriter mutable := Std IO TextStreamWriter for: out address.
            Writer()
                textStreamWriter: outTextStreamWriter address;
                putValue: value
        }.

        static method writeValue: (value: Value const ref) onTextStream: (out: Std IO TextStreamWriter ref) ::=> Void := {
            Writer()
                textStreamWriter: out address;
                putValue: value
        }.

        static method valueToString: (value: Value const ref) ::=> Std String := {
            Std String textStreamContents: {:out :: Void |
                self writeValue: value onTextStream: out
            }
        }.
    }.
}.

}. ## End of namespace JSON
}. ## End of namespace Serialization
}. ## End of namespace Std
