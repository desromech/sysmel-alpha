namespace Stdn definition: {
namespace Math definition: {
namespace LinearAlgebra definition: {
namespace Tests definition: {

sealed template GenericMatrix2x2Test(ST: Type)
    := class superclass: Stdn Testing TestCase; definition: {
    compileTime constant GLA := GenericLinearAlgebra(ST).

    compileTime constant Scalar := GLA Scalar.
    compileTime constant Vector2 := GLA Vector2.
    compileTime constant Matrix2x2 := GLA Matrix2x2.

    test: testEquals with: {
        self assert: Matrix2x2 ones equals: Matrix2x2 ones.
        self assert: Matrix2x2 zeros closeTo: Matrix2x2 zeros.
        self assert: Matrix2x2 identity closeTo: Matrix2x2 identity.
    }.

    test: testNotEquals with: {
        self assert: Matrix2x2 ones notEquals: Matrix2x2 zeros.
        self assert: Matrix2x2 ones notEquals: Matrix2x2 identity.
        self assert: Matrix2x2 zeros notEquals: Matrix2x2 identity.
    }.

    test: testCloseTo with: {
        self assert: Matrix2x2 ones closeTo: Matrix2x2 ones.
        self assert: Matrix2x2 zeros closeTo: Matrix2x2 zeros.
        self assert: Matrix2x2 identity closeTo: Matrix2x2 identity.

        self assert: Matrix2x2 ones notCloseTo: Matrix2x2 zeros.
        self assert: Matrix2x2 ones notCloseTo: Matrix2x2 identity.
        self assert: Matrix2x2 zeros notCloseTo: Matrix2x2 identity.
    }.

    test: testHash with: {
        self assert: Matrix2x2 ones hash equals: Matrix2x2 ones hash.
        self assert: Matrix2x2 zeros hash equals: Matrix2x2 zeros hash.
        self assert: Matrix2x2 identity hash equals: Matrix2x2 identity hash.

        self assert: Matrix2x2 ones hash notEquals: Matrix2x2 zeros hash.
        self assert: Matrix2x2 ones hash notEquals: Matrix2x2 identity hash.
        self assert: Matrix2x2 zeros hash notEquals: Matrix2x2 identity hash.
    }.

    test: testZeros with: {
        self assert: Matrix2x2 zeros equals: Matrix2x2(
            0, 0,
            0, 0
        ).
        self deny: Matrix2x2 zeros isIdentity.
        self assert: Matrix2x2 zeros isZeros.
        self deny: Matrix2x2 zeros isOnes.
    }.

    test: testOnes with: {
        self assert: Matrix2x2 ones equals: Matrix2x2(
            1, 1,
            1, 1
        ).
        self assert: Matrix2x2 ones isOnes.
        self deny: Matrix2x2 ones isZeros.
        self deny: Matrix2x2 ones isIdentity.
    }.

    test: testIdentity with: {
        self assert: Matrix2x2 identity equals: Matrix2x2(
            1, 0,
            0, 1
        ).
        self assert: Matrix2x2 identity isIdentity.
        self deny: Matrix2x2 identity isZeros.
        self deny: Matrix2x2 identity isOnes.
    }.

    test: testColumnMajorIndices with: {
        self assert: Matrix2x2 columnMajorIndices equals: (Matrix2x2 columns: (
            0, 1,
            2, 3
        )).

        self assert: Matrix2x2 columnMajorIndices equals: (Matrix2x2 rows: (
            0, 2,
            1, 3
        )).
    }.

    test: testRowMajorIndices with: {
        self assert: Matrix2x2 rowMajorIndices equals: (Matrix2x2 rows: (
            0, 1,
            2, 3
        )).
        self assert: Matrix2x2 rowMajorIndices equals: (Matrix2x2 columns: (
            0, 2,
            1, 3
        )).
    }.

    test: testTranspose with: {
        self assert: Matrix2x2 columnMajorIndices transpose transpose equals: Matrix2x2 columnMajorIndices.

        let a := Matrix2x2 columnMajorIndices.
        let b := Matrix2x2 rowMajorIndices.
        self assert: a transpose equals: b.
        self assert: b transpose equals: a.
        self assert: a transpose transpose equals: a.
        self assert: b transpose transpose equals: b.
    }.

    test: testScale with: {
        self assert: (Matrix2x2 scale: Vector2(2, 3)) equals: Matrix2x2(
            2, 0,
            0, 3
        ).
        self assert: (Matrix2x2 scaleX: 2 y: 3) equals: Matrix2x2(
            2, 0,
            0, 3
        ).
    }.

    compileTime if: Scalar isPrimitiveFloatType then: {
        test: testDeterminant with: {
            self assert: Matrix2x2 ones determinant equals: 0.
            self assert: Matrix2x2 zeros determinant equals: 0.
            self assert: Matrix2x2 identity determinant equals: 1.
            self assert: Matrix2x2(1, 3, 2, 4) determinant equals: -2.
            self assert: Matrix2x2(1, 2, 3, 4) determinant equals: -2.
        }.

        test: testInverse with: {
            self assert: Matrix2x2 identity inverse equals: Matrix2x2 identity.
            self assert: Matrix2x2(0, 1, 2, 3) inverse equals: Matrix2x2(-1.5, 0.5, 1.0, 0.0).
        }.
    }.
}.

compileTime constant Int32x2x2Tests := GenericMatrix2x2Test(Int32).
compileTime constant Int64x2x2Tests := GenericMatrix2x2Test(Int64).
compileTime constant Float32x2x2Tests := GenericMatrix2x2Test(Float32).
compileTime constant Float64x2x2Tests := GenericMatrix2x2Test(Float64).

} ## End of namespace Tests
} ## End of namespace LinearAlgebra
} ## End of namespace Math
} ## End of namespace Stdn
