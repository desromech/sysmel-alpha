namespace Sysmel definition: {
namespace Tools definition: {
namespace LanguageServerProtocol definition: {

useNamespace: Std Serialization.

class LanguageServer definition: {
    public field inputReader type: Std IO TextStreamReader pointer.
    public field outputWriter type: Std IO TextStreamWriter pointer.

    method setForStdio => Void := {
        inputReader := Std stdin address.
        outputWriter := Std stdout address.
    }.

    method run => Void := {
        let request mutable := MessageRef nil.
        while: (request := self readNextRequest) isNotNil  do: {
            self processRequest: request
        }
    }.

    method readNextRequest ::=> MessageRef := {
        let header mutable := MessageHeader().
        (header readFrom: inputReader _) ifFalse: {
            return: MessageRef nil
        }.

        header hasContentLength ifFalse: {
            return: MessageRef nil
        }.

        let message := Message new.
        header isJsonRequest ifTrue: {
            let jsonString := inputReader _ nextString: header contentLength.
            message jsonContent: (JSON ValueParser parseString: jsonString)
        } ifFalse: {
            message binaryContent: (inputReader _ nextBytes: header contentLength)
        }.

        message
            header: header;
            yourself
    }.

    method processRequest: (request: MessageRef const ref) ::=> Void := {
        request header isJsonRequest ifFalse: {
            return: void
        }.

        let requestObject ref := request jsonContent get: DOM ObjectRef.
        let version := requestObject getString: "jsonrpc".
        Std assert: version asArraySlice = "2.0".
        let rpcMethod := requestObject getString: "method".
        let params := requestObject getObject: "params".

        requestObject at: "id" ifPresent: {:(DOM Value const ref)id :: Void |
            self processRequest: id name: rpcMethod asArraySlice params: params
        } ifAbsent: {
            self processNotification: rpcMethod asArraySlice params: params
        }.
    }.

    method doProcessRequest: (methodName: Std ImmutableString) params: (params: DOM ObjectRef const ref) ::=> DOM Value := {
        methodName selectCase: #{
        _ : {
            let error mutable type: JsonRpcError.
            error
                code: -32601;
                message: (Std String textStreamContents: {:out :: Void |
                    out << "Unimplemented method '" << methodName << "'."
                });
                signal.
        }
        }.

        DOM Value()
    }.

    method doProcessNotification: (methodName: Std ImmutableString) params: (params: DOM ObjectRef const ref) ::=> Void := {
    }.

    method processRequest: (id: DOM Value const ref) name: (methodName: Std ImmutableString) params: (params: DOM ObjectRef const ref) ::=> Void := {
        let response := DOM Object new
            at: "jsonrpc" put: "2.0" asMutableString;
            at: "id" put: id;
            yourself.

        try: {
            try: {
                response at: "result" put: (self doProcessRequest: methodName params: params)
            } catch: JsonRpcError doing: {:exception :: Void |
                response at: "error" put: (DOM Object new
                    at: "code" put: exception code;
                    at: "message" put: exception message;
                    yourself).
            }.
        } catch: Std Error doing: {:exception :: Void |
            response at: "error" put: (DOM Object new
                at: "code" put: -32000; ## Server specific error
                at: "message" put: exception messageText asMutableString;
                yourself).
        }.

        let encodedResponse := JSON Writer valueToString: response.
        outputWriter _
            << "Content-Length: " << (encodedResponse size + 2); crlf;
            << "Content-Type: application/vscode-jsonrpc; charset=utf-8"; crlf;
            crlf;
            << encodedResponse; crlf;
            flush.
    }.

    method processNotification: (methodName: Std ImmutableString) params: (params: DOM ObjectRef const ref) ::=> Void := {
        try: {
            self doProcessNotification: methodName params: params
        } catch: Std Error doing: {:exception :: Void |
            Std stderr << "Caught error during notification processing: " << exception; nl.
        }.
    }.
}.

}. ## End of namespace LanguageServerProtocol
}. ## End of namespace Tools
}. ## End of namespace Sysmel
