namespace Std definition: {
namespace Graphics definition: {
namespace GUI definition: {

useNamespace: Std Graphics Core.

#**
 * I am a 2D rendering that takes care automatically of handling the underlying swap chain in a window.
 * I work by delegating my job into an actual SwapChainSurface
 *#
class WindowSwapChain2DSceneRenderer superclass: Scene2D Scene2DRenderer; definition: {
    protected field window type: WindowRef.
    protected field swapChain2DSceneRenderer type: SwapChain2DSceneRendererRef.
    protected field currentClearColor type: Float32x4.

    meta definition: {
        method for: (window: WindowRef const ref) ::=> WindowSwapChain2DSceneRenderer rcRef := {
            WindowSwapChain2DSceneRenderer new
                initializeWithWindow: window;
                yourself
        }.
    }.

    method initializeWithWindow: (theWindow: WindowRef const ref) ::=> Void := {
        window := theWindow.
    }.

    override method clearColor: (color: Float32x4) ::=> Void := {
        currentClearColor := color.
        swapChain2DSceneRenderer ifNotNil: {
            swapChain2DSceneRenderer clearColor: currentClearColor
        }
    }.

    override method newSceneBuilder => Scene2D BuilderRef
        := window renderingDevice _ createScene2DBuilder.

    override method renderScene: (rootNode: Scene2D NodeRef const ref) ::=> Void := {
        self validateSwapChain ifFalse: {
            return: void
        }.

        swapChain2DSceneRenderer renderScene: rootNode
    }.

    override method present => Void := {
        swapChain2DSceneRenderer ifNil: { return: void }.

        swapChain2DSceneRenderer present
    }.

    method validateSwapChain => Boolean8 := {
        window ifNil: {
            return: false.
        }.

        let currentExtent := window extent.
        (swapChain2DSceneRenderer isNil ||
        swapChain2DSceneRenderer isOutOfDate ||
        swapChain2DSceneRenderer extent ~= currentExtent) ifTrue: {

            let renderingDevice := window renderingDevice.
            let swapChainCreationInfo mutable := SwapChainCreationInfo()
                colorBufferFormat: PixelFormat B8G8R8A8_UNormSRGB;
                extent: currentExtent;
                bufferCount: 2; ## Use at least double buffering.
                yourself.

            let defaultCommandQueue := renderingDevice _ getDefaultCommandQueue.

            let newSwapChain := window createSwapChain: swapChainCreationInfo for: defaultCommandQueue.
            newSwapChain ifNil: {
                swapChain2DSceneRenderer := nil.
                return: false.
            }.

            swapChain2DSceneRenderer := SwapChain2DSceneRenderer for: newSwapChain on: defaultCommandQueue on: renderingDevice withExtent: currentExtent.
            swapChain2DSceneRenderer clearColor: currentClearColor.
        }.

        return: true.
    }.
}.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Std
