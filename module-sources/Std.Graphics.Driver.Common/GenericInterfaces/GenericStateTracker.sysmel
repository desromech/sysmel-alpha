namespace Std definition: {
namespace Graphics definition: {
namespace Core definition: {

useNamespace: Std Math Geometry.

#**
 * I am a state tracker that works by delegating its work into a command list,
 * and by constructing pipeline states on the fly through a pipeline state cache.
 *#
class GenericStateTracker superclass: StateTracker; definition: {
    public field commandList type: CommandListRef.
    public field shaderCache type: ShaderCacheRef.
    public field pipelineStateCache type: PipelineStateCacheRef.

    protected field graphicsPipelineStateDescription type: GraphicsPipelineStateDescription.
    protected field graphicsPipelineState type: PipelineStateRef.
    protected field hasExplicitGraphicsPipelineState type: Boolean8.

    protected field computePipelineStateDescription type: ComputePipelineStateDescription.
    protected field computePipelineState type: PipelineStateRef.
    protected field hasExplicitComputePipelineState type: Boolean8.

    override method asRenderingDeviceCommandList => CommandList pointer
        := commandList asRenderingDeviceCommandList.

    override method resetState => Void := {
        graphicsPipelineStateDescription := GraphicsPipelineStateDescription().
        graphicsPipelineState := nil.

        computePipelineStateDescription := ComputePipelineStateDescription().
        computePipelineState := nil.
    }.

    override method resetFor: (allocator: CommandListAllocatorRef const ref) initialPipelineState: (initialPipeline: PipelineStateRef const ref) ::=> Void := {
        commandList resetFor: allocator initialPipelineState: initialPipeline.
        self resetState.
    }.

    override method close => Void := {
        commandList close.
    }.

    method validateGraphicsPipelineState => Boolean8 := {
        hasExplicitGraphicsPipelineState ifTrue: {return: true}.

        graphicsPipelineState := pipelineStateCache getOrCreateGraphicsPipeline: graphicsPipelineStateDescription.
        graphicsPipelineState ifNotNil: {
            commandList usePipelineState: graphicsPipelineState.
            self invalidateComputePipelineState.
        }.

        graphicsPipelineState isNotNil.
    }.

    method invalidateGraphicsPipelineState => Void := {
        graphicsPipelineState := nil.
        hasExplicitGraphicsPipelineState := false.
    }.

    method validateComputePipelineState => Boolean8 := {
        hasExplicitComputePipelineState ifTrue: {return: true}.

        computePipelineState := pipelineStateCache getOrCreateComputePipeline: computePipelineStateDescription.
        computePipelineState ifNotNil: {
            commandList usePipelineState: computePipelineState.
            self invalidateGraphicsPipelineState.
        }.

        computePipelineState isNotNil.
    }.

    method invalidateComputePipelineState => Void := {
        computePipelineState := nil.
        hasExplicitComputePipelineState := false.
    }.

    override method vertexShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription vertexShader ~~ shaderStage ifTrue: {
            graphicsPipelineStateDescription vertexShader: shaderStage.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method fragmentShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription fragmentShader ~~ shaderStage ifTrue: {
            graphicsPipelineStateDescription fragmentShader: shaderStage.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method geometryShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription geometryShader ~~ shaderStage ifTrue: {
            graphicsPipelineStateDescription geometryShader: shaderStage.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method tessellationControlShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription tessellationControlShader ~~ shaderStage ifTrue: {
            graphicsPipelineStateDescription tessellationControlShader: shaderStage.
            self invalidateGraphicsPipelineState
        }.

    }.

    override method tessellationEvaluationShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription tessellationEvaluationShader ~~ shaderStage ifTrue: {
            graphicsPipelineStateDescription tessellationEvaluationShader: shaderStage.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method computeShader: (shaderStage: ShaderStageHandleRef const ref) ::=> Void := {
        hasExplicitComputePipelineState || computePipelineState isNil || computePipelineStateDescription computeShader ~~shaderStage ifTrue: {
            computePipelineStateDescription computeShader: shaderStage.
            self invalidateComputePipelineState
        }.
    }.

    override method vertexShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self vertexShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self vertexShader: shaderStage
        }
    }.

    override method fragmentShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self fragmentShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self fragmentShader: shaderStage
        }
    }.

    override method geometryShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self geometryShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self geometryShader: shaderStage
        }
    }.

    override method tessellationControlShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self tessellationControlShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self tessellationControlShader: shaderStage
        }
    }.

    override method tessellationEvaluationShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self tessellationEvaluationShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self tessellationEvaluationShader: shaderStage
        }
    }.

    override method computeShader: (entryPointInfo: GPU ShaderEntryPointInfo const pointer) ::=> Void := {
        entryPointInfo ifNil: {
            self computeShader: ShaderStageHandleRef nil
        } ifNotNil: {
            let shaderStage := shaderCache getOrCreateShaderStageFromEntryPoint: entryPointInfo _.
            self computeShader: shaderStage
        }
    }.

    override method frontFaceOrientation: (frontFaceOrientation: FrontFaceOrientation) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription frontFaceOrientation ~= frontFaceOrientation ifTrue: {
            graphicsPipelineStateDescription frontFaceOrientation: frontFaceOrientation.
            self invalidateGraphicsPipelineState
        }.

    }.

    override method faceCullingMode: (faceCullingMode: FaceCullingMode) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription faceCullingMode ~= faceCullingMode ifTrue: {
            graphicsPipelineStateDescription faceCullingMode: faceCullingMode.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method polygonMode: (polygonMode: PolygonMode) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription polygonMode ~= polygonMode ifTrue: {
            graphicsPipelineStateDescription polygonMode: polygonMode.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method primitiveTopology: (primitiveTopology: PrimitiveTopology) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription primitiveTopology ~= primitiveTopology ifTrue: {
            graphicsPipelineStateDescription primitiveTopology: primitiveTopology.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method blendingEquation: (blendingEquation: BlendingEquation const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState ifTrue: {self invalidateGraphicsPipelineState}.
        graphicsPipelineStateDescription colorAttachments do: {:each :: Void |
            each blendingEnabled not || each blendingEquation ~= blendingEquation ifTrue: {
                each
                    blendingEnabled: true;
                    blendingEquation: blendingEquation.
                self invalidateGraphicsPipelineState
            }
        }.
    }.

    override method depthTestingEnabled: (enabled: Boolean8) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthTestingEnabled ~= enabled ifTrue: {
            graphicsPipelineStateDescription depthTestingEnabled: enabled.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method depthWriteMask: (enabled: Boolean8) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthWriteMask ~= enabled ifTrue: {
            graphicsPipelineStateDescription depthWriteMask: enabled.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method depthFunction: (depthFunction: CompareFunction) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthFunction ~= depthFunction ifTrue: {
            graphicsPipelineStateDescription depthFunction: depthFunction.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method depthBiasConstantFactor: (factor: Float32) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthBiasConstantFactor ~= factor ifTrue: {
            graphicsPipelineStateDescription depthBiasConstantFactor: factor.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method depthBiasSlopeFactor: (factor: Float32) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthBiasSlopeFactor ~= factor ifTrue: {
            graphicsPipelineStateDescription depthBiasSlopeFactor: factor.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method depthBiasClamp: (factor: Float32) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription depthBiasClamp ~= factor ifTrue: {
            graphicsPipelineStateDescription depthBiasClamp: factor.
            self invalidateGraphicsPipelineState
        }.
    }.

    method setVertexLayout: (vertexLayout: VertexLayoutRef const ref) ::=> Void := {
        hasExplicitGraphicsPipelineState || graphicsPipelineState isNil || graphicsPipelineStateDescription vertexLayout ~~ vertexLayout ifTrue: {
            graphicsPipelineStateDescription vertexLayout: vertexLayout.
            self invalidateGraphicsPipelineState
        }.
    }.

    override method useShaderSignature: (shaderSignature: ShaderSignatureRef const ref) ::=> Void := {
        commandList useShaderSignature: shaderSignature.

        graphicsPipelineStateDescription shaderSignature: shaderSignature.
        self invalidateGraphicsPipelineState.

        computePipelineStateDescription shaderSignature: shaderSignature.
        self invalidateGraphicsPipelineState.
    }.

    override method setViewport: (rectangle: RectangleI32) ::=> Void := {
        commandList setViewport: rectangle
    }.

    override method setScissor: (rectangle: RectangleI32) ::=> Void := {
        commandList setScissor: rectangle
    }.

    override method usePipelineState: (pipelineState: PipelineStateRef const ref) ::=> Void := {
        commandList usePipelineState: pipelineState.
        pipelineState ifNil: {return: void}.

        pipelineState isGraphicsPipelineState ifTrue: {
            graphicsPipelineState := pipelineState.
            hasExplicitGraphicsPipelineState := true.
            return: void.
        }.

        pipelineState isComputePipelineState ifTrue: {
            computePipelineState := computePipelineState.
            hasExplicitComputePipelineState := true.
            return: void.
        }.
    }.

    override method useVertexBinding: (vertexBinding: VertexBindingRef const ref) ::=> Void := {
        commandList useVertexBinding: vertexBinding.

        vertexBinding ifNil: {
            self setVertexLayout: VertexLayoutRef nil
        } ifNotNil: {
            self setVertexLayout: vertexBinding getVertexLayout
        }.
    }.

    override method useIndexBuffer: (buffer: BufferRef const ref) ::=> Void := {
        commandList useIndexBuffer: buffer
    }.

    override method useIndexBuffer: (buffer: BufferRef const ref) at: (offset: UIntPointer) stride: (stride: UIntPointer) ::=> Void := {
        commandList useIndexBuffer: buffer at: offset stride: stride
    }.

    override method useDrawIndirectBuffer: (buffer: BufferRef const ref) ::=> Void := {
        commandList useDrawIndirectBuffer: buffer
    }.

    override method useComputeDispatchIndirectBuffer: (buffer: BufferRef const ref) ::=> Void := {
        commandList useComputeDispatchIndirectBuffer: buffer
    }.

    override method useGraphicShaderBindings: (resourcesBinding: ShaderResourceBindingsRef const ref) ::=> Void := {
        commandList useGraphicShaderBindings: resourcesBinding
    }.

    override method useComputeShaderBindings: (resourcesBinding: ShaderResourceBindingsRef const ref) ::=> Void := {
        commandList useComputeShaderBindings: resourcesBinding
    }.

    override method drawArrays: (vertexCount: UInt32) instanceCount: (instanceCount: UInt32) firstVertex: (firstVertex: UInt32) baseInstance: (baseInstance: UInt32) ::=> Void := {
        self validateGraphicsPipelineState ifFalse: {
            return: void
        }.

        commandList drawArrays: vertexCount instanceCount: instanceCount firstVertex: firstVertex baseInstance: baseInstance
    }.

    override method drawArraysIndirect: (offset: UIntPointer) drawCount: (drawCount: UIntPointer) ::=> Void := {
        self validateGraphicsPipelineState ifFalse: {
            return: void
        }.

        commandList drawArraysIndirect: offset drawCount: drawCount
    }.

    override method drawElements: (indexCount: UInt32) instanceCount: (instanceCount: UInt32) firstIndex: (firstVertex: UInt32) baseVertex: (baseVertex: UInt32) baseInstance: (baseInstance: UInt32) ::=> Void := {
        self validateGraphicsPipelineState ifFalse: {
            return: void
        }.

        commandList drawElements: indexCount instanceCount: instanceCount firstIndex: firstVertex baseVertex: baseVertex baseInstance: baseInstance
    }.

    override method drawElementsIndirect: (offset: UIntPointer) drawCount: (drawCount: UIntPointer) ::=> Void := {
        self validateGraphicsPipelineState ifFalse: {
            return: void
        }.

        commandList drawElementsIndirect: offset drawCount: drawCount
    }.

    override method dispatchCompute: (groupCount: UInt32x3) ::=> Void := {
        self validateComputePipelineState ifFalse: {
            return: void
        }.

        commandList dispatchCompute: groupCount
    }.

    override method dispatchComputeIndirect: (offset: UInt32) ::=> Void := {
        self validateComputePipelineState ifFalse: {
            return: void
        }.

        commandList dispatchComputeIndirect: offset
    }.

    override method setStencilReference: (reference: UInt32) ::=> Void := {
        commandList setStencilReference: reference
    }.

    override method executeBundle: (bundle: CommandListRef const ref) ::=> Void := {
        commandList executeBundle: bundle.
    }.

    override method beginRenderPass: (renderPass: RenderPassRef const ref) on: (framebuffer: FramebufferRef const ref) contentOnBundle: (contentOnBundle: Boolean8) ::=> Void := {
        commandList beginRenderPass: renderPass on: framebuffer contentOnBundle: contentOnBundle.

        ## Set the color attachment formats.
        {
            let colorAttachmentFormats := renderPass colorAttachmentFormats.
            graphicsPipelineStateDescription colorAttachments
                removeAll;
                reserve: colorAttachmentFormats size.
            colorAttachmentFormats do: {:format :: Void |
                graphicsPipelineStateDescription colorAttachments add: (GraphicsPipelineStateColorAttachmentDescription()
                    format: format;
                    colorMask: Boolean8x4(true, true, true, true);
                    yourself)
            }.
        }.

        ## Set the depth stencil attachment format.
        graphicsPipelineStateDescription
            depthStencilFormat: renderPass depthStencilFormat.


        ## Set the multi sampling state.
        graphicsPipelineStateDescription
            sampleCount: renderPass sampleCount;
            sampleQuality: renderPass sampleQuality.

        self invalidateGraphicsPipelineState.
    }.

    override method endRenderPass ::=> Void := {
        commandList endRenderPass
    }.

    override method resolveFramebuffer: (source: FramebufferRef const ref) into: (destination: FramebufferRef const ref) ::=> Void := {
        commandList resolveFramebuffer: source into: destination
    }.

    override method resolveTexture: (source: TextureRef const ref) level: (sourceLevel: UInt32) layer: (sourceLayer: UInt32)
        into: (destination: TextureRef const ref) level: (destinationLevel: UInt32) layer: (destinationLayer: UInt32)
        levelCount: (levelCount: UInt32) layerCount: (layerCount: UInt32)
        aspect: (aspect: TextureAspect) ::=> Void := {
        commandList resolveTexture: source level: sourceLevel layer: sourceLayer
            into: destination level: destinationLevel layer: destinationLayer
            levelCount: levelCount layerCount: layerCount
            aspect: aspect
    }.

    override method pushConstants: (values: UInt8 const pointer) size: (size: UInt32) at: (offset: UInt32) ::=> Void := {
        commandList pushConstants: values size: size at: offset
    }.

    override method memoryBarrierFrom: (sourceStage: PipelineStageMask) accesses: (sourceAccesses: AccessMask) to: (destStage: PipelineStageMask) accesses: (destAccesses: AccessMask) ::=> Void := {
        commandList memoryBarrierFrom: sourceStage accesses: sourceAccesses to: destStage accesses: destAccesses
    }.

    override method buffer: (buffer: BufferRef const ref) offset: (offset: UIntPointer) size: (size: UIntPointer) memoryBarrierFrom: (sourceStage: PipelineStageMask) accesses: (sourceAccesses: AccessMask) to: (destStage: PipelineStageMask) accesses: (destAccesses: AccessMask) ::=> Void := {
        commandList buffer: buffer offset: offset size: size memoryBarrierFrom: sourceStage accesses: sourceAccesses to: destStage accesses: destAccesses
    }.

    override method texture: (texture: TextureRef const ref) subresourceRange: (subresourceRange: TextureSubResourceRange const ref) memoryBarrierFrom: (sourceStage: PipelineStageMask) accesses: (sourceAccesses: AccessMask) to: (destStage: PipelineStageMask) accesses: (destAccesses: AccessMask) transitionFrom: (oldUsageMode: TextureUsageMode) into: (newUsageMode: TextureUsageMode) ::=> Void := {
        commandList texture: texture subresourceRange: subresourceRange memoryBarrierFrom: sourceStage accesses: sourceAccesses to: destStage accesses: destAccesses transitionFrom: oldUsageMode into: newUsageMode
    }.

    override method pushBuffer: (buffer: BufferRef const ref) transitionBarrierFrom: (oldUsageMode: BufferUsageMode) into: (newUsageMode: BufferUsageMode) ::=> Void := {
        commandList pushBuffer: buffer transitionBarrierFrom: oldUsageMode into: newUsageMode
    }.

    override method pushTexture: (texture: TextureRef const ref) subresourceRange: (subresourceRange: TextureSubResourceRange const ref) transitionBarrierFrom: (oldUsageMode: TextureUsageMode) into: (newUsageMode: TextureUsageMode) ::=> Void := {
        commandList pushTexture: texture subresourceRange: subresourceRange transitionBarrierFrom: oldUsageMode into: newUsageMode
    }.

    override method popBufferTransitionBarrier ::=> Void := {
        commandList popBufferTransitionBarrier
    }.

    override method popTextureTransitionBarrier ::=> Void := {
        commandList popTextureTransitionBarrier
    }.

    override method copyFromBuffer: (sourceBuffer: BufferRef const ref) offset: (sourceOffset: UIntPointer) into: (destBuffer: BufferRef const ref) offset: (destOffset: UIntPointer) size: (copySize: UIntPointer) ::=> Void := {
        commandList copyFromBuffer: sourceBuffer offset: sourceOffset into: destBuffer offset: destOffset size: copySize
    }.

    override method copyFromBuffer: (sourceBuffer: BufferRef const ref) intoTexture: (destTexture: TextureRef const ref) region: (copyRegion: BufferImageCopyRegion const ref) ::=> Void := {
        commandList copyFromBuffer: sourceBuffer intoTexture: destTexture region: copyRegion
    }.

    override method copyFromTexture: (sourceTexture: TextureRef const ref) intoBuffer: (destBuffer: BufferRef const ref) region: (copyRegion: BufferImageCopyRegion const ref) ::=> Void := {
        commandList copyFromTexture: sourceTexture intoBuffer: destBuffer region: copyRegion
    }.

    override method copyFromTexture: (sourceTexture: TextureRef const ref) intoTexture: (destTexture: TextureRef const ref) region: (copyRegion: ImageCopyRegion const ref) ::=> Void := {
        commandList copyFromTexture: sourceTexture intoTexture: destTexture region: copyRegion
    }.

    override method dumpToFileNamed: (fileName: Char8 const pointer) ::=> Void := {
        commandList dumpToFileNamed: fileName
    }.
}.

compileTime constant GenericStateTrackerRef := GenericStateTracker rcRef.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Std
