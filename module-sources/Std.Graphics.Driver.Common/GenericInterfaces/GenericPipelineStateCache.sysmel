namespace Std definition: {
namespace Graphics definition: {
namespace Core definition: {

#**
 * I am a generic implementation for a pipeline state cache
 *#
class GenericPipelineStateCache superclass: PipelineStateCache; definition: {
    public field device type: RenderingDeviceWeakRef.
    protected field mutex type: Std Mutex.
    protected field graphicsPipelineStates type: Std Collections Dictionary(GraphicsPipelineStateDescription, PipelineStateRef).
    protected field computePipelineStates type: Std Collections Dictionary(ComputePipelineStateDescription, PipelineStateRef).

    override method getOrCreateGraphicsPipeline: (description: GraphicsPipelineStateDescription const ref) ::=> PipelineStateRef := {
        mutex withLock: {
            graphicsPipelineStates at: description ifPresent: {:(PipelineStateRef ref)existent :: Void |
                return: existent
            }
        }.

        let strongDevice := device asRCRef.
        strongDevice ifNil: {
            return: PipelineStateRef nil.
        }.

        let result mutable := strongDevice createGraphicsPipelineState: description.
        mutex withLock: {
            graphicsPipelineStates at: description put: result
        }.

        result
    }.

    override method getOrCreateComputePipeline: (description: ComputePipelineStateDescription const ref) ::=> PipelineStateRef := {
        mutex withLock: {
            computePipelineStates at: description ifPresent: {:(PipelineStateRef ref)existent :: Void |
                return: existent
            }
        }.

        let strongDevice := device asRCRef.
        strongDevice ifNil: {
            return: PipelineStateRef nil.
        }.

        let result := strongDevice createComputePipelineState: description.
        mutex withLock: {
            computePipelineStates at: description put: result
        }.

        result
    }.
}.

compileTime constant GenericPipelineStateCacheRef := GenericPipelineStateCache rcRef.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Std
