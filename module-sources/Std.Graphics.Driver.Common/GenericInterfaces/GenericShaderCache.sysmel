namespace Std definition: {
namespace Graphics definition: {
namespace Core definition: {

#**
 * I am a handle for a shader cache.
 *#
class GenericShaderCache superclass: ShaderCache; definition: {
    public field device type: RenderingDeviceWeakRef.
    protected field mutex type: Std Mutex.
    protected field shaderModuleCache type: Std Collections Dictionary(GPU ShaderModuleInfo const pointer, ShaderLibraryHandleRef).
    protected field shaderStageCache type: Std Collections Dictionary(GPU ShaderEntryPointInfo const pointer, ShaderStageHandleRef).

    override method getOrCreateShaderLibraryFromModule: (shaderModule: GPU ShaderModuleInfo const ref) ::=> ShaderLibraryHandleRef := {
        ## Find the stage in the cache.
        mutex withLock: {
            shaderModuleCache at: shaderModule address ifPresent: {:(ShaderLibraryHandleRef ref)cached :: Void |
                return: cached
            }.
        }.

        let strongDevice := device asRCRef.
        strongDevice ifNil: {
            return: ShaderLibraryHandleRef nil
        }.

        let libraryHandle := strongDevice createShaderLibraryFromModule: shaderModule.
        mutex withLock: {
            shaderModuleCache at: shaderModule address put: libraryHandle
        }.

        return: libraryHandle
    }.

    override method getOrCreateShaderStageFromEntryPoint: (entryPointInfo: GPU ShaderEntryPointInfo const ref) ::=> ShaderStageHandleRef := {
        ## Find the stage in the cache.
        mutex withLock: {
            shaderStageCache at: entryPointInfo address ifPresent: {:(ShaderStageHandleRef ref)cached :: Void |
                return: cached
            }.
        }.

        let shaderLibrary := self getOrCreateShaderLibraryFromModule: entryPointInfo moduleInfo _.
        shaderLibrary ifNil: {
            return: ShaderStageHandleRef nil
        }.

        let shaderStageHandle := shaderLibrary createHandleForEntryPoint: entryPointInfo.
        mutex withLock: {
            shaderStageCache at: entryPointInfo address put: shaderStageHandle
        }.

        shaderStageHandle.
    }
}.

compileTime constant GenericShaderCacheRef := GenericShaderCache rcRef.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Std
