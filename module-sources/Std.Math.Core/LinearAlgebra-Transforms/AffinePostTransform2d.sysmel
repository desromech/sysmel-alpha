namespace Std definition: {
namespace Math definition: {
namespace LinearAlgebra definition: {

#**
 * I am an affine transform which is constructed by post multiplication of transformations
 *#
sealed template GenericAffinePostTransform2d(ST: Type)
    := class superclass: GenericLinearAlgebra(ST); definition: {
    public field matrix type: Matrix2x2.
    public field translation type: Vector2.

    const inline method asMatrix2x3 => Matrix2x3
        := Matrix2x3(matrix firstColumn, matrix secondColumn, translation).

    const inline method asMatrix3x3 => Matrix3x3
        := Matrix3x3(Vector3(matrix firstColumn, 0), Vector3(matrix secondColumn, 0), Vector3(translation, 1)).

    inline method setIdentity => Void := {
        matrix := Matrix2x2 identity.
        translation := Vector2 zeros.
    }.

    const inline method isTranslationOnly => Boolean8
        := matrix isIdentity.

    inline method translateBy: (newTranslation: Vector2) ::=> Void := {
        translation := translation + (self transformVector: newTranslation).
    }.

    inline method applyMatrix: (newMatrix: Matrix2x2) ::=> Void := {
        matrix := matrix * newMatrix.
    }.

    inline method transformWith: (other: SelfType const ref) ::=> Void := {
        let newMatrix := matrix * other matrix.
        let newTranslation := translation + (self transformVector: other translation).

        matrix := newMatrix.
        translation := newTranslation.
    }.

    inline method scaleBy: (scale: Vector2) ::=> Void := {
        self applyMatrix: (Matrix2x2 scale: scale)
    }.

    compileTime if: Scalar isPrimitiveFloatType then: {
        inline method rotateByRadians: (angle: Scalar) ::=> Void := {
            self applyMatrix: (Matrix2x2 rotation: angle)
        }.

        inline method rotateByDegrees: (angle: Scalar) ::=> Void := {
            self rotateByRadians: angle degreesToRadians
        }.
    }.

    const inline method transformVector: (vector: Vector2) ::=> Vector2
        := matrix * vector.

    const inline method transformPoint: (point: Vector2) ::=> Vector2
        := matrix * point + translation.

    compileTime if: Scalar isPrimitiveFloatType then: {
        const inline method inverseTransformVector: (vector: Vector2) ::=> Vector2
            := matrix inverse * vector.

        const inline method inverseTransformPoint: (point: Vector2) ::=> Vector2
            := matrix inverse * (point - translation).
    }.

    macro method restoreAfter: aBlock := {
        let oldTransform := __astBuilder gensym: #oldTransform.
        ``{
            let `,oldTransform := `,self.
            try: `,aBlock finally: {
                `,self := `,oldTransform
            }.
        } asSequenceWithoutLexicalScope
    }.
}.

compileTime constant AffinePostTransform2dI32 := GenericAffinePostTransform2d(Int32).
compileTime constant AffinePostTransform2dF32 := GenericAffinePostTransform2d(Float32).
compileTime constant AffinePostTransform2dF64 := GenericAffinePostTransform2d(Float64).

} ## End of namespace LinearAlgebra
} ## End of namespace Math
} ## End of namespace Std
