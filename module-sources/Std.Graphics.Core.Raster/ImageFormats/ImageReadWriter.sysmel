namespace Std definition: {
namespace Graphics definition: {
namespace ImageFormats definition: {

useNamespace: Std Graphics Core.

class ImageReadWriter superclass: RCObject; definition: {
    const virtual method supportsExtension: (extension: Char8 const arraySlice) ::=> Boolean8
        := false.

    const virtual method supportsMimeType: (mimeType: Char8 const arraySlice) ::=> Boolean8
        := false.

    const virtual method putForm: (form: FormRef const ref) onFileNamed: (fileName: Std String const ref) ::=> Boolean8
        := self putForm: form onFileReference: fileName asNativeFileReference.

    const virtual method putForm: (form: FormRef const ref) onFileReference: (fileReference: Std FileReference const ref) ::=> Boolean8 := {
        let outFile := fileReference binaryUnbufferedWriteStream.
        outFile ifNotNil: {
            self putForm: form onStream: outFile _.
            return: true
        }.
        return: false
    }.

    const virtual method putTextureData: (textureData: TextureDataRef const ref) onFileNamed: (fileName: Std String const ref) ::=> Boolean8
        := self putTextureData: textureData onFileReference: fileName asNativeFileReference.

    const virtual method putTextureData: (textureData: TextureDataRef const ref) onFileReference: (fileReference: Std FileReference const ref) ::=> Boolean8 := {
        let outFile := fileReference binaryUnbufferedWriteStream.
        outFile ifNotNil: {
            self putTextureData: textureData onStream: outFile _.
            return: true
        }.
        return: false
    }.

    const virtual method putForm: (form: FormRef const ref) onStream: (outputStream: Std IO Stream ref) ::=> Void := {
        ## TODO: implement this.
    }.

    const virtual method putTextureData: (textureData: TextureDataRef const ref) onStream: (outputStream: Std IO Stream ref) ::=> Void := {
        self putForm: textureData asForm onStream: outputStream
    }.

    const virtual method readFormFromFileNamed: (fileName: Std String const ref) ::=> FormRef
        := self readFormFromFileReference: fileName asNativeFileReference.

    const virtual method readFormFromFileReference: (fileReference: Std FileReference const ref) ::=> FormRef := {
        let inFile := fileReference binaryUnbufferedReadStream.
        inFile ifNil: {
            return: FormRef nil
        }.

        self readFormFromStream: inFile _
    }.

    const virtual method readTextureDataFromFileNamed: (fileName: Std String const ref) ::=> TextureDataRef
        := self readTextureDataFromFileReference: fileName asNativeFileReference.

    const virtual method readTextureDataFromFileReference: (fileReference: Std FileReference const ref) ::=> TextureDataRef := {
        let inFile := fileReference binaryUnbufferedReadStream.
        inFile ifNil: {
            return: TextureDataRef nil
        }.

        self readTextureDataFromStream: inFile _
    }.

    const virtual method readFormFromStream: (inputStream: Std IO Stream ref) ::=> FormRef
        := FormRef nil.

    const virtual method readTextureDataFromStream: (inputStream: Std IO Stream ref) ::=> TextureDataRef := {
        let form := self readFormFromStream: inputStream.
        form ifNil: {return: TextureDataRef nil}.

        form asTextureData
    }

}.

} ## End of namespace ImageFormats
} ## End of namespace Graphics
} ## End of namespace Std
