namespace Std definition: {
namespace Graphics definition: {
namespace Core definition: {

useNamespace: Std Math LinearAlgebra.
useNamespace: Std Math Geometry.

#**
 * I am an abstract rendering canvas interface. I could be implemented through
 * CPU based software rendering, or I could be implemented through a hardware based accelerator.
 *#
class Canvas superclass: Object; definition: {
    public field transform type: AffinePostTransform2dF32.
    public field drawingState type: CanvasDrawingState.
    public field paintState type: CanvasPaintState.
    public field fontFaceWithSize type: FontFaceWithSizeRef.

    protected field currentClipRectangle type: RectangleF32.

    method resetCanvasState => Void := {
        drawingState := CanvasDrawingState()
            blendingMode: BlendingMode CompositeAlphaOver;
            yourself.
        currentClipRectangle := RectangleF32 fullSpace.
        transform setIdentity.
        fontFaceWithSize := nil.
        self doSetClipRectangle: currentClipRectangle.
    }.

    ## This method clears the whole surface with the active color.
    abstract method clear ::=> Void.

    ## Fill a triangle.
    abstract method fillTriangle: (p1: Float32x2) color: (c1: Float32x4) tc: (tc1: Float32x2)
        p2: (p2: Float32x2) color: (c2: Float32x4) tc: (tc2: Float32x2)
        p3: (p3: Float32x2) color: (c3: Float32x4) tc: (tc3: Float32x2) ::=> Void.

    ## Draw the rectangle lines.
    abstract method drawSegmentStart: (startPoint: Float32x2) end: (endPoint: Float32x2) ::=> Void.

    ## Fill a rectangle.
    abstract method fillRectangle: (rectangle: RectangleF32) ::=> Void.

    ## Gets the current font face with size.
    abstract method fontFaceWithSize => FontFaceWithSize rcRef const ref.

    ## Sets the current font face with size.
    abstract method fontFaceWithSize: (font: FontFaceWithSize rcRef const ref) ::=> Void.

    ## Draw a character glyph in a particular position.
    abstract method drawGlyph: (glyph: GlyphRef const ref) at: (aPosition: Float32x2) ::=> Void.


    ## This method sets a white color.
    inline method white ::=> Void := {
        self paintState white
    }.

    ## This method sets a black color.
    inline method black ::=> Void := {
        self paintState black
    }.

    ## This method sets a transparent color.
    inline method transparent ::=> Void := {
        self paintState transparent
    }.

    inline method color: (aColor: UInt8x4) ::=> Void := {
        self paintState color: aColor
    }.

    inline method color: (aColor: Float32x4) ::=> Void := {
        self paintState color: aColor
    }.

    inline method fillTriangle: (p1: Float32x2) color: (c1: Float32x4) p2: (p2: Float32x2) color: (c2: Float32x4) p3: (p3: Float32x2) color: (c3: Float32x4)::=> Void := {
        self fillTriangle: p1 color: c1 tc: Float32x2 zeros
            p2: p2 color: c2 tc: Float32x2 zeros
            p3: p3 color: c3 tc: Float32x2 zeros
    }.

    inline method fillTriangle: (p1: Float32x2) tc: (tc1: Float32x2) p2: (p2: Float32x2) tc: (tc2: Float32x2) p3: (p3: Float32x2) tc: (tc3: Float32x2) ::=> Void := {
        self fillTriangle: p1 color: Float32x4 ones tc: tc1
            p2: p2 color: Float32x4 ones tc: tc2
            p3: p3 color: Float32x4 ones tc: tc3
    }.

    inline method fillTriangle: (p1: Float32x2) p2: (p2: Float32x2) p3: (p3: Float32x2) ::=> Void := {
        self fillTriangle: p1 color: Float32x4 ones tc: Float32x2 zeros
            p2: p2 color: Float32x4 ones tc: Float32x2 zeros
            p3: p3 color: Float32x4 ones tc: Float32x2 zeros
    }.

    inline method gradientStart: (startColor: Float32x4) position: (startPosition: Float32x2) end: (endColor: Float32x4) position: (endPosition: Float32x2) ::=> Void := {
        let transform ref := self transform.
        self paintState gradientStart: startColor position: (transform transformPoint: startPosition) end: endColor position: (transform transformPoint: endPosition)
    }.

    ## Fill a rectangle with a horizontal gradient.
    inline method fillRectangle: (rectangle: RectangleF32) verticalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> Void := {
        self gradientStart: startColor position: rectangle min end: endColor position: Float32x2(rectangle min x, rectangle max y).
        self fillRectangle: rectangle
    }.

    ## Fill a rectangle with a horizontal gradient.
    inline method fillRectangle: (rectangle: RectangleF32) horizontalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> Void := {
        self gradientStart: startColor position: rectangle min end: endColor position: Float32x2(rectangle max x, rectangle min y).
        self fillRectangle: rectangle
    }.

    ## Draw the rectangle lines.
    inline method drawRectangle: (rectangle: RectangleF32) ::=> Void := {
        rectangle isEmpty ifTrue: {
            return: void.
        }.

        let inclusiveRectangle := RectangleF32 min: rectangle min + 0.5f max: (rectangle max - 0.5f max: rectangle min + 0.5f).

        self
            drawSegmentStart: inclusiveRectangle bottomLeft end: inclusiveRectangle bottomRight;
            drawSegmentStart: inclusiveRectangle bottomRight end: inclusiveRectangle topRight;
            drawSegmentStart: inclusiveRectangle topRight end: inclusiveRectangle topLeft;
            drawSegmentStart: inclusiveRectangle topLeft end: inclusiveRectangle bottomLeft.
    }.

    ## Draws a character with the font face and size. This returns the position for drawing the next character.
    inline method drawCharacterWithState: (state: TextDrawingState) ::=> TextDrawingState := {
        fontFaceWithSize ifNil: {
            return: state.
        }.

        let newState mutable := fontFaceWithSize computeNewDrawingState: state.
        newState isVisibleCharacter ifTrue: {
            ## Get the glyph for the character
            let glyph := fontFaceWithSize getOrCreateGlyphForCodePoint: newState character.
            glyph ifNil: {
                return: newState
            }.

            ## Convert the glyph into a form. We can always do this operation with fonts.
            self drawGlyph: glyph at: newState previousPosition.
        }.

        return: newState.
    }.

    ## Draw an UTF-8 encoded string. Allow overriding for optiomization reasons.
    inline method drawUTF8String: (encodedString: Char8 const arraySlice) withState: (state: TextDrawingState) ::=> TextDrawingState := {
        let currentState mutable := state.

        (Std Collections UTF8CodePointsIterator for: encodedString) do: {:codePoint :: Void |
            currentState character: codePoint.
            currentState := self drawCharacterWithState: currentState.
        }.

        currentState
    }.

    inline method drawUTF8String: (encodedString: Char8 const arraySlice) at: (position: Float32x2) ::=> TextDrawingState
        := self drawUTF8String: encodedString withState: (TextDrawingState position: position).

    inline method drawUTF8String: (string: Std String const ref) at: (position: Float32x2) ::=> TextDrawingState
        := self drawUTF8String: string asArraySlice at: position.

    inline method drawUTF8String: (string: Std String const ref) withState: (state: TextDrawingState) ::=> TextDrawingState
        := self drawUTF8String: string asArraySlice withState: state.

    inline method drawUTF8String: (encodedString: Char8 const pointer) at: (position: Float32x2) ::=> TextDrawingState
        := self drawUTF8String: (encodedString until: Std cstrlen(encodedString)) at: position.

    abstract method doSetClipRectangle: (aRectangle: RectangleF32) ::=> Void.

    inline method currentClipRectangle => RectangleF32
        := currentClipRectangle.

    inline method applyClipRectangle: (aRectangle: RectangleF32) ::=> Void := {
        currentClipRectangle := currentClipRectangle intersectionWith: aRectangle.
        self doSetClipRectangle: currentClipRectangle.
    }.

    inline method restoreClipRectangle: (aRectangle: RectangleF32) ::=> Void := {
        currentClipRectangle := aRectangle.
        self doSetClipRectangle: currentClipRectangle.
    }.

    inline method applyLocalClipRectangle: (aRectangle: RectangleF32) ::=> Void
        := self applyClipRectangle: (aRectangle transformedWith: self transform).

    macro method withLocalClipRectangle: aRectangle do: aBlock := {
        let canvas := __astBuilder gensym: #canvas.
        let clipRectangleMemento := __astBuilder gensym: #clipRectangleMemento.
        ``{
            let `,canvas ref := `,self.
            let `,clipRectangleMemento := `,canvas currentClipRectangle.
            `,canvas applyLocalClipRectangle: `,aRectangle.
            try: `,aBlock finally: {
                `,canvas restoreClipRectangle: `,clipRectangleMemento.
            }.

        } asSequenceWithoutLexicalScope
    }.
}.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Std
