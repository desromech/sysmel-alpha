namespace Std definition: {
namespace Sound definition: {

useNamespace: SDL2.

#**
 * I represent a particular sound driver.
 *#
class SDL2SoundDriver superclass: SoundDriver; definition: {
    override method initialize => Void := {
        SoundDriverRegistry default registerDriver: self address
    }.

    override method finalize => Void := {
        SoundDriverRegistry default unregisterDriver: self address
    }.

    field mutex type: Std Mutex.
    field mainDevice type: SDL2PlaybackDevice weakRCRef.

    meta definition: {
        global Singleton mutable type: SDL2SoundDriver.
    }.

    method createDefaultPlaybackDevice => SDL2PlaybackDeviceRef := {
        SDL2InitState uniqueInstance increaseAudioUserCount.

        let result := SDL2PlaybackDevice new.

        let audioDeviceSpec mutable := SDL_AudioSpec().
        let desiredAudioSpec mutable := SDL_AudioSpec()
            freq: 48000;
            format: AUDIO_S16;
            channels: 2;
            samples: 2048;
            callback: sdl2PlaybackDeviceAudioCallback address;
            userdata: result __rcPointer__ getPointer;
            yourself.
        let audioDeviceID := SDL_OpenAudioDevice(nil, 0, desiredAudioSpec address, audioDeviceSpec address, SDL_AUDIO_ALLOW_FREQUENCY_CHANGE | SDL_AUDIO_ALLOW_FORMAT_CHANGE | SDL_AUDIO_ALLOW_SAMPLES_CHANGE).
        audioDeviceID = 0 ifTrue: {
            SDL2InitState uniqueInstance decreaseAudioUserCount.
            return: SDL2PlaybackDeviceRef nil
        }.

        result initializeWithAudioDevice: audioDeviceID audioSpec: audioDeviceSpec.
        result
    }.

    override method getOrCreateDefaultPlaybackDevice => PlaybackDeviceRef := {
        let result mutable := mainDevice asRCRef.
        result ifNotNil: {return: (result upCastFor: PlaybackDevice)}.

        mutex withLock: {
            result := mainDevice asRCRef.
            result ifNotNil: {return: (result upCastFor: PlaybackDevice)}.

            result := self createDefaultPlaybackDevice.
            mainDevice := result.
            return: (result upCastFor: PlaybackDevice)
        }.

        PlaybackDeviceRef nil.
    }.
}.

compileTime constant SDL2SoundDriverRef := SDL2SoundDriver rcRef.

} ## End of namespace Sound
} ## End of namespace Std
