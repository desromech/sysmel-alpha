namespace Std definition: {
namespace Serialization definition: {
namespace JSON definition: {
namespace Tests definition: {

class ValueParserTest superclass: Std Testing TestCase; definition: {

    test: testInteger with: {
        self assert: ((ValueParser parseString: "1" asMutableString) get: UInt64) equals: 1.
        self assert: ((ValueParser parseString: "-1" asMutableString) get: Int64) equals: -1.

        self assert: DOM valueToIntPointer(ValueParser parseString: "1" asMutableString) equals: 1.
        self assert: DOM valueToIntPointer(ValueParser parseString: "-1" asMutableString) equals: -1.
        self assert: DOM valueToUIntPointer(ValueParser parseString: "1" asMutableString) equals: 1.
    }.

    test: testFloat with: {
        self assert: ((ValueParser parseString: "1.0" asMutableString) get: Float64) equals: 1.0f.

        self assert: DOM valueToFloat64(ValueParser parseString: "1" asMutableString) equals: 1.0f.
        self assert: DOM valueToFloat64(ValueParser parseString: "-1" asMutableString) equals: -1.0f.
    }.

    test: testString with: {
        self assert: ((ValueParser parseString: "\"Hello World\"" asMutableString) get: Std String) equals: "Hello World" asMutableString.
    }.

    test: testKeywords with: {
        self assert: ((ValueParser parseString: "nil" asMutableString) get: _UndefinedType) equals: nil.
        self assert: ((ValueParser parseString: "true" asMutableString) get: Boolean8) equals: true.
        self assert: ((ValueParser parseString: "false" asMutableString) get: Boolean8) equals: false.
    }.

    test: testEmptyList with: {
        let parsedList := (ValueParser parseString: "[]" asMutableString) get: DOM ListRef.
        self assert: parsedList elements isEmpty.
    }.

    test: testSingleElementList with: {
        let parsedList := (ValueParser parseString: "[1]" asMutableString) get: DOM ListRef.
        self assert: (parsedList elements first get: UInt64) equals: 1.
    }.

    test: testThreeElementList with: {
        let parsedList := (ValueParser parseString: "[1, -2, 2.5]" asMutableString) get: DOM ListRef.
        self assert: (parsedList elements first get: UInt64) equals: 1.
        self assert: (parsedList elements second get: Int64) equals: -2.
        self assert: (parsedList elements third get: Float64) equals: 2.5.
    }.

    test: testEmptyObject with: {
        let parsedObject := (ValueParser parseString: "{}" asMutableString) get: DOM ObjectRef.
        self assert: parsedObject elements isEmpty.
    }.

    test: testSingleElementObject with: {
        let parsedObject := (ValueParser parseString: "{\"first\": 1}" asMutableString) get: DOM ObjectRef.
        self assert: parsedObject elements size equals: 1.
        ##self assert: ((parsedObject elements at: "first" asMutableString) get: UInt64) equals: 1.
    }.

    test: testThreeElementObject with: {
        let parsedObject := (ValueParser parseString: "{\"first\": 1, \"second\": -2, \"third\": 2.5}" asMutableString) get: DOM ObjectRef.
        self assert: ((parsedObject elements at: "first" asMutableString) get: UInt64) equals: 1.
        self assert: ((parsedObject elements at: "second" asMutableString) get: Int64) equals: -2.
        self assert: ((parsedObject elements at: "third" asMutableString) get: Float64) equals: 2.5.
    }.

    test: testNestedObject with: {
        let parsedObject := (ValueParser parseString: "{\"first\": 1, \"second\": {\"array\":[1, 2], \"childattr\": nil}}" asMutableString) get: DOM ObjectRef.
        self assert: ((parsedObject elements at: "first" asMutableString) get: UInt64) equals: 1.

        let nestedObject ref := (parsedObject elements at: "second" asMutableString) get: DOM ObjectRef.
        (nestedObject elements at: "childattr" asMutableString) get: _UndefinedType.

        let nestedArray ref := (nestedObject elements at: "array" asMutableString) get: DOM ListRef.
        self assert: nestedArray elements size equals: 2.
        self assert: (nestedArray elements first get: UInt64) equals: 1.
        self assert: (nestedArray elements second get: UInt64) equals: 2.
    }.
}.

} ## End of namespace Tests
}. ## End of namespace JSON
}. ## End of namespace Serialization
}. ## End of namespace Std
