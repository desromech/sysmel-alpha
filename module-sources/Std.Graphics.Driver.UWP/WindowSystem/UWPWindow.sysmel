namespace Std definition: {
namespace Graphics definition: {
namespace GUI definition: {

useNamespace: Std Graphics Core.

function sysmel_uwp_getExtent externC(windowHandle: Void pointer, outWidth: UInt32 pointer, outHeight: UInt32 pointer) => Boolean8.

class UWPWindow superclass: Window; definition: {
    public field handle type: Void pointer.
    protected field renderingDevice type: RenderingDeviceRef.
    protected field activeSwapChain type: SwapChainRef.
    public field extent type: UInt32x2.
    public field windowSystem type: UWPWindowSystem weakRCRef.

    override method extent => UInt32x2 := {
        handle ifNotNil: {
            let width mutable type: UInt32.
            let height mutable type: UInt32.
            sysmel_uwp_getExtent(handle, width address, height address) ifTrue: {
                extent := UInt32x2(width, height)
            }.
        }.

        extent
    }.

    override method close => Void := {
        renderingDevice ifNotNil: {
            renderingDevice waitForIdle
        }.

        ## Destroy the active swap chain.
        activeSwapChain ifNotNil: {
            activeSwapChain destroy.
        }.

        windowSystem ifNotNil: {:ws :: Void | ws windowClosed: self asRCRef}
    }.

    override method renderingDevice => RenderingDeviceRef := {
        renderingDevice ifNil: {
            renderingDevice := RenderingDriverRegistry defaultDriver _ getMainDevice
        }.

        renderingDevice
    }.

    override method renderingDevice: (theRenderingDevice: RenderingDeviceRef const ref) ::=> Void := {
        renderingDevice := theRenderingDevice
    }.

    override method createSwapChain: (swapChainCreationInfo: SwapChainCreationInfo const ref) for: (renderingQueue: CommandQueueRef const ref) ::=> SwapChainRef := {
        handle ifNil: {
            return: SwapChainRef nil
        }.

        let adaptedCreationInfo mutable := swapChainCreationInfo.
        adaptedCreationInfo extent = UInt32x2 zeros ifTrue: {
            adaptedCreationInfo extent: self extent.
        }.

        adaptedCreationInfo
            windowSystemName: "winrt" asMutableString;
            window: handle.

        adaptedCreationInfo presentationMode = SwapChainPresentationMode Default ifTrue: {
            vsyncDisabled ifTrue: {
                adaptedCreationInfo presentationMode: SwapChainPresentationMode Mailbox
            } ifFalse: {
                adaptedCreationInfo presentationMode: SwapChainPresentationMode Fifo
            }
        }.

        adaptedCreationInfo fallbackPresentationMode = SwapChainPresentationMode Default ifTrue: {
            vsyncDisabled ifTrue: {
                adaptedCreationInfo fallbackPresentationMode: SwapChainPresentationMode Immediate
            } ifFalse: {
                adaptedCreationInfo fallbackPresentationMode: SwapChainPresentationMode Fifo
            }
        }.

        ## Supply the old swap chain, and make sure it is not on active use anymore.
        activeSwapChain ifNotNil: {
            adaptedCreationInfo oldSwapChain: activeSwapChain.
            renderingDevice waitForIdle.
        }.

        let newActiveSwapChain := renderingDevice createSwapChain: adaptedCreationInfo for: renderingQueue.
        activeSwapChain ifNotNil: {
            activeSwapChain destroy
        }.
        activeSwapChain := newActiveSwapChain
    }.

    override method createScene2DRenderer => WindowScene2DRendererRef := {
        (WindowSwapChain2DSceneRenderer for: (self asRCRef upCastFor: Window)) upCastFor: WindowScene2DRenderer
    }.

}.

}. ## End of namespace GUI
}. ## End of namespace Graphics
}. ## End of namespace Std
