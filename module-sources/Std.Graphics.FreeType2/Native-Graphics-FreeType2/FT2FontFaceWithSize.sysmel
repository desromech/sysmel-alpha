namespace Std definition: {
namespace Graphics definition: {
namespace FT2Bindings definition: {

useNamespace: Std Graphics Core.
useNamespace: FreeType2.

class FT2FontFaceWithSize superclass: FontFaceWithSize; definition: {
    public field handle type: FT_Size.
    public field face type: FT2FontFace pointer.
    private field glyphIndexDictionary type: Std Collections Dictionary(UInt32, GlyphRef).
    private field glyphCodePointDictionary type: Std Collections Dictionary(Char32, GlyphRef).

    override method finalize => Void := {
        self releaseHandle.
    }.

    method releaseHandle => Void := {
        handle ifNotNil: {
            FT_Done_Size(handle).
            handle := nil.
        }
    }.

    method createGlyphForIndex: (glyphIndex: UInt32) ::=> GlyphRef := {
        FT2Library defaultInstance mutex withLock: {
            FT_Activate_Size(handle).
            self doCreateGlyphForIndex: glyphIndex.
        }.
    }.

    method doCreateGlyphForIndex: (glyphIndex: UInt32) ::=> GlyphRef := {
        ##Std stdout << "Glyph index " << glyphIndex; nl.
        let error mutable := FT_Load_Glyph(face _ handle, glyphIndex, 0).
        convertError(error) ifTrue: {
            return: GlyphRef nil
        }.

        let glyphSlot := face _ handle _ glyph.
        error := FT_Render_Glyph(glyphSlot, FT_Render_Mode Normal).
        convertError(error) ifTrue: {
            return: GlyphRef nil
        }.

        let glyphBitmap ref := glyphSlot _ bitmap.

        ## Create glyph form.
        let form := Form extent: UInt32x2(glyphBitmap width, glyphBitmap rows) pixelFormat: PixelFormat A8_UNorm.
        {
            let destRow mutable := form makeBlitter makeUnsafePixelIterator.
            let sourceRow mutable := glyphBitmap buffer reinterpretCastTo: UInt8 pointer.
            let sourcePitch := glyphBitmap pitch.
            0 until: glyphBitmap rows do: {:(UInt32)y :: Void |
                Std memcpy(destRow pixels, sourceRow, glyphBitmap width).
                destRow advanceRow.
                sourceRow := sourceRow + sourcePitch
            }.
        }.

        let metrics ref := glyphSlot _ metrics.
        BitmapGlyph new
            extent: Float32x2(convert26d6(metrics width), convert26d6(metrics height));
            drawOrigin: Float32x2(glyphSlot _ bitmap_left, glyphSlot _ bitmap_top negated);
            horizontalBearing: Float32x2(convert26d6(metrics horiBearingX), convert26d6(metrics horiBearingY));
            horizontalAdvance: Float32x2(convert26d6(metrics horiAdvance), 0.0f);

            verticalBearing: Float32x2(convert26d6(metrics vertBearingX), convert26d6(metrics vertBearingY));
            verticalAdvance: Float32x2(0.0f, convert26d6(metrics vertAdvance));

            form: form;
            
            upCastFor: Glyph
    }.

    inline method getOrCreateGlyphForIndex: (index: UInt32) ::=> GlyphRef := {
        glyphIndexDictionary at: index ifAbsentPut: { :: GlyphRef |
            self createGlyphForIndex: index
        }
    }.

    override method getOrCreateGlyphForCodePoint: (codePoint: Char32) ::=> GlyphRef := {
        glyphCodePointDictionary at: codePoint ifAbsentPut: { :: GlyphRef |
            self getOrCreateGlyphForIndex: FT_Get_Char_Index(face _ handle, codePoint)
        }
    }.

    override method getKerningForCharacter: (leftCharacter: Char32) with: (rightCharacter: Char32) ::=> Float32x2 := {
        ## TODO: Cache these values in a dictionary.
        let delta mutable type: FT_Vector.
        let previousIndex := FT_Get_Char_Index(face _ handle, leftCharacter).
        previousIndex ~= 0 ifTrue: {
            let index := FT_Get_Char_Index(face _ handle, rightCharacter).
            index ~= 0 ifTrue: {
                FT2Library defaultInstance mutex withLock: {
                    FT_Get_Kerning(face _ handle, previousIndex, index, FT_Kerning_Mode Unfitted, delta address).
                }.
            }
        }.

        Float32x2(convert26d6(delta x), 0.0f)
    }
}.

compileTime constant FT2FontFaceWithSizeRef := FT2FontFaceWithSize rcRef.

}. ## End of namespace Text
}. ## End of namespace Graphics
}. ## End of namespace Std
