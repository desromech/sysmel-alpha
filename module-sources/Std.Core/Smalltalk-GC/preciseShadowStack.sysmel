namespace Std definition: {
namespace GC definition: {

struct ShadowStackRecord definition: {
    public field rootCount type: UIntPointer.
    public field roots type: Void pointer pointer.

    public field previousRecord type: ShadowStackRecord pointer.

    const method rootsDo: (block: RootIterationBlock) ::=> Void := {
        <nogc>
        let position mutable := self address.
        while: position isNotNil do: {
            ##Std stdout << position << " " << position _ rootCount; nl.
            0 until: position _ rootCount do: {:i :: Void |
                block(position _ roots[i] address reinterpretCastTo: Std Reflection GCObjectHeader pointer pointer)
            }.
            position := position _ previousRecord
        }.
    }.
}.

class NativeCollector definition: {
    field activeShadowStacks type: Std Collections Vector(ShadowStackRecord pointer).

    method allStackRootsDo: (block: RootIterationBlock) ::=> Void := {
        <nogc>
        Std GC shadowStackLastRecord ifNotNil: {:record :: Void |
            activeShadowStacks add: record
        }.

        activeShadowStacks do: {:each :: Void |
            each _ rootsDo: block
        }.

        activeShadowStacks removeAll
    }.
}.

compileTime if: Compiler compilationTarget isEmbeddedPAL then: {
    method shadowStackLastRecord => ShadowStackRecord pointer ref := {
        <nogc>
        (EmbeddedPAL sysmel_epal_tls_shadowStackLastRecord() reinterpretCastTo: ShadowStackThreadRecord pointer) _.
    }.
} else: {
    global shadowStackLastRecord mutable threadLocal type: ShadowStackRecord pointer.
}.

inline function __sysmel_gc_safePoint externC() ::=> Void := {
    <nogc>
    ##Std stdout << "__sysmel_gc_safePoint"; nl.
}.

inline function __sysmel_gc_shadowStackEnter externC(record: ShadowStackRecord pointer) ::=> Void := {
    <nogc>
    let shadowStackLastRecord ref := Std GC shadowStackLastRecord.
    Std assert: shadowStackLastRecord isNil || shadowStackLastRecord > record.

    record _ previousRecord: shadowStackLastRecord.
    shadowStackLastRecord := record.
    ##__sysmel_gc_safePoint().
}.

inline function __sysmel_gc_shadowStackLeave externC(record: ShadowStackRecord pointer) ::=> Void := {
    <nogc>
    let shadowStackLastRecord ref := Std GC shadowStackLastRecord.
    let newLastRecord := record _ previousRecord.

    Std assert: newLastRecord isNil || newLastRecord > shadowStackLastRecord.
    shadowStackLastRecord := newLastRecord.

    ##__sysmel_gc_safePoint().
}.

inline function __sysmel_gc_shadowStackLandingPad externC(record: ShadowStackRecord pointer) ::=> Void := {
    <nogc>
    let shadowStackLastRecord ref := Std GC shadowStackLastRecord.

    ## The unwind process is going to do part of this job, but it will
    ## overshoot a bit in case the CFA is used instead of the stack pointer
    ## because the CFA is above the shadow stack record.
    Std assert: shadowStackLastRecord isNil || shadowStackLastRecord >= record.
    shadowStackLastRecord := record.

    ##__sysmel_gc_safePoint().
}.

inline function shadowStackPrepareUnwindTo(targetSP: Void pointer) ::=> Void := {
    <nogc>
    let deletedRecord := targetSP reinterpretCastTo: ShadowStackRecord pointer.
    let shadowStackLastRecord ref := Std GC shadowStackLastRecord.
    while: shadowStackLastRecord isNotNil && shadowStackLastRecord < deletedRecord do: {
        shadowStackLastRecord := shadowStackLastRecord _ previousRecord.
    }.
}.

inline function getShadowStackContextMemento() => Void pointer
    := Std GC shadowStackLastRecord.

inline function restoreShadowStackContextMemento(memento: Void pointer) => Void := {
    Std GC shadowStackLastRecord := memento reinterpretCastTo: ShadowStackRecord pointer
}.

Compiler compilationTarget managedObjectModel
    gcShadowStackRecordStructure: ShadowStackRecord;
    gcShadowStackEnterFunction: __sysmel_gc_shadowStackEnter;
    gcShadowStackLeaveFunction: __sysmel_gc_shadowStackLeave;
    gcShadowStackLandingPadFunction: __sysmel_gc_shadowStackLandingPad;
    gcSafePointFunction: __sysmel_gc_safePoint.

}. ## End of namespace GC
}. ## End of namespace Std
