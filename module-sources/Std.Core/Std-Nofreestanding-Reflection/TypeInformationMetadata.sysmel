namespace Std definition: {
namespace Reflection definition: {

TypeInformationMetadata definition: {
    field subclasses type: Std Collections Vector(TypeInformationMetadata pointer).
    
    override method initialize => Void := {
        superclass ifNotNil: {
            superclass _ addSubclass: self address
        }
    }.

    method addSubclass: (subclass: TypeInformationMetadata pointer) ::=> Void := {
        subclasses add: subclass
    }.

    const method subclasses => TypeInformationMetadata pointer const arraySlice
        := subclasses asArraySlice.
        
    const method copyValueIntoNativeHeap: (value: Void const pointer) ::=> Void pointer := {
        let result := zmalloc(instanceDataSize).
        result ifNil: {
            return: nil
        }.

        self copyValue: value intoRecentlyAllocatedStorage: result.
        result
    }.

    const method moveValueIntoNativeHeap: (value: Void pointer) ::=> Void pointer := {
        let result := zmalloc(instanceDataSize).
        result ifNil: {
            return: nil
        }.

        self moveValue: value intoRecentlyAllocatedStorage: result.
        result
    }.

    const method copyValue: (value: Void const pointer) intoRecentlyAllocatedStorage: (storage: Void pointer) ::=> Void := {
        basicInitializationFunctionPointer ifNotNil: {
            basicInitializationFunctionPointer(storage)
        }.

        copyConstructorFunctionPointer ifNil: {
            memcpy(storage, value, instanceDataSize)
        } ifNotNil: {
            copyConstructorFunctionPointer(storage, value)
        }
    }.

    const method moveValue: (value: Void pointer) intoRecentlyAllocatedStorage: (storage: Void pointer) ::=> Void := {
        basicInitializationFunctionPointer ifNotNil: {
            basicInitializationFunctionPointer(storage)
        }.

        moveConstructorFunctionPointer ifNil: {
            copyConstructorFunctionPointer ifNil: {
                memcpy(storage, value, instanceDataSize)
            } ifNotNil: {
                copyConstructorFunctionPointer(storage, value)
            }
        } ifNotNil: {
            moveConstructorFunctionPointer(storage, value)
        }
    }.
}.

}. ## End of namespace Reflection
}. ## End of namespace Std
