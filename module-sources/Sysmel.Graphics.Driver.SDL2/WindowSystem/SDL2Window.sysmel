namespace Stdn definition: {
namespace Graphics definition: {
namespace GUI definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics Driver SoftRenderer.
useNamespace: SDL2.

class SDL2WindowSystem.

class SDL2Window superclass: Window; definition: {
    public field handle type: SDL_Window pointer.
    field windowSystem type: SDL2WindowSystem pointer.
    public field isFullscreen type: Boolean8.
    protected field currentCursor type: Cursor sharedPointer.
    protected field renderingDevice type: RenderingDevicePtr.
    protected field activeSwapChain type: SwapChainPtr.
    protected field softSceneRenderer type: WindowScene2DRendererPtr.

    override method initialize => Void := {
        currentCursor := SystemCursor arrow upCastFor: Cursor.
    }.

    override method finalize => Void := {
        handle ifNotNil: {
            self close.
        }.
    }.

    method setWindowSystem: (owner: SDL2WindowSystem pointer) ::=> Void := {
        windowSystem := owner
    }.

    override method windowSystem => WindowSystemRef
        := windowSystem _ asSharedPointer upCastFor: WindowSystem.

    override method extent => UInt32x2 := {
        let w mutable type: Int32.
        let h mutable type: Int32.
        SDL_GetWindowSize(handle, w address, h address).
        Int32x2(w, h) castTo: UInt32x2
    }.

    override method close => Void := {
        renderingDevice ifNotNil: {
            renderingDevice _ waitForIdle
        }.

        ## Destroy the active swap chain.
        activeSwapChain ifNotNil: {
            activeSwapChain _ destroy.
        }.

        ## Destroy the active soft scene renderer.
        softSceneRenderer ifNotNil: {
            softSceneRenderer _ destroy.
            softSceneRenderer reset.
        }.

        handle ifNotNil: {
            ## Remove myself from the window dictionary before actually destroying the window.
            windowSystem ifNotNil: {
                windowSystem _ windowDestroyed: self address.
                windowSystem := nil.
            }.

            SDL_DestroyWindow(handle).

            handle := nil.
        }.
    }.

    override method renderingDevice => RenderingDevice sharedPointer := {
        renderingDevice ifNil: {
            renderingDevice := RenderingDriverRegistry defaultDriver _ getMainDevice
        }.

        renderingDevice
    }.

    override method renderingDevice: (theRenderingDevice: RenderingDevice sharedPointer) ::=> Void := {
        renderingDevice := theRenderingDevice
    }.

    override method createSwapChain: (swapChainCreationInfo: SwapChainCreationInfo const ref) for: (renderingQueue: CommandQueuePtr const ref) ::=> SwapChainPtr := {
        let adaptedCreationInfo mutable := swapChainCreationInfo.

        adaptedCreationInfo extent = UInt32x2 zeros ifTrue: {
            adaptedCreationInfo extent: self extent.
        }.

        ## Get the rendering device.
        self renderingDevice _ isSoftRenderingDevice ifTrue: {
            let result := SDL2SoftwareRendererSwapChain for: self.
            (result _ initializeWith: adaptedCreationInfo for: renderingQueue on: (renderingDevice getPointer castTo: SoftRenderingDevice pointer) _) ifFalse: {
                return: SwapChainPtr nil
            }.

            return: (result upCastFor: SwapChain).
        }.

        adaptedCreationInfo presentationMode = SwapChainPresentationMode Default ifTrue: {
            vsyncDisabled ifTrue: {
                adaptedCreationInfo presentationMode: SwapChainPresentationMode Mailbox
            } ifFalse: {
                adaptedCreationInfo presentationMode: SwapChainPresentationMode Fifo
            }
        }.

        adaptedCreationInfo fallbackPresentationMode = SwapChainPresentationMode Default ifTrue: {
            vsyncDisabled ifTrue: {
                adaptedCreationInfo fallbackPresentationMode: SwapChainPresentationMode Immediate
            } ifFalse: {
                adaptedCreationInfo fallbackPresentationMode: SwapChainPresentationMode Fifo
            }
        }.

        ## Get the native window handle.
        let windowInfo mutable type: SDL_SysWMinfo.
        windowInfo version setCurrent.
        SDL_GetWindowWMInfo(handle, windowInfo address).

        ## Setup the swap chain creation.
        windowInfo subsystem selectCase: #{
        SDL_SYSWM_WINDOWS : {
            adaptedCreationInfo
                windowSystemName: "win32" asMutableString;
                window: windowInfo info win window.
        }.
        SDL_SYSWM_X11 : {
            adaptedCreationInfo
                windowSystemName: "xlib" asMutableString;
                display: windowInfo info x11 display;
                window: windowInfo info x11 window.
        }.
        SDL_SYSWM_COCOA : {
            adaptedCreationInfo
                windowSystemName: "cocoa" asMutableString;
                window: windowInfo info cocoa window.
        }.
        _ : {
            Stdn stderr << "Unsupported window system"; nl.
            return: SwapChainPtr nil
        }.
        }.

        ## Supply the old swap chain, and make sure it is not on active use anymore.
        activeSwapChain ifNotNil: {
            adaptedCreationInfo oldSwapChain: activeSwapChain.
            renderingDevice _ waitForIdle.
        }.

        let newActiveSwapChain := renderingDevice _ createSwapChain: adaptedCreationInfo for: renderingQueue.
        activeSwapChain ifNotNil: {
            activeSwapChain _ destroy
        }.
        activeSwapChain := newActiveSwapChain
    }.

    override method createScene2DRenderer => WindowScene2DRendererPtr := {
        self renderingDevice _ isSoftRenderingDevice ifTrue: {
            softSceneRenderer ifNotNil: {
                softSceneRenderer _ destroy.
                softSceneRenderer reset
            }.

            softSceneRenderer := (SDL2Software2DSceneRenderer for: self) upCastFor: WindowScene2DRenderer.
            return: softSceneRenderer.
        }.

        (WindowSwapChain2DSceneRenderer for: (self asSharedPointer upCastFor: Window)) upCastFor: WindowScene2DRenderer
    }.

    override method grabMouseCapture => Void := {
        SDL_CaptureMouse(true)
    }.

    override method releaseMouseCapture => Void := {
        SDL_CaptureMouse(false)
    }.

    override method fullscreen => Boolean8
        := isFullscreen.

    override method fullscreen: (newValue: Boolean8) ::=> Void := {
        newValue ifTrue: {
            SDL_SetWindowFullscreen(handle, SDL_WINDOW_FULLSCREEN_DESKTOP).
        } ifFalse: {
            SDL_SetWindowFullscreen(handle, 0).
        }.

        isFullscreen := newValue
    }.

    override method toggleFullscreen => Void
        := self fullscreen: self fullscreen not.

    override method setCursor: (cursor: Cursor sharedPointer const ref) ::=> Void := {
        currentCursor == cursor ifTrue: {
            return: void
        }.

        currentCursor := cursor.
        self applyCurrentCursor.
    }.

    method applyCurrentCursor => Void := {
        currentCursor isNil || currentCursor _ isFullyTransparent ifTrue: {
            SDL_ShowCursor(SDL_DISABLE).
        } ifFalse: {
            SDL_ShowCursor(SDL_ENABLE).
            let convertedCursor := windowSystem _ getConvertedCursorFor: currentCursor.
            SDL_SetCursor(convertedCursor).
        }
    }.

}.

}. ## End of namespace GUI
}. ## End of namespace Graphics
}. ## End of namespace Stdn
