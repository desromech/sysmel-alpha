namespace Stdn definition: {
namespace Math definition: {
namespace Geometry definition: {

sealed template GenericRay3d(ST: Type)
    := class superclass: GenericGeometricShape(ST); definition: {

    public field origin type: Vector3.
    public field direction type: Vector3.
    public field inverseDirection type: Vector3.

    public field tmin type: Scalar.
    public field tmax type: Scalar.

    meta definition: {
        inline method origin: (theOrigin: Vector3) direction: (theDirection: Vector3) tmin: (theTMin: Scalar) tmax: (theTMax: Scalar)::=> InstanceType
            := InstanceType basicNewValue origin:
                theOrigin;
                direction: theDirection;
                inverseDirection: 1 / theDirection;
                tmin: theTMin;
                tmax: theTMax;
                yourself.

        inline method origin: (theOrigin: Vector3) direction: (theDirection: Vector3) tmax: (theTMax: Scalar)::=> InstanceType
            := self origin: theOrigin direction: theDirection tmin: 0 tmax: theTMax.

        inline method origin: (theOrigin: Vector3) direction: (theDirection: Vector3) ::=> InstanceType
            := self origin: theOrigin direction: theDirection tmin: 0 tmax: Scalar infinity.

        inline method from: (startPoint: Vector3) to: (endPoint: Vector3) ::=> InstanceType := {
            let vector := endPoint - startPoint.
            let vectorLength := vector length.
            let direction := vector / (vectorLength ~= 0 ifTrue: vectorLength ifFalse: 1).
            InstanceType origin: startPoint direction: direction tmin: 0 tmax:  vectorLength.
        }.
    }.

    const inline method pointAtDistance: (distance: Scalar) ::=> Vector3
        := origin + distance*direction.
}.

compileTime constant Ray3dF32 := GenericRay3d(Float32).
compileTime constant Ray3dF64 := GenericRay3d(Float64).

} ## End of namespace Geometry
} ## End of namespace Math
} ## End of namespace Stdn
