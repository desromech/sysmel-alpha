namespace Stdn definition: {
namespace Serialization definition: {
namespace JSON definition: {

template Parser(RT: Type)
 := class definition: {
    compileTime constant ResultType := RT.
    compileTime constant DefaultResult := RT defaultValue.

    class Callback definition: {
        virtual method beginArray ::=> Void
            := void.

        virtual method endArray ::=> ResultType
            := DefaultResult.

        virtual method beginObject ::=> Void
            := void.

        virtual method endObject ::=> ResultType
            := DefaultResult.

        virtual method onInteger: (value: Int64) ::=> ResultType
            := DefaultResult.

        virtual method onUnsignedInteger: (value: UInt64) ::=> ResultType
            := DefaultResult.

        virtual method onNil ::=> ResultType
            := DefaultResult.

        virtual method onBoolean: (value: Boolean8) ::=> ResultType
            := DefaultResult.

        virtual method onFloat: (value: Float64) ::=> ResultType
            := DefaultResult.

        virtual method onString: (string: Stdn String const ref) ::=> ResultType
            := DefaultResult.

        virtual method onError ::=> ResultType
            := DefaultResult.

    }.

    public field callback type: Callback pointer.
    public field tokenStream type: TokenStream.
    field currentToken type: TokenType.

    method beginReadingStream: (stream: Stdn IO Stream pointer) ::=> Void := {
        tokenStream := TokenStream for: stream.
    }.

    method parseNextArray => ResultType := {
        callback _ onError
    }.

    method parseNextObject => ResultType := {
        callback _ onError
    }.

    method nextToken => TokenType := {
        currentToken = TokenType None ifTrue: {
            currentToken := tokenStream next
        }.

        currentToken
    }.

    method advanceToken => TokenType := {
        currentToken := TokenType None
    }.

    method parseNextValue => ResultType := {
        self nextToken selectCase: #{
        (TokenType Integer , TokenType HexInteger) asValueInSetPattern : {
            tokenStream text first = '-' ifTrue: {
                let value := Stdn parseInt64(tokenStream text data, tokenStream text size).
                self advanceToken.
                callback _ onInteger: value
            } ifFalse: {
                let value := Stdn parseUInt64(tokenStream text data, tokenStream text size).
                self advanceToken.
                callback _ onUnsignedInteger: value
            }
        }.

        TokenType Float : {
            let value := Stdn parseFloat64(tokenStream text data, tokenStream text size).
            self advanceToken.
            callback _ onFloat: value
        }.

        TokenType String : {
            let stringCopy := tokenStream text.
            self advanceToken.
            callback _ onString: stringCopy
        }.

        TokenType LeftBracket : {
            self parseNextArray.
        }.

        TokenType LeftCurlyBracket : {
            self parseNextObject.
        }.

        TokenType Identifier : {
            tokenStream text selectCase: #{
            "nil" asMutableString : {
                self advanceToken.
                callback _ onNil
            }.
            "true" asMutableString : {
                self advanceToken.
                callback _ onBoolean: true
            }.
            "false" asMutableString : {
                self advanceToken.
                callback _ onBoolean: false
            }.
            _ : {
                callback _ onError
            }.
            }
        }.

        _ : {callback _ onError}
        }.
    }.

}.

}. ## End of namespace JSON
}. ## End of namespace Serialization
}. ## End of namespace Stdn
