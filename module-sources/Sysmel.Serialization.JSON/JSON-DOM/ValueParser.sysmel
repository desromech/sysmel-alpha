namespace Stdn definition: {
namespace Serialization definition: {
namespace JSON definition: {

useNamespace: Stdn Serialization DOM.

compileTime constant ValueParser := Parser(Value).

class ValueParserCallback superclass: ValueParser Callback; definition: {

    override method beginArray ::=> Value
        := List sharedNew.

    override method endArray: (theArray: Value ref) ::=> Void
        := void.

    override method putValue: (theValue: Value const ref) onArray: (theArray: Value ref) ::=> Void
        := (theArray get: ListPtr) _ elements add: theValue.

    override method beginObject ::=> Value
        := Object sharedNew.

    override method endObject: (theObject: Value ref) ::=> Void
        := void.

    override method putKey: (key: Stdn String const ref) value: (theValue: Value const ref) onObject: (theObject: Value ref) ::=> Void
        := (theObject get: ObjectPtr) _ elements at: key put: theValue.

    override method onInteger: (value: Int64) ::=> Value
        := value.

    override method onUnsignedInteger: (value: UInt64) ::=> Value
        := value.

    override method onNil ::=> Value
        := nil.

    override method onBoolean: (value: Boolean8) ::=> Value
        := value.

    override method onFloat: (value: Float64) ::=> Value
        := value.

    override method onString: (value: Stdn String const ref) ::=> Value
        := value.

}.

ValueParser definition: {
    meta definition: {
        method parseString: (string: Stdn String const ref) ::=> Value := {
            let callback mutable type: ValueParserCallback.
            let parser mutable type: ValueParser.
            parser
                callback: callback address;
                beginReadingStream: string readMemoryStream address;
                parseNextValue
        }.

        method parseStream: (stream: Stdn IO Stream pointer) ::=> Value := {
            let callback mutable type: ValueParserCallback.
            let parser mutable type: ValueParser.
            parser
                callback: callback address;
                beginReadingStream: stream;
                parseNextValue
        }.
    }.
}.

}. ## End of namespace JSON
}. ## End of namespace Serialization
}. ## End of namespace Stdn
