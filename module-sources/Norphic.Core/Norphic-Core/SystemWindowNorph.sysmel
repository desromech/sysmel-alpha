namespace Norphic definition: {

useNamespace: Std Graphics Core.
useNamespace: Std Graphics GUI.
useNamespace: Std Math Geometry.

#**
 * I am a root norph that is a wrapper for a window created by a Window system.
 *#
class SystemWindowNorph superclass: RootNorph; definition: {
    protected field windowSystem type: WindowSystemRef.
    protected field window type: WindowRef.
    protected field sceneRenderer type: WindowScene2DRendererRef.
    protected field hasChanged type: Boolean8.

    override method defaultColor => Float32x4
        := self theme _ systemWindowBackground.

    meta definition: {
        method createFor: (windowSystem: WindowSystemRef const ref)
            title: (theTitle: Std String const ref) extent: (theExtent: Int32x2) ::=> InstanceType rcPointer := {

            self createFor: windowSystem creationInfo: (WindowCreationInfo()
                resizable: true;
                extent: theExtent;
                title: theTitle;
                yourself)
        }.

        method createFor: (windowSystem: WindowSystemRef const ref) creationInfo: (creationInfo: WindowCreationInfo const ref) ::=> InstanceType rcPointer := {

            let window := windowSystem createWindow: creationInfo.
            window ifNil: {
                return: InstanceType rcPointer nil
            }.

            ## Create the drawing surface.
            let renderer := window createScene2DRenderer.
            renderer ifNil: {
                return: InstanceType rcPointer nil
            }.

            ## Create the system window norph.
            let result := SystemWindowNorph rcNew.
            result _
                bounds: (RectangleF32 min: Float32x2 zeros extent: (creationInfo extent castTo: Float32x2));
                initializeWithWindowSystem: windowSystem window: window renderer: renderer.

            ## Create the event handler
            window eventHandler: (SystemWindowNorphEventHandler new
                norph: result getPointer;
                upCastFor: EventHandler).

            return: result
        }.
    }.

    inline method initializeWithWindowSystem: (theWindowSystem: WindowSystemRef const ref)
        window: (theWindow: WindowRef const ref)
        renderer: (theRenderer: WindowScene2DRendererRef const ref) ::=> Void := {

        windowSystem := theWindowSystem.
        window := theWindow.
        sceneRenderer := theRenderer.
    }.

    method render => Void := {
        sceneRenderer
            clearColor: self color;
            renderScene: (self buildContentSceneGraphWith: sceneRenderer newSceneBuilder);
            present
    }.

    override method setMouseCaptureNorph: (norph: Norph pointer) ::=> Void := {
        super setMouseCaptureNorph: norph.

        norph ifNil: {
            window releaseMouseCapture
        } ifNotNil: {
            window grabMouseCapture
        }
    }.

    override method destroy => Void := {
        window ifNotNil: {
            window close.
        }.
        window := nil.
        windowSystem := nil.

        super destroy.
    }.

    override method changed => Void := {
        hasChanged := true.
    }.

    inline method onTick => Void := {
        hasChanged ifTrue: {
            hasChanged := false.
            self render
        }
    }.

    override method currentActiveMouseCursorChanged => Void := {
        window setCursor: self mouseFocusNorphCursor
    }.
}.

class SystemWindowNorphEventHandler superclass: EventHandler; definition: {
    public field norph type: SystemWindowNorph pointer.

    override method onTickEvent: (event: TickEvent ref) ::=> Void := {
        norph _ onTick
    }.

    override method onExposeEvent: (event: ExposeEvent ref) ::=> Void := {
        norph _ render
    }.

    override method onWindowMovedEvent: (event: WindowMovedEvent ref) ::=> Void := {
        norph _ movedInto: event newPosition.
    }.

    override method onWindowResizingEvent: (event: WindowResizingEvent ref) ::=> Void := {
        norph _ extentChangedInto: event newSize.
    }.

    override method onWindowSizeChangedEvent: (event: WindowSizeChangedEvent ref) ::=> Void := {
        norph _ extentChangedInto: event newSize.
    }.

    override method onWindowCloseRequestEvent: (event: WindowCloseRequestEvent ref) ::=> Void := {
        norph _ destroy
    }.

    override method onKeyPressedEvent: (event: KeyPressedEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphKeyPressedEvent()
            symbol: event symbol;
            modifiers: event modifiers;
            isRepeat: event isRepeat;
            yourself.
        norph _ dispatchKeyboardEvent: convertedEvent.
    }.

    override method onKeyReleasedEvent: (event: KeyReleasedEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphKeyReleasedEvent()
            symbol: event symbol;
            modifiers: event modifiers;
            isRepeat: event isRepeat;
            yourself.
        norph _ dispatchKeyboardEvent: convertedEvent.
    }.

    override method onTextInputEvent: (event: TextInputEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphTextInputEvent()
            text: event text;
            yourself.
        norph _ dispatchTextInputEvent: convertedEvent.
    }.

    override method onMouseMoveEvent: (event: MouseMoveEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphMouseMoveEvent()
            mouseId: event mouseId;
            position: (event position castTo: Float32x2);
            modifiers: event modifiers;
            buttonState: event buttonState;
            delta: (event delta castTo: Float32x2);
            yourself.
        norph _ dispatchMouseEvent: convertedEvent
    }.

    override method onMouseButtonPressedEvent: (event: MouseButtonPressedEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphMouseButtonPressedEvent()
            mouseId: event mouseId;
            position: (event position castTo: Float32x2);
            modifiers: event modifiers;
            button: event button;
            clickCount: event clickCount;
            yourself.
        norph _ dispatchMouseEvent: convertedEvent
    }.

    override method onMouseButtonReleasedEvent: (event: MouseButtonReleasedEvent ref) ::=> Void := {
        let convertedEvent mutable := NorphMouseButtonReleasedEvent()
            mouseId: event mouseId;
            position: (event position castTo: Float32x2);
            modifiers: event modifiers;
            button: event button;
            clickCount: event clickCount;
            yourself.
        norph _ dispatchMouseEvent: convertedEvent
    }.
}.

}. ## End of namespace Norphic
