namespace Norphic definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Math Geometry.

#**
 * I am a norph that displays a text.
 *#
class StringNorph superclass: Norph; definition: {
    protected field text type: Stdn String.

    method text => Stdn String const ref
        := text.

    method text: (newText: Stdn String const ref) ::=> Void := {
        text := newText.
        self extent: self minimalExtent.
    }.

    override method renderOn: (canvas: Canvas ref) ::=> Void := {
        let fontFaceWithSize ref := self theme _ normalFontFaceWithSize.
        canvas
            ##color: Float32x4(1, 0, 0, 1);
            ##fillRectangle: self localBounds;

            fontFaceWithSize: fontFaceWithSize;
            color: self stringColor;
            drawUTF8String: text at: Float32x2 (0.0f, fontFaceWithSize _ ascender).
    }.

    override method defaultColor => Float32x4
        := self theme _ textColor.

    virtual method stringColor => Float32x4
        := self color.

    override method defaultExtent => Float32x2
        := self minimalContentExtent.

    override method minimalContentExtent => Float32x2 := {
        let fontFaceWithSize ref := self theme _ normalFontFaceWithSize.
        fontFaceWithSize ifNotNil: {
            return: (fontFaceWithSize _ computeExtentForString: text)
        }.

        Float32x2 zeros
    }
}.

}. ## End of namespace Norphic
