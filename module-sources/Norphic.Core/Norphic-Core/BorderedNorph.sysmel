namespace Norphic definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Math Geometry.

#**
 * I am a norph with a borderd
 *#
class BorderedNorph superclass: Norph; definition: {
    protected field borderColor type: Float32x4.

    virtual method borderColor => Float32x4
        := borderColor.
    virtual method focusedBorderColor => Float32x4
        := self borderColor interpolateTo: Float32x4 blue lighter at: 0.5f.

    virtual method borderColor: (theNewColor: Float32x4 const ref) ::=> Void := {
        borderColor := theNewColor.
        self changed.
    }.

    override method initialize => Void := {
        borderColor := self theme _ defaultBorderColor.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilder ref) ::=> NorphSceneNodePtr := {
        (super buildSceneGraphWith: builder) _ --
        ((builder borderRectangle: self localBounds color: self currentBorderColor) upCastFor: NorphSceneNode).
    }.

    virtual method currentBorderColor => Float32x4 := {
        self hasKeyboardFocus ifTrue: {
            self focusedBorderColor
        } ifFalse: {
            self borderColor
        }
    }.

    override method onGotFocusEvent: (event: NorphGotFocusEvent ref) ::=> Void := {
        self changed.
    }.

    override method onLostFocusEvent: (event: NorphLostFocusEvent ref) ::=> Void := {
        self changed.
    }.
}.

}. ## End of namespace Norphic
