namespace Norphic definition: {

useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.

#**
 * I am a root norph that roughly corresponds to a window in a Window system.
 *#
class RootNorph superclass: Norph; definition: {
    protected field mouseFocusNorph type: Norph rcPointer.
    protected field mouseCaptureNorph type: Norph rcPointer.
    protected field keyboardFocusNorph type: Norph rcPointer.
    protected field activePopupLeaf type: PopupNorph rcPointer.

    const override method mouseFocusNorph => Norph pointer
        := mouseFocusNorph getPointer.

    const override method mouseCaptureNorph => Norph pointer
        := mouseCaptureNorph getPointer.

    const override method keyboardFocusNorph => Norph pointer
        := keyboardFocusNorph getPointer.

    const override method rootNorph => RootNorph pointer
        := self address reinterpretCastTo: SelfType pointer.

    const override method rootLocalPosition => Float32x2
        := Float32x2 zeros.

    override method initialize => Void := {

    }.

    override method finalize => Void := {

    }.

    method killAllPopups ::=> Void := {
        let chainStart := activePopupLeaf.
        self killPopupChain: chainStart.
    }.

    method killPopupChain: (popup: PopupNorphPtr const ref) ::=> Void := {
        popup ifNil: {return: void}.

        let parent := popup _ parentPopup.
        self killPopup: popup.
        self killPopupChain: parent
    }.

    method killPopup: (thePopup: PopupNorphPtr const ref) ::=> Void := {
        ## Make a copy in case thePopup points to activePopupLeaf.
        let popup := thePopup.

        popup _ owner ifNil: {
            return: void
        } ifNotNil: {:o :: Void |
            activePopupLeaf = popup ifTrue: {
                activePopupLeaf := popup _ parentPopup
            }.

            o _ removeSubnorph: (popup upCastFor: Norph).
            popup _ killed.
        }.
    }.

    method activatePopup: (popup: PopupNorphPtr const ref) at: (popupPosition: Float32x2) withParent: (newParent: NorphPtr const ref) ::=> Void := {
        popup _ owner ifNotNil: {
            self killPopup: popup
        }.

        activePopupLeaf isNotNil && activePopupLeaf ~= popup ifTrue: {
            self killPopup: activePopupLeaf
        }.

        activePopupLeaf := popup.
        self addSubnorph: (popup upCastFor: Norph).
        popup _
            position: popupPosition;
            activatedWithParent: newParent
    }.

    virtual method setMouseCaptureNorph: (norph: Norph pointer) ::=> Void := {
        norph ifNil: {
            mouseCaptureNorph reset
        } ifNotNil: {
            mouseCaptureNorph := norph _ asRCPointer
        }
    }.

    virtual method currentActiveMouseCursorChanged => Void := {

    }.

    virtual method mouseFocusNorphCursor => CursorRef := {
        mouseFocusNorph ifNotNil: {
            mouseFocusNorph _ currentMouseCursor
        } ifNil: {
            self currentMouseCursor
        }
    }.

    method mouseEventHasFoundItsTarget: (event: NorphMouseEvent ref) ::=> Void := {
        mouseFocusNorph == event targetNorph ifTrue: {
            return: void.
        }.

        mouseFocusNorph ifNotNil: {
            let leaveEvent mutable type: NorphMouseLeaveEvent.
            leaveEvent setFrom: event.
            mouseFocusNorph _ processEvent: leaveEvent
        }.

        mouseFocusNorph := event targetNorph.

        mouseFocusNorph ifNotNil: {
            let enterEvent mutable type: NorphMouseEnterEvent.
            enterEvent setFrom: event.
            mouseFocusNorph _ processEvent: enterEvent.
        }.

        self currentActiveMouseCursorChanged.
    }.


    override method dispatchMouseEventToChildren: (event: NorphMouseEvent ref) ::=> Void := {
        mouseCaptureNorph ifNil: {
            super dispatchMouseEventToChildren: event
        } ifNotNil: {
            let oldPosition := event position.
            try: {
                event position: oldPosition - (mouseCaptureNorph _ globalPosition - self globalPosition).
                mouseCaptureNorph _ dispatchMouseEvent: event
            } finally: {
                event position: oldPosition
            }
        }
    }.

    virtual method setNewKeyboardFocus: (newKeyboardFocus: Norph pointer) ::=> Void := {
        keyboardFocusNorph getPointer == newKeyboardFocus ifTrue: {
            return: void
        }.

        keyboardFocusNorph ifNotNil: {
            let lostFocusEvent mutable type: NorphLostFocusEvent.
            keyboardFocusNorph _ processEvent: lostFocusEvent
        }.

        keyboardFocusNorph := newKeyboardFocus _ asRCPointer.

        keyboardFocusNorph ifNotNil: {
            let gotFocusEvent mutable type: NorphGotFocusEvent.
            keyboardFocusNorph _ processEvent: gotFocusEvent.
        }.
    }.

    override method dispatchKeyboardEvent: (event: NorphKeyboardEvent ref) ::=> Void := {
        keyboardFocusNorph ifNotNil: {
            keyboardFocusNorph _ dispatchKeyboardEvent: event.
            return: void
        }.

        super dispatchKeyboardEvent: event.
    }.

    override method dispatchPropagatedKeyboardEvent: (event: NorphKeyboardEvent ref) ::=> Void := {
        ## Do nothing
    }.

    override method dispatchTextInputEvent: (event: NorphTextInputEvent ref) ::=> Void := {
        keyboardFocusNorph ifNotNil: {
            keyboardFocusNorph _ dispatchTextInputEvent: event.
            return: void
        }.

        super dispatchTextInputEvent: event.
    }.

    override method dispatchPropagatedTextInputEvent: (event: NorphTextInputEvent ref) ::=> Void := {
        ## Do nothing
    }.
}.

compileTime constant RootNorphPtr := RootNorph rcPointer.

}. ## End of namespace Norphic
