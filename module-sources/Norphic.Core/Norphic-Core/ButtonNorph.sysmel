namespace Norphic definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Math Geometry.

#**
 * I am an abstract button widget norph.
 *#
class AbstractButtonMorph superclass: BorderedNorph; definition: {
    protected field isDown type: Boolean8.

    override method defaultColor => Float32x4
        := self theme _ buttonColor.

    method raiseActivatedEvent => Void := {
        Stdn stdout << "TODO: button raiseActivatedEvent"; nl.
    }.

    override method onKeyPressedEvent: (event: NorphKeyPressedEvent ref) ::=> Void := {
        event isRepeat not &&
        event symbol == Stdn Graphics GUI KeySymbol Return ifTrue: {
            isDown := true.
            self changed.
            event wasHandled: true
        }.

        super onKeyPressedEvent: event
    }.

    override method onKeyReleasedEvent: (event: NorphKeyReleasedEvent ref) ::=> Void := {
        event isRepeat not &&
        event symbol == Stdn Graphics GUI KeySymbol Return ifTrue: {
            isDown ifTrue: {
                isDown := false.
                self changed.
                self raiseActivatedEvent
            }.
            event wasHandled: true
        }.

        super onKeyReleasedEvent: event
    }.

    override method onGotFocusEvent: (event: NorphGotFocusEvent ref) ::=> Void := {
        self changed.
    }.

    override method onLostFocusEvent: (event: NorphLostFocusEvent ref) ::=> Void := {
        isDown ifTrue: {
            isDown := false.
        }.
        self changed.
    }.

    override method onMouseEnterEvent: (event: NorphMouseEnterEvent ref) ::=> Void := {
        self changed.
    }.

    override method onMouseLeaveEvent: (event: NorphMouseLeaveEvent ref) ::=> Void := {
        self changed.
    }.

    override method handlesKeyboardEvents => Boolean8
        := true.

    override method onMouseButtonPressedEvent: (event: NorphMouseButtonPressedEvent ref) ::=> Void := {
        super onMouseButtonPressedEvent: event.

        event isLeftButton ifFalse: {
            return: void.
        }.

        isDown := true.
        self changed.
        event wasHandled: true.
        self grabMouseCapture.
    }.

    override method onMouseButtonReleasedEvent: (event: NorphMouseButtonReleasedEvent ref) ::=> Void := {
        super onMouseButtonReleasedEvent: event.

        event isLeftButton ifFalse: {
            return: void.
        }.

        isDown := false.
        self changed.
        event wasHandled: true.
        self releaseMouseCapture.

        (self localBounds includesPoint: event position) ifTrue: {
            self raiseActivatedEvent
        }
    }.

    virtual method currentColor => Float32x4 := {
        self hasMouseFocus ifTrue: {
            self color lighter lighter
        } ifFalse: {
            self color
        }
    }.

    override method renderOn: (canvas: Canvas ref) ::=> Void := {
        let baseColor := self currentColor.
        let topColor mutable := baseColor.
        let bottomColor mutable := baseColor muchDarker.
        isDown ifTrue: {
            Stdn swapValue: topColor with: bottomColor
        }.

        let localBounds := self localBounds.
        canvas
            fillRectangle: self localBounds verticalGradientStart: topColor end: bottomColor;

            color: self currentBorderColor;
            drawRectangle: self localBounds
    }.
}.

#**
 * I am a button widget norph with a text label.
 *#
class SimpleButtonMorph superclass: AbstractButtonMorph; definition: {
    protected field label type: StringNorph sharedPointer.

    method initialize => Void := {
        label := StringNorph sharedNew.

        let labelRef := label upCastFor: Norph.
        self addSubnorph: labelRef.

        let sizer := HorizontalBoxSizer sharedNew.
        sizer _ element: label do: {:constraints :: Void |
            constraints centered; allBorders; borderSize: 2.
        }.

        self
            sizer: (sizer upCastFor: Sizer);
            autolayout: true
    }.

    method text => Stdn String const ref := {
        label _ text.
    }.

    method text: (newLabel: Stdn String const ref) ::=> Void := {
        label _ text: newLabel
    }.
}.

}. ## End of namespace Norphic
