namespace Norphic definition: {

#**
 * I am a simple list view norph.
 * I am just a single column table of strings.
 *#
class ListViewNorph superclass: TableViewNorph; definition: {
    protected field listDataSource type: SimpleListDataSource rcPointer.

    override method defaultColor => Float32x4
        := self theme _ listViewBackground.

    override method initialize => Void := {
        listDataSource := SimpleListDataSource rcNew.
        self dataSource: (listDataSource upCastFor: DataSource).

        self addColumn: (SimpleStringDataSourceElementColumnView rcNew upCastFor: TableViewColumn).
    }.

    const method listDataSource => SimpleListDataSource rcPointer const ref
        := listDataSource.

    method removeAll ::=> Void := {
        listDataSource _ removeAll.
        self changed.
    }.

    method addItem: (item: Std String const ref) ::=> UIntPointer := {
        let result := listDataSource _ addItem: ((SimpleStringDataSourceElement for: item) upCastFor: DataSourceElement).
        self changed.
        result
    }.
}.

compileTime constant ListViewNorphPtr := ListViewNorph rcPointer.

}. ## End of namespace Norphic
