namespace Norphic definition: {

#**
 * I am a button widget norph with a text rcRef.
 *#
class SimpleButtonNorph superclass: AbstractButtonNorph; definition: {
    protected field label type: StringNorph rcRef.

    method initialize => Void := {
        label := StringNorph new.

        self addSubnorph: (label upCastFor: Norph).

        self
            sizer: (HorizontalBoxSizer new
                element: label with: {:(SizerElementRef const ref)constraints :: Void |
                    constraints centered; allBorders; borderSize: 2.
                };
                upCastFor: Sizer);
            autolayout: true
    }.

    method text => Std String const ref := {
        label text.
    }.

    method text: (newLabel: Std String const ref) ::=> Void := {
        label text: newLabel
    }.
}.

compileTime constant SimpleButtonNorphRef := SimpleButtonNorph rcRef.

}. ## End of namespace Norphic
