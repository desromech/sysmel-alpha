namespace Norphic definition: {

useNamespace: Std Graphics Core.

#**
 * I am a menu or menu bar item norph.
 *#
class AbstractMenuItemNorph superclass: Norph; definition: {
    public field menu type: MenuNorph rcRef.

    protected field label type: StringNorph rcRef.

    override method color => Float32x4
        := self theme activeMenuItemHighlightColor.

    method initialize => Void := {
        label := StringNorph new.

        self addSubnorph: (label upCastFor: Norph).

        self sizer: (HorizontalBoxSizer new
            element: label with: {:(SizerElementRef const ref)constraints :: Void |
                constraints centered; allBorders; borderSize: 3.
            };
            upCastFor: Sizer)
    }.

    virtual method isHighlighted => Boolean8
        := false.

    method label => Std String const ref := {
        label text.
    }.

    method label: (newLabel: Std String const ref) ::=> Void := {
        label text: newLabel.
        self fit.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilderRef const ref) ::=> NorphSceneNodeRef := {
        self isHighlighted ifFalse: {
            return: NorphSceneNodeRef nil.
        }.

        super buildSceneGraphWith: builder
    }.
}.

}. ## End of namespace Norphic
