namespace Norphic definition: {

useNamespace: Std Graphics Core.

#**
 * I am a menu or menu bar item norph.
 *#
class AbstractMenuItemNorph superclass: Norph; definition: {
    public field menu type: MenuNorph rcPointer.

    protected field label type: StringNorph rcPointer.

    override method color => Float32x4
        := self theme _ activeMenuItemHighlightColor.

    method initialize => Void := {
        label := StringNorph rcNew.

        let labelRef := label upCastFor: Norph.
        self addSubnorph: labelRef.

        let sizer := HorizontalBoxSizer rcNew.
        sizer _ element: label with: {:constraints :: Void |
            constraints centered; allBorders; borderSize: 3.
        }.

        self sizer: (sizer upCastFor: Sizer)
    }.

    virtual method isHighlighted => Boolean8
        := false.

    method label => Std String const ref := {
        label _ text.
    }.

    method label: (newLabel: Std String const ref) ::=> Void := {
        label _ text: newLabel.
        self fit.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilder ref) ::=> NorphSceneNodePtr := {
        self isHighlighted ifFalse: {
            return: NorphSceneNodePtr nil.
        }.

        super buildSceneGraphWith: builder
    }.
}.

}. ## End of namespace Norphic
