namespace Norphic definition: {

#**
 * I am a menu bar norph.
 *#
class MenuBarNorph superclass: Norph; definition: {
    protected field items type: Stdn Collections Vector(MenuBarItemNorph sharedPointer).
    protected field activeItem type: MenuBarItemNorph pointer.
    protected field changingActiveItem type: Boolean8.

    override method defaultColor => Float32x4
        := self theme _ menuBarBackground.

    override method initialize => Void := {
        self
            sizer: (HorizontalBoxSizer sharedNew upCastFor: Sizer);
            autolayout: true
    }.

    override method ownedPopupKilled: (popup: PopupNorph sharedPointer const ref) ::=> Void := {
        changingActiveItem ifFalse: {
            self itemClicked: nil
        }
    }.

    method addMenu: (label: Stdn String const ref) ::=> MenuNorph sharedPointer := {
        ## Create the menu item
        let menuItem := MenuBarItemNorph sharedNew.
        menuItem _ label: label.
        self addSubnorph: (menuItem upCastFor: Norph).
        items add: menuItem.

        self sizer _
            element: menuItem with: {:constraints :: Void |
            }.

        ## Create the menu itself.
        let menu := MenuNorph sharedNew.
        menuItem _ menu: menu.
        menu
    }.

    method activeItem => MenuBarItemNorph pointer
        := activeItem.

    method itemClicked: (item: MenuBarItemNorph pointer) ::=> Void := {
        changingActiveItem ifTrue: {return: void }.
        changingActiveItem := true.

        try: {
            let newActiveItem := activeItem == item ifTrue: nil ifFalse: item.

            let oldActiveItem := activeItem.
            activeItem := newActiveItem.

            oldActiveItem ifNotNil: {
                oldActiveItem _ deactivated.
            }.
            activeItem ifNotNil: {
                activeItem _ activated.
            }.

        } finally: {
            changingActiveItem := false.
        }.
    }.

    method itemMouseEnter: (item: MenuBarItemNorph pointer) ::=> Void := {
        activeItem isNotNil && item ~~ activeItem ifTrue: {
            self itemClicked: item.
        }
    }.

    macro method addMenu: menuName with: aBlock :=
        ``(`,aBlock __macroInlineBlock: (`,self addMenu: `,menuName) _).
}.

compileTime constant MenuBarNorphPtr := MenuBarNorph sharedPointer.

}. ## End of namespace Norphic
