namespace Norphic definition: {

#**
 * I am a menu bar norph.
 *#
class MenuBarNorph superclass: Norph; definition: {
    protected field items type: Std Collections Vector(MenuBarItemNorph rcRef).
    protected field activeItem type: MenuBarItemNorphRef.
    protected field changingActiveItem type: Boolean8.

    override method defaultColor => Float32x4
        := self theme menuBarBackground.

    override method initialize => Void := {
        self
            sizer: (HorizontalBoxSizer new upCastFor: Sizer);
            autolayout: true
    }.

    override method ownedPopupKilled: (popup: PopupNorph rcRef const ref) ::=> Void := {
        changingActiveItem ifFalse: {
            self itemClicked: MenuBarItemNorphRef nil
        }
    }.

    method addMenu: (label: Std String const ref) ::=> MenuNorph rcRef := {
        ## Create the menu item
        let menuItem := MenuBarItemNorph new
            label: label;
            yourself.
        self addSubnorph: (menuItem upCastFor: Norph).
        items add: menuItem.

        self sizer
            element: menuItem with: {:(SizerElementRef const ref)constraints :: Void |
            }.

        ## Create the menu itself.
        let menu := MenuNorph new.
        menuItem menu: menu.
        menu
    }.

    method activeItem => MenuBarItemNorphRef const ref
        := activeItem.

    method itemClicked: (item: MenuBarItemNorphRef const ref) ::=> Void := {
        changingActiveItem ifTrue: {return: void }.
        changingActiveItem := true.

        try: {
            let newActiveItem := activeItem == item ifTrue: MenuBarItemNorphRef nil ifFalse: item.

            let oldActiveItem := activeItem.
            activeItem := newActiveItem.

            oldActiveItem ifNotNil: {
                oldActiveItem deactivated.
            }.
            activeItem ifNotNil: {
                activeItem activated.
            }.

        } finally: {
            changingActiveItem := false.
        }.
    }.

    method itemMouseEnter: (item: MenuBarItemNorphRef const ref) ::=> Void := {
        activeItem isNotNil && item ~~ activeItem ifTrue: {
            self itemClicked: item.
        }
    }.

    macro method addMenu: menuName with: aBlock :=
        ``(`,aBlock __macroInlineBlock: (`,self addMenu: `,menuName)).
}.

compileTime constant MenuBarNorphRef := MenuBarNorph rcRef.

}. ## End of namespace Norphic
