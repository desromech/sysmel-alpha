namespace Norphic definition: {

#**
 * I am abstract base for text editor widgets in Norphic.
 *#
class AbstractTextEditorNorph superclass: BorderedNorph; definition: {
    protected field textModel type: TextModel rcRef.
    protected field textEditingArea type: TextEditingAreaNorph rcRef.

    override method isClipped => Boolean8
        := true.

    override method initialize => Void := {
        self
            buildLayout;
            createTextModel
    }.

    virtual method buildLayout => Void := {
        self createTextEditingArea.
        self addSubnorph: (textEditingArea upCastFor: Norph).

        self
            sizer: (HorizontalBoxSizer new
                element: textEditingArea with: {:(SizerElementRef const ref)constraints :: Void |
                    constraints expanded; centered; allBorders; proportion: 1.0; borderSize: 4.
                };
                upCastFor: Sizer);
            autolayout: true
    }.

    override method defaultColor => Float32x4
        := self theme textEditorBackground.

    override method handlesKeyboardEvents => Boolean8
        := true.

    const virtual method textModel => TextModel rcRef const ref
        := textModel.

    const method text ::=> Std String := {
        textModel ifNil: {
            Std String ()
        } ifNotNil: {
            textModel text
        }
    }.

    method text: (newText: Std String const ref) ::=> Void := {
        textModel ifNotNil: {
            textModel text: newText
        }
    }.

    virtual method textModel: (aTextModel: TextModel rcRef const ref) ::=> Void := {
        textModel := aTextModel.
        textEditingArea textModel: aTextModel.
    }.

    virtual method createTextEditingArea => Void := {
        textEditingArea := TextEditingAreaNorph new.
    }.

    virtual method createTextModel => Void := {
        self textModel: (SimpleTextModel new
            color: self theme textColor;
            fontFaceWithSize: self theme normalFontFaceWithSize;
            upCastFor: TextModel).
    }.
}.

compileTime constant AbstractTextEditorNorphRef := AbstractTextEditorNorph rcRef.

}. ## End of namespace Norphic
