namespace Norphic definition: {

#**
 * I am abstract base for text editor widgets in Norphic.
 *#
class AbstractTextEditorNorph superclass: BorderedNorph; definition: {
    protected field textModel type: TextModel sharedPointer.
    protected field textEditingArea type: TextEditingAreaNorph sharedPointer.

    override method isClipped => Boolean8
        := true.

    override method initialize => Void := {
        self
            buildLayout;
            createTextModel
    }.

    virtual method buildLayout => Void := {
        self createTextEditingArea.
        self addSubnorph: (textEditingArea upCastFor: Norph).

        let sizer := HorizontalBoxSizer sharedNew.
        sizer _ element: textEditingArea with: {:constraints :: Void |
            constraints expanded; centered; allBorders; proportion: 1.0; borderSize: 4.
        }.

        self
            sizer: (sizer upCastFor: Sizer);
            autolayout: true
    }.

    override method defaultColor => Float32x4
        := self theme _ textEditorBackground.

    override method handlesKeyboardEvents => Boolean8
        := true.

    const virtual method textModel => TextModel sharedPointer const ref
        := textModel.

    const method text ::=> Stdn String := {
        textModel ifNil: {
            Stdn String ()
        } ifNotNil: {
            textModel _ text
        }
    }.

    method text: (newText: Stdn String const ref) ::=> Void := {
        textModel ifNotNil: {
            textModel _ text: newText
        }
    }.

    virtual method textModel: (aTextModel: TextModel sharedPointer const ref) ::=> Void := {
        textModel := aTextModel.
        textEditingArea _ textModel: aTextModel.
    }.

    virtual method createTextEditingArea => Void := {
        textEditingArea := TextEditingAreaNorph sharedNew.
    }.

    virtual method createTextModel => Void := {
        let simpleTextModel := SimpleTextModel sharedNew.
        simpleTextModel _
            color: self theme _ textColor;
            fontFaceWithSize: self theme _ normalFontFaceWithSize.

        self textModel: (simpleTextModel upCastFor: TextModel).
    }.
}.

compileTime constant AbstractTextEditorNorphPtr := AbstractTextEditorNorph sharedPointer.

}. ## End of namespace Norphic
