namespace Norphic definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics GUI.
useNamespace: Stdn Math Geometry.

#**
 * I am norph whose task is to displaying a text model.
 *#
class TextModelNorph superclass: Norph; definition: {
    protected field textModel type: TextModel sharedPointer.

    const virtual method textModel => TextModel sharedPointer const ref
        := textModel.

    virtual method textModel: (theTextModel: TextModel sharedPointer const ref) ::=> Void := {
        textModel := theTextModel.
        self textModelChanged.
    }.

    virtual method textModelChanged => Void := {
        self changed.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilder) ::=> NorphSceneNodePtr := {
        textModel ifNil: { return: NorphSceneNodePtr nil }.

        let container := builder container.

        textModel _
            textSpansInRectangle: self localBounds do: {:(TextModelSpan ref)span :: Void |
                let spanNode := builder text: span utf8String in: span rectangle
                    font: span fontFaceWithSize color: span color drawingState: span textDrawingState.
                container _ add: (spanNode upCastFor: NorphSceneNode)
            }.

        container upCastFor: NorphSceneNode
    }.
}.

}. ## End of namespace Norphic
