namespace Norphic definition: {

useNamespace: Std Graphics Core.
useNamespace: Std Graphics GUI.
useNamespace: Std Math Geometry.

#**
 * I am norph whose task is to displaying a text model.
 *#
class TextModelNorph superclass: Norph; definition: {
    protected field textModel type: TextModel rcRef.

    const virtual method textModel => TextModel rcRef const ref
        := textModel.

    virtual method textModel: (theTextModel: TextModel rcRef const ref) ::=> Void := {
        textModel := theTextModel.
        self textModelChanged.
    }.

    virtual method textModelChanged => Void := {
        self changed.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilderRef const ref) ::=> NorphSceneNodeRef := {
        textModel ifNil: { return: NorphSceneNodeRef nil }.

        let container := builder container.

        textModel
            textSpansInRectangle: self localBounds do: {:(TextModelSpan ref)span :: Void |
                let spanNode := builder text: span utf8String in: span rectangle
                    font: span fontFaceWithSize color: span color drawingState: span textDrawingState.
                container add: spanNode asNode
            }.

        container asNode
    }.
}.

}. ## End of namespace Norphic
