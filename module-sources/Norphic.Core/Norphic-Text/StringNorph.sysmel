namespace Norphic definition: {

useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.

#**
 * I am a norph that displays a text.
 *#
class StringNorph superclass: Norph; definition: {
    protected field text type: Std String.

    method text => Std String const ref
        := text.

    method text: (newText: Std String const ref) ::=> Void := {
        text := newText.
        self extent: self minimalExtent.
    }.

    override method buildSceneGraphWith: (builder: NorphSceneBuilderRef const ref) ::=> NorphSceneNodeRef := {
        let fontFaceWithSize ref := self theme normalFontFaceWithSize.
        (builder text: text asArraySlice in: self localBounds font: fontFaceWithSize color: self stringColor) asNode
    }.

    override method defaultColor => Float32x4
        := self theme textColor.

    virtual method stringColor => Float32x4
        := self color.

    override method defaultExtent => Float32x2
        := self minimalContentExtent.

    override method minimalContentExtent => Float32x2 := {
        let fontFaceWithSize ref := self theme normalFontFaceWithSize.
        fontFaceWithSize ifNotNil: {
            return: (fontFaceWithSize computeExtentForString: text)
        }.

        Float32x2 zeros
    }.

    override method handlesMouseEvents => Boolean8
        := false.

    override method handlesKeyboardEvents => Boolean8
        := false.
}.

compileTime constant StringNorphRef := StringNorph rcRef.

}. ## End of namespace Norphic
