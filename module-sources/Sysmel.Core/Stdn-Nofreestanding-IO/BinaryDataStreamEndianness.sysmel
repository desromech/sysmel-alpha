namespace Stdn definition: {
namespace IO definition: {

#**
 * Binary data stream endianness mode.
 *#
enum BinaryDataStreamEndianness valueType: Int32; values: #{
    ## No byte swapping.
    Native: 0.

    ## Little endian encoding.
    Little: 1.

    ## Big endian encoding.
    Big: 2.

    ## The opposite endianness to this architecture.
    Opposite: (if: Compiler compilationTarget isLittleEndian then: Big else: Little).
}.

function swapUInt16(value: UInt16) => UInt16
    := ((value & 16rff00u) >> 8u) |
        ((value & 16r00ffu) << 8u) castTo: UInt16.

function swapUInt32(value: UInt32) => UInt32
    := ((value & 16rff000000u) >> 24u) |
       ((value & 16r00ff0000u) >> 8u) |
       ((value & 16r0000ff00u) << 8u) |
       ((value & 16r000000ffu) << 24u).

function swapUInt64(value: UInt64) => UInt64
   := ((value & 16rff00000000000000u) >> 56u) |
      ((value & 16r00ff000000000000u) >> 40u) |
      ((value & 16r0000ff0000000000u) >> 24u) |
      ((value & 16r000000ff00000000u) >> 8u) |
      ((value & 16r00000000ff000000u) << 8u) |
      ((value & 16r0000000000ff0000u) << 24u) |
      ((value & 16r000000000000ff00u) << 40u) |
      ((value & 16r00000000000000ffu) << 56u).

}. ## End of namespace IO
}. ## End of namespace Stdn
