namespace Stdn definition: {
useNamespace: Win32.

namespace IO definition: {

#**
 * File stream class.
 *#
class UnbufferedFileStream superclass: Stream;
definition: {
    public field handle type: HANDLE.
    public field isConsole type: Boolean8.
    public field isOwner type: Boolean8.
    private field eofDetected_ private type: Boolean8.

    meta method fromHandle: (handle: HANDLE) isOwner: (isOwner: Boolean8) ::=> SelfType
        := SelfType basicNewValue
            handle: handle;
            isConsole: GetFileType(handle) = FILE_TYPE_CHAR;
            isOwner: isOwner;
            yourself.

    override method finalize => Void
        := self close.

    method close => Void := {
        isOwner && handle isNotNil ifTrue: {
            CloseHandle(handle).
        }.
        handle := nil.
    }.

    method open: (path: CTypes ConstString) mode: (mode: FileOpenMode) permissions: (permissions: FilePermissions) ::=> Boolean8 := {
        let desiredAccess mutable type: DWORD := 0.
        let creationDisposition mutable type: DWORD := 0.
        let flagsAndAttributes mutable type: DWORD := 0.

        (mode anyMask: FileOpenMode Write) || (mode anyMask: FileOpenMode Append) ifTrue: {
            (mode anyMask: FileOpenMode Existent) ifTrue: {
                (mode anyMask: FileOpenMode Truncate) ifTrue: {
                    creationDisposition := TRUNCATE_EXISTING
                } ifFalse: {
                    creationDisposition := OPEN_EXISTING
                }.
            } ifFalse: {
                (mode anyMask: FileOpenMode Truncate) ifTrue: {
                    creationDisposition := CREATE_ALWAYS
                } ifFalse: {
                    creationDisposition := CREATE_NEW
                }.
            }.

            desiredAccess := GENERIC_WRITE.
            (mode anyMask: FileOpenMode Read) ifTrue: {
                desiredAccess := desiredAccess | GENERIC_READ
            }.
        } ifFalse: {
            (mode anyMask: FileOpenMode Read) ifFalse: {
                return: false.
            }.

            desiredAccess := GENERIC_READ.
            creationDisposition := OPEN_EXISTING.
        }.

        flagsAndAttributes := FILE_ATTRIBUTE_NORMAL.
        (permissions anyMask: FilePermissions OwnerWrite) ifFalse: {
            flagsAndAttributes := FILE_ATTRIBUTE_READONLY
        }.

        handle := CreateFileA(path, desiredAccess, FILE_SHARE_DELETE | FILE_SHARE_READ | FILE_SHARE_WRITE, nil, creationDisposition, flagsAndAttributes, nil).
        handle == nil ifTrue: {return: false}.

        isConsole := GetFileType(handle) = FILE_TYPE_CHAR.
        isOwner := true.

        ## If we are appending, move the cursor until the end.
        (mode anyMask: FileOpenMode Append) ifTrue: {
            SetFilePointerEx(handle, LARGE_INTEGER(), nil, FILE_END)
        }.

        true
    }.

    method open: (path: String const ref) mode: (mode: FileOpenMode) permissions: (permissions: FilePermissions) ::=> Boolean8
        := self open: path asCString mode: mode permissions: permissions.

    method open: (path: CTypes ConstString) mode: (mode: FileOpenMode) ::=> Boolean8
        := self open: path mode: mode permissions: FilePermissions DefaultFilePermissions.

    method open: (path: String const ref) mode: (mode: FileOpenMode) ::=> Boolean8
        := self open: path asCString mode: mode permissions: FilePermissions DefaultFilePermissions.

    method open: (path: Char8 const arraySlice) mode: (mode: FileOpenMode) permissions: (permissions: FilePermissions) ::=> Boolean8
        := self open: path asMutableString mode: mode permissions: permissions.

    method open: (path: Char8 const arraySlice) mode: (mode: FileOpenMode) ::=> Boolean8
        := self open: path asMutableString mode: mode permissions: FilePermissions DefaultFilePermissions.

    method isValid => Boolean8
        := handle ~= nil.

    override method atEnd => Boolean8
        := eofDetected_.

    override method position => Int64 := {
        let newFilePointer mutable type: LARGE_INTEGER.
        SetFilePointerEx(handle, LARGE_INTEGER() QuadPart: 0; yourself, newFilePointer address, FILE_CURRENT) = 0 ifTrue: {
            0
        } ifFalse: {
            newFilePointer QuadPart castTo: Int64
        }
    }.

    override method seek: (offset: Int64) mode: (mode: SeekMode) ::=> Boolean8 := {
        let mappedMode := mode selectCase: #{
        SeekMode Set : FILE_BEGIN.
        SeekMode Current : FILE_CURRENT.
        SeekMode End : FILE_END.
        _ : {return: false}.
        }.

        SetFilePointerEx(handle, LARGE_INTEGER() QuadPart: 0; yourself, nil, mappedMode castTo: DWORD) ~= 0
    }.

    override method write: (buffer: Void const pointer) size: (size: UIntPointer) ::=> IntPointer := {
        let writtenBytes mutable type: DWORD.
        let bytesToWrite := size castTo: DWORD.
        isConsole ifTrue: {
            WriteConsoleA(handle, buffer, bytesToWrite, writtenBytes address, nil) = 0 ifTrue: {
                return: -1
            }.
        } ifFalse: {
            WriteFile(handle, buffer, bytesToWrite, writtenBytes address, nil) = 0 ifTrue: {
                return: -1
            }.
        }.

        writtenBytes castTo: IntPointer.
    }.

    override method read: (buffer: Void pointer) size: (size: UIntPointer) ::=> IntPointer := {
        let readBytes mutable type: DWORD.
        let bytesToRead := size castTo: DWORD.

        isConsole ifTrue: {
            ReadConsoleA(handle, buffer, bytesToRead, readBytes address, nil) = 0 ifTrue: {
                return: -1
            }.
        } ifFalse: {
            ReadFile(handle, buffer, bytesToRead, readBytes address, nil) = 0 ifTrue: {
                return: -1
            }.
        }.

        eofDetected_ := readBytes = 0.
        readBytes castTo: IntPointer.
    }.
}.

class FileStream definition: {
    meta method fromHandle: (handle: HANDLE) isOwner: (isOwner: Boolean8) bufferingMode: (mode: StreamBufferingMode) ::=> SelfType := {
        <notInCompileTime>
        SelfType basicNewValue
            handle: handle;
            isConsole: GetFileType(handle) = FILE_TYPE_CHAR;
            isOwner: isOwner;
            initialize;
            bufferingMode: mode;
            yourself.
    }
}.

class AtomicFileStream definition: {
    meta method fromHandle: (handle: HANDLE) isOwner: (isOwner: Boolean8) bufferingMode: (mode: StreamBufferingMode) ::=> SelfType := {
        <notInCompileTime>
        SelfType basicNewValue
            handle: handle;
            isConsole: GetFileType(handle) = FILE_TYPE_CHAR;
            isOwner: isOwner;
            initialize;
            bufferingMode: mode;
            yourself.
    }
}.

global stdinFile mutable := AtomicFileStream fromHandle: GetStdHandle(STD_INPUT_HANDLE) isOwner: false bufferingMode: StreamBufferingMode Block.
global stdoutFile mutable := AtomicFileStream fromHandle: GetStdHandle(STD_OUTPUT_HANDLE) isOwner: false
    bufferingMode: (GetConsoleCP() ~= 0 ifTrue: StreamBufferingMode Line ifFalse: StreamBufferingMode Block).
global stderrFile mutable := AtomicFileStream fromHandle: GetStdHandle(STD_ERROR_HANDLE) isOwner: false bufferingMode: StreamBufferingMode None.

}. ## End of namespace IO

global stdout mutable := IO TextStreamWriter for: IO stdoutFile address.
global stderr mutable := IO TextStreamWriter for: IO stderrFile address.
}. ## End of namespace Stdn
