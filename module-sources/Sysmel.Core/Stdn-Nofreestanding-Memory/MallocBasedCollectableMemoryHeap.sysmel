namespace Stdn definition: {
namespace Memory definition: {

class MallocBasedCollectableMemoryHeap definition: {
    compileTime constant SweepPredicateBlock := ((Void pointer) => Boolean8) nativeStackBlockClosure.

    struct AllocationRecord definition: {
        public field pointer type: Void pointer.
        public field size type: UIntPointer.
    }.

    field mutex type: Stdn Mutex.
    field currentAllocationList type: Stdn Collections Vector(AllocationRecord).
    field survivingAllocationList type: Stdn Collections Vector(AllocationRecord).
    field totalAllocatedSize type: UIntPointer.

    method totalAllocatedSize => UIntPointer := {
        mutex withLock: {
            totalAllocatedSize
        }
    }.

    method allocate: (size: UIntPointer) ::=> Void pointer := {
        size == 0 ifTrue: {
            return: nil
        }.

        let result := LibC malloc(size).
        result ifNotNil: {
            mutex withLock: {
                currentAllocationList add: (AllocationRecord()
                    pointer: result;
                    size: size;
                    yourself).
                totalAllocatedSize := totalAllocatedSize + size
            }
        }.

        result
    }.

    method sweepAllocationsWhen: (sweepPredicateBlock: SweepPredicateBlock) ::=> Void := {
        mutex withLock: {
            survivingAllocationList removeAll.
            currentAllocationList do: {:each :: Void |
                sweepPredicateBlock(each pointer) ifTrue: {
                    LibC free(each pointer).
                    totalAllocatedSize := totalAllocatedSize - each size
                } ifFalse: {
                    survivingAllocationList add: each
                }
            }.

            survivingAllocationList swapWith: currentAllocationList
        }.
    }.
}.

compileTime constant GarbageCollectableMemoryHeap := MallocBasedCollectableMemoryHeap.

} ## End of namespace Memory.
} ## End of namespace Stdn.
