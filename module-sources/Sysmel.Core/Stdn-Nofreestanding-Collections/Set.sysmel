namespace Stdn definition: {
namespace Collections definition: {

template Set(VT: Stdn Concepts HashKey)
    := class definition: {
    compileTime constant ValueType := VT.
    compileTime constant StorageElementType := Optional(ValueType).
    compileTime constant StorageType := Vector(StorageElementType).

    private field size_ type: UIntPointer.
    private field storage_ type: StorageType.

    Stdn Collections SetCollectionMixin.

    const inline method size => UIntPointer
        := size_.

    const inline method capacity => UIntPointer
        := storage_ size.

    const inline method scanFor: (value: ValueType const ref) withHash: (hash: UIntPointer)::=> UIntPointer := {
        let currentCapacity := storage_ size.
        currentCapacity == 0 ifTrue: {
            return: currentCapacity.
        }.

        let slotIndex := hash % currentCapacity.

        ## Linear scan forward.
        slotIndex until: currentCapacity do: {:(UIntPointer)i :: Void |
            let slot ref := storage_[i].
            slot isPresent not || slot _ = value ifTrue: {
                return: i
            }.
        }.

        ## Linear scan wrap-around. Avoid the expensive module % by breaking the loop in two parts.
        0 until: slotIndex do: {:(UIntPointer)i :: Void |
            let slot ref := storage_[i].
            slot isPresent not || slot _ = value ifTrue: {
                return: i
            }.
        }.

        return: currentCapacity
    }.

    const inline method isValidIndex: (slotIndex: UIntPointer) ::=> Boolean8
        := slotIndex < storage_ size.

    const inline method scanFor: (value: ValueType const ref) ::=> UIntPointer
        := self scanFor: value withHash: value hash.

    const inline method includes: (value: ValueType const ref) ::=> Boolean8 := {
        let slotIndex := self scanFor: value.
        return: (self isValidIndex: slotIndex) && (storage_[slotIndex] isPresent)
    }.

    inline method add: (value: ValueType const ref) ::=> Boolean8 := {
        let slotIndex mutable := self scanFor: value.
        (self isValidIndex: slotIndex) ifFalse: {
            self increaseCapacity.
            slotIndex := self scanFor: value.
        }.

        Stdn assert: (self isValidIndex: slotIndex).
        let isNewElement := storage_[slotIndex] isPresent not.
        isNewElement ifTrue: {
            storage_[slotIndex] := value.
            size_ := size_ + 1.
            self checkFullCapacityCondition.
        } ifFalse: {
            storage_[slotIndex] := value
        }.

        isNewElement
    }.

    method increaseCapacity => Void := {
        let newCapacity := storage_ size * 2u max: 16u.
        ## Create the new storage
        let newStorage mutable type: StorageType.
        newStorage resizeTo: newCapacity.

        ## Swap the storage with the old storage.
        storage_ swapWith: newStorage.
        let oldStorage ref := newStorage.

        ## Reinsert all of the present elements.
        oldStorage do: {:(StorageElementType ref) element :: Void |
            element isPresent ifTrue: {
                let slotIndex := self scanFor: element _.
                Stdn assert: (self isValidIndex: slotIndex).
                storage_[slotIndex] := element asMoveReference.
            }
        }.
    }.

    method checkFullCapacityCondition => Void := {
        let capacity := storage_ capacity.
        let remainingCapacity := capacity - size_.
        let threshold := capacity / 4.
        remainingCapacity < threshold ifTrue: {
            self increaseCapacity
        }
    }.

    const method do: (aBlock: ((ValueType const ref) => Void) nativeBlockClosure) ::=> Void := {
        storage_ do: {:(StorageElementType ref)each :: Void |
            each isPresent ifTrue: {
                aBlock(each _)
            }
        }
    }.

    inline method remove: (value: ValueType const ref) ::=> Void := {
        self remove: value ifAbsent: {:: Void | Stdn raiseError("Failed to find value in dictionary"). }
    }.

    inline method remove: (value: ValueType const ref) ifAbsent: (absentBlock: (Void => Void) nativeBlockClosure) ::=> Void := {
        let slotIndex := self scanFor: value.
        (self isValidIndex: slotIndex) && storage_[slotIndex] isPresent ifFalse: {
            return: absentBlock()
        }.

        storage_[slotIndex] reset.
        size_ := size_ - 1.
        self fixCollisionsFrom: slotIndex
    }.

    inline method fixCollisionsFrom: (start: UIntPointer) ::=> Void := {
        let currentIndex mutable := start.
        let capacity := storage_ size.
        let element mutable type: StorageElementType pointer.
        while: {
            currentIndex := (currentIndex + 1) % capacity.
            element := storage_[currentIndex] address.
            element _ isPresent
        } do: {
            let newIndex := self scanFor: element _ _.
            newIndex ~= currentIndex ifTrue: {
                Stdn swapValue: storage_[currentIndex] with: storage_[newIndex]
            }.
        }
    }.

    method removeAll => Void := {
        size_ := 0.
        storage_ do: {:(StorageElementType ref)each :: Void |
            each reset
        }
    }.
}.

}. ## End of namespace Collections
}. ## End of namespace Stdn
