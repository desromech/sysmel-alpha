namespace Std definition: {
namespace Algorithms definition: {

template InsertionSort(VT: Type)
:= struct definition: {
    compileTime constant ValueType := VT.
    compileTime constant LessThanBlockType := ((ValueType const ref -- ValueType const ref) => Boolean8) nativeBlockClosure.

    meta definition: {
        static method sort: (data: ValueType arraySlice) with: (lessThan: LessThanBlockType) ::=> Void := {
            ## Ported implementation from: https://en.wikipedia.org/wiki/Insertion_sort [July, 2020]
            let elements := data elements.
            1 until: data size do: {:i :: Void |
                let x mutable := elements[i] asMoveReference.
                let j mutable := (i castTo: IntPointer) - 1.
                while: j >= 0 && lessThan(x, elements[j]) do: {
                    elements[j + 1] := elements[j] asMoveReference.
                    j := j - 1.
                }.
                elements[j + 1] := x asMoveReference
            }.
        }.
    }.

}.

}. ## End of namespace namespace Algorithms
}. ## End of namespace namespace Std
