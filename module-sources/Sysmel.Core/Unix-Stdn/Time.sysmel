namespace Stdn definition: {
namespace Chrono definition: {
    compileTime constant Time := Int64.
    compileTime constant TicksPerMicrosecond := 1.
    compileTime constant TicksPerMillisecond := TicksPerMicrosecond*1000.
    compileTime constant TicksPerSecond := TicksPerMillisecond*1000.

    function microsecondClockValue() => Time := {
        let tv mutable type: Unix timeval.
        Unix gettimeofday(tv address, nil).

        (tv tv_sec castTo: Int64)*1000000 + tv tv_usec
    }.

    function monotonicClockValue() => Time := {
        let ts mutable type: Unix timespec.
        Unix clock_gettime(Unix CLOCK_MONOTONIC, ts address).

        (ts tv_sec castTo: Int64)*1000000 + ts tv_nsec/1000
    }.

    function processClockValue() => Time := {
        let ts mutable type: Unix timespec.
        Unix clock_gettime(Unix CLOCK_PROCESS_CPUTIME_ID, ts address).

        (ts tv_sec castTo: Int64)*1000000 + ts tv_nsec/1000
    }.

    function threadClockValue() => Time := {
        let ts mutable type: Unix timespec.
        Unix clock_gettime(Unix CLOCK_THREAD_CPUTIME_ID, ts address).

        (ts tv_sec castTo: Int64)*1000000 + ts tv_nsec/1000
    }.

    inline function now() => Time
        := monotonicClockValue().

    inline function microseconds(time: Time) => Time
        := time * TicksPerMicrosecond.

    inline function milliseconds(time: Time) => Time
        := time * TicksPerMillisecond.

    inline function seconds(time: Time) => Time
        := time * TicksPerSecond.

    inline function sleepFor(time: Time) => Void := {
        time > 0 ifTrue: {
            Unix usleep(time castTo: Unix USeconds_T)
        }.
    }.

    inline function ticksToFloatSeconds(ticks: Time) => Float64
        := (ticks castTo: Float64) / TicksPerSecond.

    inline function sleepUntil(time: Time) => Void
        := sleepFor(time - now()).

    macro method profileTicksToRun: aBlock := {
        let startTicks := __astBuilder gensym: #startTicks.
        let endTicks := __astBuilder gensym: #endTicks.
        ``{
            let `,startTicks := Stdn Chrono processClockValue().
            `,aBlock __macroInlineBlock.
            let `,endTicks := Stdn Chrono processClockValue().
            `,endTicks - `,startTicks
        }
    }.

    macro method profileTimeToRun: aBlock := ``(Stdn Chrono ticksToFloatSeconds(Stdn Chrono profileTicksToRun: `,aBlock)).
}.
}.
