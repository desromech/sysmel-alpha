namespace Smalltalk definition: {
namespace Runtime definition: {

useNamespace: Std GC.
useNamespace: Std Reflection.

let hasInitialSymbolDeduplication mutable type: Boolean8.

function deduplicateSymbolRoot(symbolRoot: GCObjectHeader pointer pointer) => Void := {
    let rootPointer ref := (symbolRoot reinterpretCastTo: ProtoObject pointer) _.
    let rootPointerClass := rootPointer class.
    rootPointerClass == Symbol ifTrue: {
        rootPointer := Symbol internSymbolInstance: (rootPointer reinterpretCastTo: Symbol)
    } ifFalse: {
        ## We need to apply this same transformation to the instance variables.
        rootPointerClass __manipulateInstance: rootPointer gcSlotsWith: {:(ProtoObject pointer)slotPointer :: Void |
            let originalSlotValue := slotPointer _.
            originalSlotValue class == Symbol ifTrue: {
                slotPointer _ := Symbol internSymbolInstance: (originalSlotValue reinterpretCastTo: Symbol)
            }.
        }.
    }.
}.

function ensureInitialSymbolDeduplication() => Void := {
    hasInitialSymbolDeduplication ifTrue: {return: void}.
    hasInitialSymbolDeduplication := true.

    NativeCollector uniqueInstance allModuleRootsDo: {:each :: Void |
        deduplicateSymbolRoot(each)
    }
}.

function registerModuleRoots(rootPointerDescriptors: GCRootPointersDescriptor pointer, rootPointerDescriptorCount: UIntPointer) => Void := {
    NativeCollector uniqueInstance registerModuleRoots: (rootPointerDescriptors until: rootPointerDescriptorCount).
    hasInitialSymbolDeduplication ifTrue: {
        0 until: rootPointerDescriptorCount do: {:i :: Void |
            rootPointerDescriptors[i] rootsDo: {:each :: Void |
                deduplicateSymbolRoot(each)
            }
        }
    }.
}.

function unregisterModuleRoots(rootPointerDescriptors: GCRootPointersDescriptor pointer, rootPointerDescriptorCount: UIntPointer) => Void := {
    ## Std stdout << "unregisterModuleRoots rootPointerDescriptors " << rootPointerDescriptors << " rootPointerDescriptorCount " << rootPointerDescriptorCount; nl.
}.

Compiler compilationTarget managedObjectModel
    moduleRootRegistrationFunction: registerModuleRoots.


}. ## End of namespace Runtime
}. ## End of namespace Smalltalk
