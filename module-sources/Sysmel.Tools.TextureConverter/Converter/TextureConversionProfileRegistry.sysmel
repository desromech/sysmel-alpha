namespace Sysmel definition: {
namespace Tools definition: {
namespace TextureConverter definition: {

useNamespace: Std Graphics Core.

#**
 * I am a texture converter.
 *#
class TextureConversionProfileRegistry superclass: Std RCObject; definition: {
    meta definition: {
        let singleton mutable type: TextureConversionProfileRegistry uniquePointer.
        let singletonOnceCreation mutable type: Std OnceFlag.

        method uniqueInstance => TextureConversionProfileRegistry ref := {
            singletonOnceCreation do: {
                singleton reset: TextureConversionProfileRegistry nativeNew.
            }.

            singleton _
        }.
    }.

    private field mutex type: Std Mutex.
    private field registeredProfiles type: Std Collections Vector(TextureConversionProfile rcRef).

    method addProfile: (profile: TextureConversionProfile rcRef const ref) ::=> Void := {
        mutex withLock: {
            registeredProfiles add: profile
        }
    }.

    method getProfiles ::=> Std Collections Vector(TextureConversionProfile rcRef) := {
        mutex withLock: {
            return: registeredProfiles
        }.
    }.

    method findNamed: (name: Char8 const arraySlice) default: (default: TextureConversionProfile rcRef const ref) ::=> TextureConversionProfile rcRef := {
        mutex withLock: {
            registeredProfiles do: {:each :: Void |
                each name = name ifTrue: {
                    return: each
                }
            }
        }.

        default
    }.
}.

template TextureConversionProfileRegisterer (T: Type)
    := class definition: {

    method initialize => Void := {
        TextureConversionProfileRegistry uniqueInstance addProfile: (T new upCastFor: TextureConversionProfile)
    }.
}.
} ## End of namespace Tools
} ## End of namespace TextureConverter
} ## End of namespace Sysmel
