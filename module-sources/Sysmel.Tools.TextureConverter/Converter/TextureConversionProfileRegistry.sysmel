namespace Sysmel definition: {
namespace Tools definition: {
namespace TextureConverter definition: {

useNamespace: Stdn Graphics Core.

#**
 * I am a texture converter.
 *#
class TextureConversionProfileRegistry superclass: Stdn SharedObject; definition: {
    meta definition: {
        let singleton mutable type: TextureConversionProfileRegistry uniquePointer.
        let singletonOnceCreation mutable type: Stdn OnceFlag.

        method uniqueInstance => TextureConversionProfileRegistry ref := {
            singletonOnceCreation do: {
                singleton reset: TextureConversionProfileRegistry nativeNew.
            }.

            singleton _
        }.
    }.

    private field mutex type: Stdn Mutex.
    private field registeredProfiles type: Stdn Collections Vector(TextureConversionProfile sharedPointer).

    method addProfile: (profile: TextureConversionProfile sharedPointer const ref) ::=> Void := {
        mutex withLock: {
            registeredProfiles add: profile
        }
    }.

    method getProfiles ::=> Stdn Collections Vector(TextureConversionProfile sharedPointer) := {
        mutex withLock: {
            return: registeredProfiles
        }.
    }.

    method findNamed: (name: Char8 const arraySlice) default: (default: TextureConversionProfile sharedPointer const ref) ::=> TextureConversionProfile sharedPointer := {
        mutex withLock: {
            registeredProfiles do: {:each :: Void |
                each _ name = name ifTrue: {
                    return: each
                }
            }
        }.

        default
    }.
}.

template TextureConversionProfileRegisterer (T: Type)
    := class definition: {

    method initialize => Void := {
        TextureConversionProfileRegistry uniqueInstance addProfile: (T sharedNew upCastFor: TextureConversionProfile)
    }.
}.
} ## End of namespace Tools
} ## End of namespace TextureConverter
} ## End of namespace Sysmel
