namespace Sysmel definition: {
namespace Tools definition: {
namespace TextureConverter definition: {

useNamespace: Std Graphics Core.

#**
 * Generic parameters used by the texture conversion tool.
 *#
struct Parameters definition: {
    public field usageType type: TextureUsageType.
    public field inferUsageFromName type: Boolean8.
    public field generateMipmaps type: Boolean8.
    public field targetFormat type: PixelFormat.
    public field targetProfileName type: Std String.
    public field inputFileName type: Std String.
    public field outputFileName type: Std String.
    public field minMipLevelExtent type: UInt32x3.
    public field maxMipLevelExtent type: UInt32x3.

    method initialize => Void := {
        generateMipmaps := true.
        minMipLevelExtent := UInt32x3 ones.
        maxMipLevelExtent := UInt32x3 infinityOrMaxValue.
    }.

    method printHelpOnto: (out: Std IO TextStreamWriter ref) ::=> Void := {
        out << "Sysmel.Tools.TextureConverter <options>"; nl.
        out << "-h[elp] Print this message"; nl.
        out << "-no-mipmap Do not generate mipmaps"; nl.
        out << "-color Process the texture as a color texture by typically using the sRGB color space."; nl.
        out << "-non-color Process the texture as non-color data."; nl.
        out << "-normal Process the texture as a tangent space normal map."; nl.
        out << "-target-profile The target platform profile to use."; nl.
        out << "-min-mip-width The minimum width for a mip map."; nl.
        out << "-min-mip-height The minimum height for a mip map."; nl.
        out << "-min-mip-depth The minimum depth for a mip map."; nl.
        out << "-max-mip-width The maximum width for a mip map."; nl.
        out << "-max-mip-height The maximum height for a mip map."; nl.
        out << "-max-mip-depth The maximum depth for a mip map."; nl.
        out << "-i The input file name."; nl.
        out << "-o The output file name."; nl.

        out nl.
        out << "Target profiles:"; nl.
        let profiles := TextureConversionProfileRegistry uniqueInstance getProfiles.
        profiles do: {:each :: Void |
            out << each name << ": " << each description; nl
        }.
    }.

    method parseFromCommandLine: (args: Std ImmutableString const arraySlice) ::=> Boolean8 := {
        for: (let i mutable type: UIntPointer := 0) while: (i < args size) do: {
            let arg := args[i].
            arg selectCase: #{
                "-h" : {
                    self printHelpOnto: Std stdout.
                    return: false
                }.
                "-help" : {
                    self printHelpOnto: Std stdout.
                    return: false
                }.

                "-no-mipmap" : {
                    generateMipmaps := false.
                }.

                "-color" : {
                    usageType := TextureUsageType Color
                }.

                "-normal" : {
                    usageType := TextureUsageType NormalMap
                }.

                "-non-color" : {
                    usageType := TextureUsageType NonColorData
                }.
                "-target-profile" : {
                    targetProfileName := args[i := i + 1].
                }.

                "-min-mip-width" : {
                    minMipLevelExtent x: args[i := i + 1] parseUInt32
                }.
                "-min-mip-height" : {
                    minMipLevelExtent y: args[i := i + 1] parseUInt32
                }.
                "-min-mip-depth" : {
                    minMipLevelExtent z: args[i := i + 1] parseUInt32
                }.

                "-max-mip-width" : {
                    maxMipLevelExtent x: args[i := i + 1] parseUInt32
                }.
                "-max-mip-height" : {
                    maxMipLevelExtent y: args[i := i + 1] parseUInt32
                }.
                "-max-mip-depth" : {
                    maxMipLevelExtent z: args[i := i + 1] parseUInt32
                }.
                "-i" : {
                    inputFileName := args[i := i + 1].
                }.

                "-o" : {
                    outputFileName := args[i := i + 1].
                }.
                _ : {return: false}
            }.
        } continueWith: (i := i + 1).
        true
    }.
}.

} ## End of namespace Tools
} ## End of namespace TextureConverter
} ## End of namespace Sysmel
