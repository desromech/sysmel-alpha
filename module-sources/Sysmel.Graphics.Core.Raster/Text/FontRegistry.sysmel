namespace Stdn definition: {
namespace Graphics definition: {
namespace Core definition: {

#**
 * I am a registry for loaded fonts.
 *#
class FontRegistry definition: {
    private field fontDictionaryMutex type: Stdn Mutex.
    private field fontDictionary type: Stdn Collections Dictionary(Stdn String, Font sharedPointer).

    private field fontFaceDictionaryMutex type: Stdn Mutex.
    private field fontFaceDictionary type: Stdn Collections Dictionary(Stdn String, FontFace sharedPointer).

    private field defaultSans type: Font sharedPointer.
    private field defaultSerif type: Font sharedPointer.
    private field defaultMonospaced type: Font sharedPointer.

    meta definition: {
        let defaultInstance mutable type: FontRegistry uniquePointer.
        let defaultInstanceOnceCreation mutable type: Stdn OnceFlag.

        method default => FontRegistry ref := {
            defaultInstanceOnceCreation do: {
                defaultInstance reset: FontRegistry nativeNew.
            }.

            defaultInstance _
        }.
    }.

    method loadFontNamed: (fontName: Stdn String const ref) with: (fontFillingBlock: ((Font ref) => Void) nativeBlockClosure) ::=> Font sharedPointer := {
        fontDictionaryMutex withLock: {
            fontDictionary at: fontName ifAbsentPut: {:: (Font sharedPointer) |
                let font mutable := Font sharedNew.
                fontFillingBlock(font _).
                font
            }
        }
    }.

    method loadFontFace: (faceFileName: Stdn String const ref) ::=> FontFace sharedPointer := {
        fontFaceDictionaryMutex withLock: {
            fontFaceDictionary at: faceFileName ifAbsentPut: {:: (FontFace sharedPointer) |
                FontFaceLoaderRegistry uniqueInstance loadFontFaceFromFileNamed: faceFileName
            }
        }
    }.

    method loadRequiredFontFace: (faceFileName: Stdn String const ref) ::=> FontFace sharedPointer := {
        fontFaceDictionaryMutex withLock: {
            fontFaceDictionary at: faceFileName ifAbsentPut: {:: (FontFace sharedPointer) |
                FontFaceLoaderRegistry uniqueInstance loadRequiredFontFaceFromFileNamed: faceFileName
            }
        }
    }.

    method setDefaultSansFont: (font: Font sharedPointer const ref) ::=> Void
        := defaultSans := font.

    method defaultSans => Font sharedPointer := {
        defaultSans ifNil: {
            defaultSans := self loadFontNamed: "DejaVuSans" asMutableString
                with: {:(Font ref) font :: Void |
                let dejaVuParentFolder := FileSystem resourcesDirectory / "fonts/dejavu/ttf".
                font
                    normalFace: (self loadRequiredFontFace: (dejaVuParentFolder / "DejaVuSans.ttf") fullName asMutableString);
                    boldFace: (self loadRequiredFontFace: (dejaVuParentFolder / "DejaVuSans-Bold.ttf") fullName asMutableString);
                    italicFace: (self loadRequiredFontFace: (dejaVuParentFolder / "DejaVuSans-Oblique.ttf") fullName asMutableString);
                    italicBoldFace: (self loadRequiredFontFace: (dejaVuParentFolder / "DejaVuSans-BoldOblique.ttf") fullName asMutableString).
            }
        }.

        defaultSans.
    }.

    method setDefaultSerifFont: (font: Font sharedPointer const ref) ::=> Void
        := defaultSerif := font.

    method defaultSerif => Font sharedPointer := {
        defaultSerif ifNil: {
            defaultSerif := self loadFontNamed: "DejaVuSerif" asMutableString
                with: {:(Font ref) font :: Void |
                font
                    normalFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuSerif.ttf" asMutableString);
                    boldFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuSerif-Bold.ttf" asMutableString);
                    italicFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuSerif-Oblique.ttf" asMutableString);
                    italicBoldFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuSerif-BoldOblique.ttf" asMutableString).
            }
        }.

        defaultSerif
    }.

    method setDefaultMonospaced: (font: Font sharedPointer const ref) ::=> Void
        := defaultMonospaced := font.

    method defaultMonospaced => Font sharedPointer := {
        defaultMonospaced ifNil: {
            defaultMonospaced := self loadFontNamed: "DejaVuMono" asMutableString
                with: {:(Font ref) font :: Void |
                font
                    normalFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuMono.ttf" asMutableString);
                    boldFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuMono-Bold.ttf" asMutableString);
                    italicFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuMono-Oblique.ttf" asMutableString);
                    italicBoldFace: (self loadRequiredFontFace: "resources/fonts/dejavu/ttf/DejaVuMono-BoldOblique.ttf" asMutableString).
            }
        }.

        defaultMonospaced
    }.

}.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Stdn
