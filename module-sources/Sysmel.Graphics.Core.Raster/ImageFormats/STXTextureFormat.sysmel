namespace Stdn definition: {
namespace Graphics definition: {
namespace ImageFormats definition: {

useNamespace: Stdn Graphics Core.

class STXTextureFormat superclass: ImageReadWriter; definition: {
    override const method supportsExtension: (extension: Char8 const arraySlice) ::=> Boolean8
        := extension = "stx".

    const override method supportsMimeType: (mimeType: Char8 const arraySlice) ::=> Boolean8
        := mimeType = "image/vnd-sysmel.stx".

    override const method putTextureData: (textureData: TextureData ref) onStream: (outputStream: Stdn IO Stream ref) ::=> Void := {
        ## File header.
        {
            let textureFileHeader mutable := TextureFileHeader()
                setValid;
                yourself.
            outputStream write: textureFileHeader address size: TextureFileHeader instanceSize.
        }.

        ## Texture data header.
        {
            let textureDataHeader type: TextureDataHeader pointer := textureData address.
            outputStream write: textureDataHeader size: TextureDataHeader instanceSize.
        }.

        ## Write the different levels.
        Stdn assert: textureData levelsData size = textureData miplevels * textureData layers.
        let levelDataSize := textureData levelsData size * TextureLevelData instanceSize.
        outputStream write: textureData levelsData data size: levelDataSize.

        ## Write the texture pixel data.
        outputStream write: textureData pixels getPointer size: textureData linearDataSize.
    }.

    override const method readTextureDataFromStream: (inputStream: Stdn IO Stream ref) ::=> TextureDataPtr := {
        ## Check the file header.
        {
            let fileHeader mutable type: TextureFileHeader.
            (inputStream read: fileHeader address size: TextureFileHeader instanceSize) = TextureFileHeader instanceSize && fileHeader isValid ifFalse: {
                return: TextureDataPtr nil
            }
        }.

        ## Create the texture data result.
        let result := TextureData sharedNew.

        ## Read the texture data header.
        {
            let textureDataHeader type: TextureDataHeader pointer := result getPointer.
            (inputStream read: textureDataHeader size: TextureDataHeader instanceSize) = TextureDataHeader instanceSize ifFalse: {
                return: TextureDataPtr nil
            }.
        }.

        ## Read the levels data.
        result _ levelsData resizeTo: result _ layers * result _ miplevels.
        let levelDataSize := result _ levelsData size * TextureLevelData instanceSize.
        (inputStream read: result _ levelsData data size: levelDataSize) = (levelDataSize castTo: IntPointer) ifFalse: {
            return: TextureDataPtr nil
        }.

        ## Read the pixel data.
        result _ pixels reset: (Stdn malloc(result _ linearDataSize) reinterpretCastTo: UInt8 pointer).
        (inputStream read: result _ pixels getPointer size: result _ linearDataSize) = result _ linearDataSize ifFalse: {
            return: TextureDataPtr nil
        }.

        result
    }.
}.

global STXTextureFormatRegistration mutable type: ImageReadWriterRegisterer(STXTextureFormat).

Form extend: {
    method writeSTXfileNamed: (fileName: Stdn String const ref) ::=> Boolean8 := {
        let readWriter := STXTextureFormat().
        readWriter putForm: self onFileNamed: fileName.
    }.
}.

} ## End of namespace ImageFormats
} ## End of namespace Graphics
} ## End of namespace Stdn
