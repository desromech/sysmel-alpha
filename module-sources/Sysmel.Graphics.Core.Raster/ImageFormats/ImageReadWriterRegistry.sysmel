namespace Std definition: {
namespace Graphics definition: {
namespace ImageFormats definition: {

useNamespace: Std Graphics Core.

class ImageReadWriterRegistry definition: {
    meta definition: {
        let singleton mutable type: ImageReadWriterRegistry uniquePointer.
        let singletonOnceCreation mutable type: Std OnceFlag.

        method uniqueInstance => ImageReadWriterRegistry ref := {
            singletonOnceCreation do: {
                singleton reset: ImageReadWriterRegistry nativeNew.
            }.

            singleton _
        }.
    }.

    private field mutex type: Std Mutex.
    private field registeredImageReadWriters type: Std Collections Vector(ImageReadWriter rcPointer).

    method addImageReadWriter: (imageReadWriter: ImageReadWriter rcPointer) ::=> Void := {
        mutex withLock: {
            registeredImageReadWriters add: imageReadWriter
        }
    }.

    method findForFileName: (fileName: Std String const ref) ::=> ImageReadWriter pointer
        := self findForFileReference: fileName asNativeFileReference.

    method findForFileReference: (fileReference: Std FileReference const ref) ::=> ImageReadWriter pointer
        := self findWithExtension: fileReference extension.

    method findWithExtension: (extension: Char8 const arraySlice) ::=> ImageReadWriter pointer := {
        mutex withLock: {
            registeredImageReadWriters do: {:each :: Void |
                (each _ supportsExtension: extension) ifTrue: {
                    return: each getPointer
                }
            }
        }.

        nil
    }.

    method findWithMimeType: (mimeType: Char8 const arraySlice) ::=> ImageReadWriter pointer := {
        mutex withLock: {
            registeredImageReadWriters do: {:each :: Void |
                (each _ supportsMimeType: mimeType) ifTrue: {
                    return: each getPointer
                }
            }
        }.

        nil
    }.

}.

template ImageReadWriterRegisterer(T: Type)
    := class definition: {

    method initialize => Void := {
        ImageReadWriterRegistry uniqueInstance addImageReadWriter: (T rcNew upCastFor: ImageReadWriter)
    }.
}.


Form extend: {
    meta method readFromFileNamed: (fileName: Std String const ref) ::=> FormPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findForFileName: fileName.
        readWriter ifNil: {return: FormPtr nil}.

        readWriter _ readFormFromFileNamed: fileName.
    }.

    meta method readFromStream: (stream: Std IO Stream ref) withExtension: (extension: Char8 const arraySlice) ::=> FormPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findWithExtension: extension.
        readWriter ifNil: {return: FormPtr nil}.

        readWriter _ readFormFromStream: stream.
    }.

    meta method readFromStream: (stream: Std IO Stream ref) withMimeType: (mimeType: Char8 const arraySlice) ::=> FormPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findWithMimeType: mimeType.
        readWriter ifNil: {return: FormPtr nil}.

        readWriter _ readFormFromStream: stream.
    }.

    method writeToFileNamed: (fileName: Std String const ref) ::=> Boolean8 := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findForFileName: fileName.
        readWriter ifNil: {return: false}.

        readWriter _ putForm: self onFileNamed: fileName.
    }.
}.

TextureData extend: {
    meta method readFromFileNamed: (fileName: Std String const ref) ::=> TextureDataPtr
        := self readFromFileReference: fileName asNativeFileReference.

    meta method readFromFileReference: (fileReference: Std FileReference const ref) ::=> TextureDataPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findForFileReference: fileReference.
        readWriter ifNil: {return: TextureDataPtr nil}.

        readWriter _ readTextureDataFromFileReference: fileReference.
    }.

    meta method readFromStream: (stream: Std IO Stream ref) withExtension: (extension: Char8 const arraySlice) ::=> TextureDataPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findWithExtension: extension.
        readWriter ifNil: {return: TextureDataPtr nil}.

        readWriter _ readTextureDataFromStream: stream.
    }.

    meta method readFromStream: (stream: Std IO Stream ref) withMimeType: (mimeType: Char8 const arraySlice) ::=> TextureDataPtr := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findWithMimeType: mimeType.
        readWriter ifNil: {return: TextureDataPtr nil}.

        readWriter _ readTextureDataFromStream: stream.
    }.

    method writeToFileNamed: (fileName: Std String const ref) ::=> Boolean8
        := self writeToFileReference: fileName asNativeFileReference.

    method writeToFileReference: (fileReference: Std FileReference const ref) ::=> Boolean8 := {
        let readWriter := ImageReadWriterRegistry uniqueInstance findForFileReference: fileReference.
        readWriter ifNil: {return: false}.

        readWriter _ putTextureData: self onFileReference: fileReference.
    }.
}.

} ## End of namespace ImageFormats
} ## End of namespace Graphics
} ## End of namespace Std
