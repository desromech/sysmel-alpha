namespace Stdn definition: {
namespace Graphics definition: {
namespace Scene2D definition: {

useNamespace: Stdn Math LinearAlgebra.
useNamespace: Stdn Math Geometry.
useNamespace: Stdn Graphics Core.

#**
 * I am an scene graph builder that allows keeping track of a clipping rectangle
 * in order to allow a front-end to reduce the number of required nodes that it actually needs.
 *#
class Builder definition: {
    method container => ContainerNodePtr
        := ContainerNode sharedNew.

    virtual method createTextureHandleFor: (texture: SharedObjectPtr const ref) view: (view: SharedObjectPtr const ref) ::=> StrongTextureHandlePtr
        := StrongTextureHandlePtr nil.

    virtual method createTextureFromForm: (form: FormPtr const ref) ::=> StrongTextureHandlePtr
        := StrongTextureHandlePtr nil.

    method texture: (textureHandle: StrongTextureHandlePtr const ref) rectangle: (rectangle: RectangleF32) ::=> TextureNodePtr := {
        let result := TextureNode sharedNew.
        result _
            rectangle: rectangle;
            textureHandle: textureHandle.
        result
    }.

    method fillTriangle: (p1: Float32x2) p2: (p2: Float32x2) p3: (p3: Float32x2) color: (color: Float32x4) ::=> FilledTriangleNodePtr := {
        let result := FilledTriangleNode sharedNew.
        result _
            rectangle: (((RectangleF32 point: p1) unionWith: p2) unionWith: p3);
            p1: p1;
            p2: p2;
            p3: p3;
            color: color.
        result
    }.

    method fillTriangle: (p1: Float32x2) color: (c1: Float32x4) p2: (p2: Float32x2) color: (c2: Float32x4) p3: (p3: Float32x2) color: (c3: Float32x4) ::=> FilledTriangleNodePtr := {
        let result := FilledTriangleNode sharedNew.
        result _
            rectangle: (((RectangleF32 point: p1) unionWith: p2) unionWith: p3);
            p1: p1;
            c1: c1;
            p2: p2;
            c2: c2;
            p3: p3;
            c3: c3.
        result
    }.

    method fillRectangle: (rectangle: RectangleF32) color: (color: Float32x4) ::=> FilledRectangleNodePtr := {
        let result := FilledRectangleNode sharedNew.
        result _ rectangle: rectangle; color: color.
        result
    }.

    method fillRectangle: (rectangle: RectangleF32) verticalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> FilledRectangleNodePtr := {
        let result := FilledRectangleNode sharedNew.
        result _
            rectangle: rectangle;
            verticalGradientStart: startColor end: endColor.
        result
    }.

    method fillRectangle: (rectangle: RectangleF32) horizontalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> FilledRectangleNodePtr := {
        let result := FilledRectangleNode sharedNew.
        result _
            rectangle: rectangle;
            horizontalGradientStart: startColor end: endColor.
        result
    }.

    method borderRectangle: (rectangle: RectangleF32) color: (color: Float32x4) width: (borderWidth: Float32) ::=> BorderRectangleNodePtr := {
        let result := BorderRectangleNode sharedNew.
        result _
            rectangle: rectangle;
            color: color;
            borderSize: borderWidth.
        result
    }.

    method borderRectangle: (rectangle: RectangleF32) color: (color: Float32x4) ::=> BorderRectangleNodePtr
        := self borderRectangle: rectangle color: color width: 1.

    method clipRectangle: (rectangle: RectangleF32) node: (node: NodePtr const ref) ::=> ClippedRectangleNodePtr := {
        let result := ClippedRectangleNode sharedNew.
        result _
            rectangle: rectangle;
            child: node.
        result
    }.

    method blendingMode: (blendingMode: BlendingMode) node: (node: NodePtr const ref) ::=> BlendingModeNodePtr := {
        let result := BlendingModeNode sharedNew.
        result _
            blendingMode: blendingMode;
            child: node.
        result
    }.

    method transform: (transform: AffinePostTransform2dF32) node: (node: NodePtr const ref) ::=> TransformNodePtr := {
        let result := TransformNode sharedNew.
        result _
            transform: transform;
            child: node.
        result
    }.

    method translate: (translation: Float32x2) node: (node: NodePtr const ref) ::=> TransformNodePtr
        := self transform: (AffinePostTransform2dF32()
            setIdentity;
            translation: translation;
            yourself) node: node.

    method text: (encodedString: Char8 const arraySlice) in: (rectangle: RectangleF32) font: (font: FontFaceWithSizePtr const ref) color: (color: Float32x4) ::=> TextNodePtr := {
        font ifNil: {return: TextNodePtr nil}.
        let baseline := Float32x2(0.0f, font _ ascender).
        self text: encodedString in: rectangle font: font color: color drawingState: (TextDrawingState position: rectangle min + baseline)
    }.

    method text: (encodedString: Char8 const arraySlice) at: (position: Float32x2) font: (font: FontFaceWithSizePtr const ref) color: (color: Float32x4) ::=> TextNodePtr
        := self text: encodedString in: (RectangleF32 point: position) font: font color: color.

    method text: (encodedString: Char8 const arraySlice) in: (rectangle: RectangleF32)
        font: (font: FontFaceWithSizePtr const ref) color: (color: Float32x4) drawingState: (drawingState: TextDrawingState) ::=> TextNodePtr := {
        let result := TextNode sharedNew.
        result _
            rectangle: rectangle;
            string: encodedString;
            fontFaceWithSize: font;
            color: color;
            drawingState: drawingState.
        result
    }.

    method text: (encodedString: Char8 const arraySlice) at: (position: Float32x2)
        font: (font: FontFaceWithSizePtr const ref) color: (color: Float32x4) drawingState: (drawingState: TextDrawingState) ::=> TextNodePtr
        := self text: encodedString in: (RectangleF32 point: position) font: font color: color drawingState: drawingState.

}.

compileTime constant BuilderPtr := Builder sharedPointer.

} ## End of namespace Scene2D
} ## End of namespace Graphics
} ## End of namespace Stdn
