namespace Std definition: {
namespace Graphics definition: {
namespace Scene2D definition: {

class FilledRectangleNode superclass: RectangularShapeNode; definition: {
    public field bottomLeftColor type: Float32x4.
    public field bottomRightColor type: Float32x4.
    public field topLeftColor type: Float32x4.
    public field topRightColor type: Float32x4.

    method color: (color: Float32x4) ::=> Void := {
        bottomLeftColor := bottomRightColor := color.
        topLeftColor := topRightColor := color.
    }.

    method verticalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> Void := {
        bottomLeftColor := bottomRightColor := startColor.
        topLeftColor := topRightColor := endColor.
    }.

    method horizontalGradientStart: (startColor: Float32x4) end: (endColor: Float32x4) ::=> Void := {
        bottomLeftColor := topLeftColor := startColor.
        bottomRightColor := topRightColor := endColor.
    }.

    const method hasVerticalGradient => Boolean8
        := bottomLeftColor = bottomRightColor && topLeftColor = topRightColor.

    const method hasHorizontalGradient => Boolean8
        := bottomLeftColor = topLeftColor && bottomRightColor = topRightColor.

    const method hasSingleColor => Boolean8
        := bottomLeftColor = bottomRightColor && bottomRightColor = topLeftColor && topLeftColor = topRightColor.

    override method accept: (visitor: Visitor ref) ::=> Void
        := visitor visitFilledRectangleNode: self

}.

compileTime constant FilledRectangleNodePtr := FilledRectangleNode rcPointer.


} ## End of namespace Scene2D
} ## End of namespace Graphics
} ## End of namespace Std
