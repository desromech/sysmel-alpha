namespace Std definition: {
namespace Graphics definition: {
namespace Driver definition: {
namespace SoftRenderer definition: {

useNamespace: Std Graphics Core.

#**
 * I am a CPU only implementation for a shader resource binding.
 *#
class SoftShaderResourceBindings superclass: ShaderResourceBindings; definition: {
    public field shaderSignature type: SoftShaderSignatureRef.
    public field bankIndex type: UIntPointer.
    public field bankDescription type: ShaderSignatureBankDescription pointer.

    public field bindingTable type: Std Collections Vector(Void pointer).
    public field bindingHandleTable type: Std Collections Vector(RenderingDriverObjectHandle rcRef).

    method createBindingTable => Void := {
        let bindingTableSize := bankDescription _ elements size.
        bindingTable resizeTo: bindingTableSize.
        bindingHandleTable resizeTo: bindingTableSize.
    }.

    const inline method isValidLocation: (location: IntPointer) ::=> Boolean8 :={
        (location castTo: UIntPointer) < bindingTable size.
    }.

    override method bindOn: (location: IntPointer) uniformBuffer: (buffer: BufferRef const ref) ::=> Void := {
        let size := buffer ifNil: {0} ifNotNil: {buffer size}.
        self bindOn: location uniformBuffer: buffer offset: 0 size: size.
    }.

    override method bindOn: (location: IntPointer) uniformBuffer: (buffer: BufferRef const ref) offset: (offset: UIntPointer) size: (size: UIntPointer) ::=> Void := {
        (self isValidLocation: location) ifFalse: {return: void}.

        let ulocation := location castTo: UIntPointer.

        let softBuffer := buffer downCastFor: SoftBuffer.
        softBuffer ifNil: {
            bindingTable[ulocation] := nil.
            bindingHandleTable[ulocation] := nil.
            void
        } ifNotNil: {
            bindingTable[ulocation] := softBuffer memoryStartPointer + offset.
            bindingHandleTable[ulocation] := softBuffer upCastFor: RenderingDriverObjectHandle.
        }
    }.

    override method bindOn: (location: IntPointer) storageBuffer: (buffer: BufferRef const ref) ::=> Void := {
        let size := buffer ifNil: {0} ifNotNil: {buffer size}.
        self bindOn: location uniformBuffer: buffer offset: 0 size: size.
    }.

    override method bindOn: (location: IntPointer) storageBuffer: (buffer: BufferRef const ref) offset: (offset: UIntPointer) size: (size: UIntPointer) ::=> Void := {
        (self isValidLocation: location) ifFalse: {return: void}.

        let ulocation := location castTo: UIntPointer.

        let softBuffer := buffer downCastFor: SoftBuffer.
        softBuffer ifNil: {
            bindingTable[ulocation] := nil.
            bindingHandleTable[ulocation] := nil.
            void
        } ifNotNil: {
            bindingTable[ulocation] := softBuffer memoryStartPointer + offset.
            bindingHandleTable[ulocation] := softBuffer upCastFor: RenderingDriverObjectHandle.
        }
    }.

    override method bindOn: (location: IntPointer) sampledTextureView: (textureView: TextureViewRef const ref) ::=> Void := {

    }.

    override method bindOn: (location: IntPointer) storageImageView: (textureView: TextureViewRef const ref) ::=> Void := {

    }.

    override method bindOn: (location: IntPointer) sampler: (sampelr: SamplerRef const ref) ::=> Void := {

    }.
}.

}. ## End of namespace SoftRenderer
}. ## End of namespace Driver
}. ## End of namespace Graphics
}. ## End of namespace Std
