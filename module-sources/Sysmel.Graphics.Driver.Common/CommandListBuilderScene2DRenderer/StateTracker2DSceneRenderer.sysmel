namespace Stdn definition: {
namespace Graphics definition: {
namespace Core definition: {

useNamespace: Stdn Math Geometry.
useNamespace: Stdn Graphics Scene2DMesh.

#**
 * I am a 2D scene renderer that draws onto a specific state tracker.
 *#
class StateTracker2DSceneRenderer superclass: MeshListBuilder2DSceneRenderer; definition: {
    public field stateTracker type: StateTrackerPtr.

    protected field shaderSignature type: ShaderSignaturePtr.

    protected field sampler type: SamplerPtr.
    protected field samplingStateBinding type: ShaderResourceBindingsPtr.

    protected field transformationStateBuffer type: BufferPtr.
    protected field transformationStateBinding type: ShaderResourceBindingsPtr.

    protected field vertexBuffer type: BufferPtr.
    protected field vertexLayout type: VertexLayoutPtr.
    protected field vertexBinding type: VertexBindingPtr.

    protected field indexBuffer type: BufferPtr.

    protected field texturingBindingDictionary type: Stdn Collections Dictionary(TextureViewPtr, ShaderResourceBindingsPtr).
    protected field texturingBindings type: Stdn Collections Vector(ShaderResourceBindingsPtr).
    protected field nextAvailableTexturingBindings type: UIntPointer.

    private field currentActiveClipSubmesh type: Int32.
    private field lastActiveTextureView type: TextureViewPtr.

    override method renderScene: (rootNode: Scene2D NodePtr const ref) ::=> Void := {
        rootNode ifNil: { return: void }.

        super renderScene: rootNode.

        stateTracker ifNil: { return: void }.

        self submitVertices && self submitIndices && self submitMatrices ifFalse: {
            return: void
        }.

        stateTracker _
            useShaderSignature: shaderSignature;
            useGraphicShaderBindings: samplingStateBinding;
            useGraphicShaderBindings: transformationStateBinding;
            vertexShader: SimpleShader vertex shaderEntryPointInfo address;
            fragmentShader: SimpleShader coloredFragment shaderEntryPointInfo address;
            useVertexBinding: vertexBinding;
            useIndexBuffer: indexBuffer.

        nextAvailableTexturingBindings := 0.
        lastActiveTextureView := TextureViewPtr nil.
        currentActiveClipSubmesh := -1.
        texturingBindingDictionary removeAll.
        meshBuilder submeshes do: {:each :: Void |
            self renderSubmesh: each.
        }.
    }.

    method activateClipSubmesh: (clipSubmeshIndex: Int32) ::=> Void := {
        ## TODO: Implement support for polygon based clipping by using the stencil buffer.

        let newClippingBounds mutable := RectangleF32 extent: (extent castTo: Float32x2).
        let currentClipSubmesh mutable := clipSubmeshIndex.

        ## TODO: Avoid this loop by keeping a stack of active clipping bounds.
        while: currentClipSubmesh >= 0 do: {
            let submesh ref := meshBuilder submeshes[currentClipSubmesh].
            newClippingBounds := newClippingBounds intersectionWith: submesh boundingBox.
            currentClipSubmesh := submesh clipSubmesh
        }.

        newClippingBounds isEmpty ifTrue: {
            stateTracker _ setScissor: RectangleI32().
        } ifFalse: {
            stateTracker _ setScissor: newClippingBounds asRectangleI32
        }.

        currentActiveClipSubmesh := clipSubmeshIndex.
    }.

    method renderSubmesh: (submesh: Submesh ref) ::=> Void := {
        submesh clipSubmeshType = ClipSubmeshType None ifFalse: {return: void}.
        submesh clipSubmesh = currentActiveClipSubmesh ifFalse: {
            self activateClipSubmesh: submesh clipSubmesh
        }.

        submesh fontFaceWithSize isNotNil && submesh textureView isNil ifTrue: {
            let (let texture, let textureView) := (submesh fontFaceWithSize getPointer castTo: RenderingDeviceFontFaceWithSize pointer) _
                validRenderingTextureAndView.
            submesh
                texture: texture;
                textureView: textureView.
        }.

        submesh textureView ~= lastActiveTextureView ifTrue: {
            submesh textureView ifNil: {
                stateTracker _ fragmentShader: SimpleShader coloredFragment shaderEntryPointInfo address.
            } ifNotNil: {
                let binding := self getOrCreateTextureBindingFor: submesh textureView.
                binding ifNil: {
                    stateTracker _ fragmentShader: SimpleShader coloredFragment shaderEntryPointInfo address.
                } ifNotNil: {
                    submesh fontFaceWithSize ifNil: {
                        stateTracker _ fragmentShader: SimpleShader texturedFragment shaderEntryPointInfo address.
                    } ifNotNil: {
                        stateTracker _ fragmentShader: SimpleShader textFragment shaderEntryPointInfo address.
                    }.
                    stateTracker _ useGraphicShaderBindings: binding
                }
            }.

            lastActiveTextureView := submesh textureView
        }.

        stateTracker _
            blendingEquation: submesh blendingMode asBlendingEquation;
            primitiveTopology: submesh primitiveTopology;
            drawElements: submesh indexCount instanceCount: 1 firstIndex: submesh firstIndex baseVertex: 0baseInstance: 0.
    }.

    method submitVertices => Boolean8 := {
        let requiredCapacity := meshBuilder vertices size * MeshVertex instanceSize.
        vertexBuffer isNil || requiredCapacity > vertexBuffer _ size ifTrue: {
            self createVertexBufferWithCapacity: requiredCapacity.
            vertexBuffer ifNil: {
                return: false.
            }
        }.

        requiredCapacity > 0 ifTrue: {
            vertexBuffer _ uploadAt: 0 size: requiredCapacity data: meshBuilder vertices data
        }.

        true
    }.

    method createVertexBufferWithCapacity: (requiredCapacity: UIntPointer) ::=> Void := {
        let newCapacity := (requiredCapacity asLargerPowerOfTwo max: 16) * MeshVertex instanceSize.
        vertexBuffer := device _ createBuffer: (BufferDescription()
            size: newCapacity;
            heapType: MemoryHeapType HostToDevice;
            usageModes: BufferUsageMode Array;
            mainUsageMode: BufferUsageMode Array;
            mappingFlags: BufferMappingFlags DynamicStorage;
            stride: MeshVertex instanceSize;
            yourself
        ) initialData: nil.

        vertexLayout ifNil: {
            let layoutDescription := VertexLayoutDescription()
                addAttribute: (VertexAttributeDescription()
                    location: 0;
                    format: PixelFormat R32G32_Float;
                    offset: 0;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: 2;
                    format: PixelFormat R32G32_Float;
                    offset: 8;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: 1;
                    format: PixelFormat R32G32B32A32_Float;
                    offset: 16;
                    yourself);
                addBufferWithStride: MeshVertex instanceSize;
                yourself.

            vertexLayout := device _ getDefaultVertexLayoutCache _ getOrCreateWithDescription: layoutDescription.
            vertexBinding := device _ createVertexBindingFor: vertexLayout.
        }.

        let vertexBuffers mutable := BufferPtr array (vertexBuffer).
        vertexBinding _ bindVertexBuffers: vertexBuffers.
    }.

    method submitIndices => Boolean8 := {
        let requiredCapacity := meshBuilder indices size * 2.
        indexBuffer isNil || requiredCapacity > indexBuffer _ size ifTrue: {
            self createIndexBufferWithCapacity: requiredCapacity.
            indexBuffer ifNil: {
                return: false.
            }
        }.

        requiredCapacity > 0 ifTrue: {
            indexBuffer _ uploadAt: 0 size: requiredCapacity data: meshBuilder indices data
        }.

        true
    }.

    method createIndexBufferWithCapacity: (requiredCapacity: UIntPointer) ::=> Void := {
        let newCapacity := (requiredCapacity asLargerPowerOfTwo max: 16) * 2.
        indexBuffer := device _ createBuffer: (BufferDescription()
            size: newCapacity;
            heapType: MemoryHeapType HostToDevice;
            usageModes: BufferUsageMode ElementArray;
            mainUsageMode: BufferUsageMode ElementArray;
            mappingFlags: BufferMappingFlags DynamicStorage;
            stride: MeshVertex instanceSize;
            yourself
        ) initialData: nil.
    }.

    method submitMatrices => Boolean8 := {
        transformationStateBuffer ifNil: {
            let shaderSignatureDescription := ShaderSignatureDescription()
                ## Sampling state.
                addBank: (ShaderSignatureBankDescription()
                    addElementWithType: ShaderBindingType Sampler;
                    yourself
                );

                ## Transformation state.
                addBank: (ShaderSignatureBankDescription()
                    addElementWithType: ShaderBindingType UniformBuffer;
                    yourself
                );

                ## Texturing state.
                addBank: (ShaderSignatureBankDescription()
                    addElementWithType: ShaderBindingType SampledImage;
                    yourself
                );
                yourself.
            shaderSignature := device _ getDefaultShaderSignatureCache _ getOrCreateWithDescription: shaderSignatureDescription.

            sampler := device _ createSampler: (SamplerDescription()
                filter: TextureFilter MinLinear | TextureFilter MagLinear | TextureFilter MipmapLinear;
                addressU: TextureAddressMode Wrap;
                addressV: TextureAddressMode Wrap;
                addressW: TextureAddressMode Wrap;
                maxLod: 10000.0f;
                yourself).

            samplingStateBinding := shaderSignature _ createShaderResourceBindings: 0.
            samplingStateBinding _ bindOn: 0 sampler: sampler.

            transformationStateBuffer := device _ createBuffer: (BufferDescription()
                size: 256;
                heapType: MemoryHeapType HostToDevice;
                usageModes: BufferUsageMode Uniform;
                mainUsageMode: BufferUsageMode Uniform;
                mappingFlags: BufferMappingFlags DynamicStorage;
                stride: 0;
                yourself
            ) initialData: nil.

            transformationStateBinding := shaderSignature _ createShaderResourceBindings: 1.
            transformationStateBinding _ bindOn: 0 uniformBuffer: transformationStateBuffer.
        }.

        {
            let transformationState mutable := TransformationState()
                projectionMatrix: (Float32x4x4 reverseDepthOrthoLeft: 0 right: extent x
                    bottom: extent y top: 0
                    near: -100.0f far: 100.0f
                    invertedY: device _ shouldInvertProjectionY);
                yourself.
            transformationStateBuffer _ uploadAt: 0 size: TransformationState instanceSize data: transformationState address
        }.

        true.
    }.

    method getOrCreateTextureBindingFor: (textureView: TextureViewPtr const ref) ::=> ShaderResourceBindingsPtr := {
        texturingBindingDictionary at: textureView ifAbsentPut: { :: ShaderResourceBindingsPtr |
            nextAvailableTexturingBindings >= texturingBindings size ifTrue: {
                texturingBindings add: (shaderSignature _ createShaderResourceBindings: 2)
            }.

            let result := texturingBindings[nextAvailableTexturingBindings].
            result _ bindOn: 0 sampledTextureView: textureView.
            nextAvailableTexturingBindings := nextAvailableTexturingBindings + 1.
            result
        }
    }.

}.

compileTime constant StateTracker2DSceneRendererPtr := StateTracker2DSceneRenderer sharedPointer.

}. ## End of namespace Core
}. ## End of namespace Graphics
}. ## End of namespace Stdn
