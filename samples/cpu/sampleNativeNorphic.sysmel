Compiler
    importModuleNamed: #"Norphic.Core";
	importModuleNamed: #"Sysmel.Graphics.Headfull".

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics GUI.
useNamespace: Stdn Math Geometry.
useNamespace: Norphic.

Stdn MainEntryPoint: {:args :: Int32 |
    let windowSystem mutable := createDefaultWindowSystem().
    windowSystem ifNil: {
        return: 0
    }.
    windowSystem _ targetFrameRate: 20.

    ## Create the main window.
    let mainWindow := SystemWindowNorph createFor: windowSystem title: "Test Norphic" asMutableString extent: Int32x2(640, 480).
    mainWindow ifNil: {
        return: 0.
    }.

    let menuBar := MenuBarNorph sharedNew.
    let statusBar := StatusBarNorph sharedNew.
    let rectangleWithBorder := BorderedNorph sharedNew.
    let list := ListViewNorph sharedNew.
    let label := LabelNorph sharedNew.
    let button := SimpleButtonNorph sharedNew.
    let textInput := TextInputNorph sharedNew.
    let textArea := TextAreaNorph sharedNew.

    ## Menu bar
    {
        menuBar _
            addMenu: "File" asMutableString with: {:menu :: Void |
                menu
                    addItem: "New" asMutableString doing: { :(NorphEvent ref) event :: Void |
                        Stdn stdout << "TODO: File new"; nl.
                    };
                    addItem: "Open" asMutableString doing: { :(NorphEvent ref) event :: Void |
                        Stdn stdout << "TODO: File open"; nl.
                    };
                    addSeparator;
                    addItem: "Exit" asMutableString doing: { :(NorphEvent ref) event :: Void |
                        mainWindow _ destroy
                    }
            };
            addMenu: "Help" asMutableString with: {:menu :: Void |
                menu addItem: "About" asMutableString doing: { :(NorphEvent ref) event :: Void |
                    Stdn stdout << "TODO: Help about"; nl.
                }
            }
    }.

    ## Rectangle with border.
    {
        rectangleWithBorder _
            color: Float32x4(1.0f, 0.0f, 0.0f, 1.0f);
            borderColor: Float32x4(0.0f, 0.0f, 0.0f, 1.0f).
    }.

    ## Sample label
    {
        label _
            text: "Hello World" asMutableString.
    }.

    ## Sample button
    {
        button _
            text: "Button" asMutableString.

        button _ when: NorphActivatedEvent do: { :(NorphEvent ref) event :: Void |
            list _ addItem: "Extra item" asMutableString.
        }.
    }.

    ## List view
    {
        list _
            addItem: "First" asMutableString;
            addItem: "Second" asMutableString;
            addItem: "Third" asMutableString.
    }.

    ## Layout
    mainWindow _
        buildLayoutWith: {:b :: Void |
            b verticalBoxWith: {:b :: Void |
                b
                    element: menuBar with: {:c :: Void | c expanded};
                    horizontalPanedWith: {:b :: Void |
                        b
                            verticalPanedWith: {:b :: Void |
                                b
                                    element: rectangleWithBorder;
                                    element: label;
                                    element: button;
                                    element: list
                            };
                            verticalPanedWith: {:b :: Void |
                                b
                                    element: textInput;
                                    element: textArea
                            }
                    } constraints: {:c :: Void | c expanded; proportion: 1.0f};
                    element: statusBar with: {:c :: Void | c expanded}
            }.
        };
        autolayout: true;
        updateLayout.

    ## Run the actual main loop.
    return: windowSystem _ runMainLoop.
}.
