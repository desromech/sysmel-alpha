class ClientProcessor definition: {
    field clientSocket type: NStd IO Socket.
    field clientThread type: NStd Thread.

    method startProcessingNewClient: (theClientSocket: NStd IO Socket ref) ::=> Void := {
        clientSocket := theClientSocket asMoveReference.
        clientThread start: { :: Void |
            self echoLoop
        }
    }.

    method echoLoop => Void := {
        NStd stdout << "Accepted client with endpoint " << clientSocket endpoint; nl.
        let buffer mutable type: (UInt8 array: 2048).

        while: clientSocket isValid do: {
            let readCount := clientSocket read: buffer[0] address size: 2048.
            readCount > 0 ifTrue: {
                clientSocket write: buffer[0] address size: (readCount castTo: UIntPointer)
            } ifFalse: {
                clientSocket close.
            }
        }.

        NStd stdout << "Client with endpoint has " << clientSocket endpoint << " has closed its connection."; nl.
        self address nativeDelete.
    }.
}.

function main externC (argc: Int32, argv: Char8 const pointer pointer) => Int32 := {
    let port mutable type: UInt16 := 1234.
    argc > 1 ifTrue: {
        port := LibC atoi(argv[1]) castTo: UInt16
    }.

    let endpoint := NStd IO Endpoint ipv4SourcePort: port.
    let socket mutable := NStd IO Socket tcpIPv4.
    socket isValid ifFalse: {
        NStd stdout << "Failed to create the socket."; nl.
        return: 1.
    }.

    socket setOption: NStd IO SocketOption ReuseAddress value: 1.

    (socket bind: endpoint) ifFalse: {
        NStd stdout << "Failed to bind listening port " << port; nl.
        return: 1.
    }.

    ## Start listening on the socket.
    socket listen: 10.

    ## Main loop.
    while: socket isValid do: {
        let clientSocket mutable := socket accept.
        clientSocket isValid ifTrue: {
            let processor := ClientProcessor nativeNew.
            processor _ startProcessingNewClient: clientSocket.
        }
    }.

    socket close.

    return: 0.
}.
